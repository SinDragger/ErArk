;-------------------------------------------------
;裤など、腰边りから足首までを覆うも之を穿かせる関数。连裤袜なども含む
;指定番号によって名前がCSTR:45に代入される。
;99番までは、そ之番号之梦魇之内衣。
;EQUIP:外套は裤之特征标志。ビット管理する
;-------------------------------------------------
@SET_OUTER, ARG, ARG:1
;ARG之裤をARG:1番之も之にする。ARG:1==0なら裤を脱がす
;汚れ＆ずれたりした标志等を初期化して新品にする
STAIN:ARG:外套 = 0
TCVAR:ARG:26 = 0

IF ARG:1 == 0 || COND("脚之使用", ARG) == 0
	IF CHECK_OUTER("つなぎ", ARG)
		EQUIP:ARG:上着 = 0
		CSTR:ARG:42 = 
	ENDIF
	EQUIP:ARG:外套 = 0
	CSTR:ARG:45 = 
	RETURN 0
ENDIF

;名前記录
CSTR:ARG:45 = %PALAM_OUTER(ARG:1, ARG, 1)%
IF CHECK_OUTER("つなぎ", ARG) == 0 && CHECK_SHIRT("つなぎ", ARG)
	EQUIP:ARG:上着 = 0
	CSTR:ARG:42 = 
ENDIF

SIF EQUIP:ARG:外套 && !CHECK_OUTER("连裤袜", ARG)
	CALL SET_BODYS, ARG
IF EQUIP:ARG:外套 && EQUIP:ARG:胖次
	SIF CHECK_OUTER("紧勒", ARG) && !CHECK_PANTIES("紧勒", ARG) && !CHECK_PANTIES("丁字裤", ARG) && !CHECK_PANTIES("Ｖ后入", ARG)
		CALL SET_PANTIES, ARG
	SIF CHECK_OUTER("拉链", ARG) || CHECK_OUTER("Ｖ露出", ARG) || CHECK_OUTER("Ａ露出", ARG)
		CALL SET_PANTIES, ARG
ENDIF
SIF EQUIP:ARG:外套 && CHECK_OUTER("连裤袜", ARG)
	CALL SET_SOCKS, ARG

TCVAR:ARG:26 = 0
RETURN ARG:1
;-------------------------------------------------
;ARG之穿いている裤之名前。ARG:1==0なら正式名称 by CSTR。ARG:1==1なら短缩名称 by EQUIP。
;-------------------------------------------------
@OUTER(ARG, ARG:1)
#FUNCTIONS
IF ARG:1 == 0
	LOCALS = %CSTR:ARG:45%
	SIF CONFIG("衣装之细かい色表示之禁止")
		RETURNF LOCALS
	LOCALS:1 = %TEXTS("衣装之色", EQUIP:ARG:外套)%
	SIF LOCALS:1 != "" && (SUBSTRING(CSTR:ARG:45, 0, 2) != SUBSTRING(LOCALS:1, 0, 2) )
		LOCALS = %LOCALS%(%LOCALS:1%)
ELSE
	IF CHECK_OUTER("运动短裤", ARG)
		LOCALS = 运动短裤
	ELSEIF CHECK_OUTER("热裤", ARG)
		LOCALS = 热裤
	ELSEIF CHECK_OUTER("打底裤", ARG)
		LOCALS = 打底裤
	ELSEIF CHECK_OUTER("连裤袜", ARG)
		LOCALS = 连裤袜
	ELSEIF CHECK_OUTER("紧身裤", ARG)
		LOCALS = 紧身裤
	ELSEIF CHECK_OUTER("睡衣", ARG)
		LOCALS = 睡衣
	ELSEIF CHECK_OUTER("混色胖次", ARG)
		LOCALS = 混色胖次
	ELSE
		LOCALS = 裤
	ENDIF
ENDIF
RETURNF LOCALS

;-------------------------------------------------
;ARG番之裤之正式名称
;ARG:1 > 0ならARG:1は角色编号として働く
;-------------------------------------------------
@NAME_OUTER(ARG, ARG:1)
#FUNCTIONS

LOCALS = %PALAM_OUTER(ARG, ARG:1)%
RETURNF LOCALS

;-------------------------------------------------
;ARG番之裤之详细パラ
;ARG:1 > 0ならARG:1は角色编号として働き、ARG:1 > 0 && ARG:2 > 0ならEQUIPを定める
;-------------------------------------------------
@PALAM_OUTER(ARG, ARG:1, ARG:2)
#FUNCTIONS
VARSET LOCAL
VARSET LOCALS

IF ARG:1 > 0 && COND("脚之使用", ARG:1) == 0
	SIF ARG:2 > 0
		EQUIP:(ARG:1):外套 = LOCAL
	RETURNF LOCALS
ENDIF

SELECTCASE ARG
CASE 17
	IF CFLAG:(ARG:1):8 != 1
		LOCALS = 热裤
		LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "热裤", "皮质", "通气性无", "黒")
	ENDIF
CASE 23
	IF CFLAG:(ARG:1):8 == 2
		LOCALS = 低腰胖次
		LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "裤", "低腰", "橙")
	ELSE
		LOCALS = 低腰胖次
		LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "裤", "低腰", "薄緑")
	ENDIF
CASE 34
	LOCALS = 热裤
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "热裤", "薄緑")
CASE 42
	IF CFLAG:(ARG:1):209 == 2
		LOCALS = 热裤
		LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "热裤", "黒")
	ENDIF
CASE 58
	LOCALS = 低腰胖次
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "裤", "低腰", "橙")
CASE 100
	LOCALS = 运动短裤
	LOCAL = SETFLAG_OUTER("运动短裤", "运动短裤カラー")
CASE 101
	LOCALS = 高叉运动短裤
	LOCAL = SETFLAG_OUTER("高叉", "Ｖ后入", "运动短裤", "运动短裤カラー")
CASE 102
	LOCALS = 打底裤
	LOCAL = SETFLAG_OUTER("ずれ不可", "打底裤")
CASE 103
	LOCALS = 热裤
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "热裤")
CASE 104
	LOCALS = 连裤袜
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "连裤袜")
CASE 105
	LOCALS = 连裤网袜
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "连裤袜", "蕾丝")
CASE 106
	LOCALS = 开洞连裤袜
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "连裤袜", "ＶＡ露出")
CASE 107
	LOCALS = 紧身裤
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "紧身裤")
CASE 108
	; IF ITEM:睡衣 && TALENT:小只
	; 	LOCALS = 睡裤
	; 	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "睡衣", "裤")
	; ENDIF
CASE 109
	LOCALS = 混色胖次
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "混色胖次")

CASE 120
	LOCALS = 运动短裤
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "运动短裤", "ＶＡ露出", "运动短裤カラー")
CASE 125
	LOCALS = 打底裤
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "打底裤", "ＶＡ露出")
CASE 130
	LOCALS = 热裤
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "热裤", "Ａ露出")
CASE 135
	LOCALS = 热裤
	LOCAL = SETFLAG_OUTER("ずれ不可", "コキ不可", "热裤", "缝隙")
ENDSELECT

SIF ARG:1 > 0 && ARG:2 > 0
	EQUIP:(ARG:1):外套 = LOCAL

RETURNF LOCALS

;-------------------------------------------------
;指示されたとおり之裤之标志を設定するために使う
;-------------------------------------------------
@SETFLAG_OUTER(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9, ARGS:10)
#FUNCTION
#DIM LCOUNT

LOCAL = 0
FOR LCOUNT, 1, 11
	ARGS = %ARGS:LCOUNT%
	SELECTCASE ARGS
	CASE "裤"
		SETBIT LOCAL, 0
		LOCAL |= SETFLAG_COMMON("ずれ不可")
		LOCAL |= SETFLAG_COMMON("コキ不可")
	CASE "打底裤"
		SETBIT LOCAL, 1
		LOCAL |= SETFLAG_COMMON("ずれ不可")
	CASE "热裤"
		SETBIT LOCAL, 2
		SETBIT LOCAL, 0
		LOCAL |= SETFLAG_COMMON("ずれ不可")
		LOCAL |= SETFLAG_COMMON("コキ不可")
	CASE "运动短裤"
		SETBIT LOCAL, 3
	CASE "高叉"
		SETBIT LOCAL, 4
	CASE "丁字裤", "Tフロント"
		SETBIT LOCAL, 5
	CASE "Ｖ后入", "V后入"
		SETBIT LOCAL, 6
	CASE "丁字裤", "T后入"
		SETBIT LOCAL, 7
	CASE "低腰", "浅履き"
		SETBIT LOCAL, 8
	CASE "连裤袜"
		SETBIT LOCAL, 9
		LOCAL |= SETFLAG_COMMON("透视")
	CASE "紧身裤"
		SETBIT LOCAL, 10
	CASE "睡衣"
		SETBIT LOCAL, 11
	CASE "混色胖次"
		SETBIT LOCAL, 12

	CASE "运动短裤カラー"
		IF RAND:2
			LOCAL |= SETFLAG_COMMON("赤")
		ELSE
			LOCAL |= SETFLAG_COMMON("紺")
		ENDIF
	CASEELSE
		SIF STRLENS(ARGS)
			LOCAL |= SETFLAG_COMMON(ARGS)
	ENDSELECT
	SIF STRLENS(ARGS:(LCOUNT + 1) ) == 0
		RETURNF LOCAL
NEXT
RETURNF LOCAL

;-------------------------------------------------
;ARG之裤が条件ARGSを满たすか确认する
;-------------------------------------------------
@CHECK_OUTER(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:外套 == 0
	RETURNF 0
SELECTCASE ARGS
;ずれ不可でなく、ずらしていない
CASE "ずらし可", "能撥开"
	SIF SETFLAG_OUTER("Ｖ露出")&EQUIP:ARG:外套
		RETURNF 0
	IF SETFLAG_OUTER("拉链")&EQUIP:ARG:外套
		SIF SETFLAG_OUTER("拉链开")&EQUIP:ARG:外套
			RETURNF 0
		SIF CHECK_OUTER("拉链閉")
			RETURNF 1
	ENDIF
	SIF SETFLAG_OUTER("ずらした")&EQUIP:ARG:外套
		RETURNF 0
	SIF SETFLAG_OUTER("ずれ不可")&EQUIP:ARG:外套
		RETURNF 0
	RETURNF 1
;连裤袜で、破いていない
CASE "破ける"
	SIF SETFLAG_OUTER("可撕")&EQUIP:ARG:外套
		RETURNF 0
	SIF SETFLAG_OUTER("连裤袜")&EQUIP:ARG:外套
		RETURNF 1
;拉链があり、あけていない
CASE "拉链閉"
	SIF SETFLAG_OUTER("拉链开")&EQUIP:ARG:外套
		RETURNF 0
	SIF SETFLAG_OUTER("拉链")&EQUIP:ARG:外套
		RETURNF 1
CASE "露Ｖ胖次"
	SIF SETFLAG_OUTER("拉链开", "ずらした", "可撕", "Ｖ露出", "缝隙")&EQUIP:ARG:外套
		RETURNF 0
	RETURNF 1
CASE "露Ａ胖次"
	SIF SETFLAG_OUTER("拉链开", "ずらした", "可撕", "Ａ露出", "缝隙")&EQUIP:ARG:外套
		RETURNF 0
	RETURNF 1
CASE "紧勒"
	SIF SETFLAG_OUTER("ずらした", "拉链开", "Ｖ露出", "缝隙", "可撕")&EQUIP:ARG:外套
		RETURNF 0
	SIF SETFLAG_OUTER("高叉", "丁字裤")&EQUIP:ARG:外套
		RETURNF 1
CASE "潮湿"
	SIF SETFLAG_OUTER("ずらした", "拉链开", "Ｖ露出", "缝隙", "可撕")&EQUIP:ARG:外套
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:外套, "潮湿")
		RETURNF 1
CASE "润湿"
	SIF SETFLAG_OUTER("ずらした", "拉链开", "Ｖ露出", "缝隙", "可撕", "通气性无")&EQUIP:ARG:外套
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:外套, "濡れ")
		RETURNF 1
CASEELSE
	SIF (SETFLAG_OUTER(ARGS)&EQUIP:ARG:外套) == SETFLAG_OUTER(ARGS)
		RETURNF 1
ENDSELECT
RETURNF 0
