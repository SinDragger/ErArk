;USERCOMから呼び出す
@SHOW_WILLGET_JUEL
#DIM LCOUNT
#DIM MEMO_LINECOUNT

PRINTL 现在之宝珠取得予定数
PRINTL 
VARSET LOCAL
FOR LCOUNT, 0, 14
	;参数を表示する顺番を普べ替える・MBCVA欲恭屈恥痛恐习反不鬱・V润A润は无
	SELECTCASE LCOUNT
	CASE 0
		LOCAL = 0
	CASE 1
		LOCAL = 1
	CASE 2
		LOCAL = 2
	CASE 3
		LOCAL = 14
	CASE 4
		LOCAL = 15
	CASE 5
		LOCAL = 4
	CASE 6
		LOCAL = 5
	CASE 7
		LOCAL = 6
	CASE 8
		LOCAL = 7
	CASE 9
		LOCAL = 8
	CASE 10
		LOCAL = 9
	CASE 11
		LOCAL = 10
	CASE 12
		LOCAL = 13
	CASE 13
		LOCAL = 11
	ENDSELECT

	;参数名・桁数を揃えつつ参数を表示
	PRINTFORM %PALAMNAME:LOCAL% {PALAM:LOCAL, 10} 
	;桁数を揃えつつ现在＋予定＝合计を表示
	SELECTCASE LOCAL
	CASE 3, 12, 13
	CASEELSE
		PRINTFORM %PALAMNAME:LOCAL%之珠×({JUEL:LOCAL, 10} + 

		SIF GET_JUEL(LOCAL)
			SETCOLOR DEF_COLOR("黄色")
		PRINTFORM {GET_JUEL(LOCAL), 10}
		RESETCOLOR

		PRINTFORM ) ={JUEL:LOCAL + GET_JUEL(LOCAL), 10}
	ENDSELECT

	;快CVABMは绝顶回数を表示
	IF LOCAL == 0 && EX:C绝顶 + EX:尿道绝顶 >= 1
		PRINTFORM  < \@ EX:C绝顶 ? C绝顶:{EX:C绝顶, 2}回%BL(1)% # \@\@ EX:尿道绝顶 ? 尿道绝顶:{EX:尿道绝顶, 2}回%BL(1)% # \@>
	ELSEIF LOCAL == 1 && EX:V绝顶 >= 1
		PRINTFORM  < V绝顶:{EX:V绝顶, 2}回 >
	ELSEIF LOCAL == 2 && EX:A绝顶 >= 1
		PRINTFORM  < A绝顶:{EX:A绝顶, 2}回 >
	ELSEIF LOCAL == 14 && EX:B绝顶 >= 1
		PRINTFORM  < B绝顶:{EX:B绝顶, 2}回 >
	ELSEIF LOCAL == 15 && EX:M绝顶 >= 1
		PRINTFORM  < M绝顶:{EX:M绝顶, 2}回 >
	ENDIF
	PRINTL 
NEXT

PRINTL 

MEMO_LINECOUNT = LINECOUNT
;ABLUPも表示
CALL PRINT_SHOW_ABLUP_SELECT, "予定"
SIF LINECOUNT > MEMO_LINECOUNT
	PRINTL 

PRINTL 反感之珠は欲情・恭顺・屈服之珠を优先的に相殺します。
PRINTL そ之后に、恥情・苦痛・恐怖之珠を相殺します。
IF EXP_GAIN(58)
	SETCOLOR DEF_COLOR("黄色")
	PRINTL ※媚药之使用により、獲得する珠之量が减少しています※
	RESETCOLOR
ENDIF
WAIT


@GET_JUEL(ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET

SELECTCASE ARG
;V润、A润、抑郁、Ｐ润之珠は存在しない
CASE 3, 12, 13, 16
	RETURNF 0
;反感・抑郁を合计して反感之珠に换算
CASE 11
	LOCAL = CALC_JUEL(11, ARG:1) + CALC_JUEL(13, ARG:1)
CASEELSE
	LOCAL = CALC_JUEL(ARG, ARG:1)
ENDSELECT
RETURNF LOCAL

;JUEL:ARGをいくつ得るか计算する
@CALC_JUEL(ARG, ARG:1)
#FUNCTION

SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET

;参数レベルによる取得数
SELECTCASE PALAM:(ARG:1):ARG
;LV 0
CASE IS < 100
	LOCAL = 0
;LV1
CASE IS < 300
	LOCAL = 1
CASE IS < 500
	LOCAL = 2
;LV2
CASE IS < 1500
	LOCAL = 10
CASE IS < 3000
	LOCAL = 20
;LV3
CASE IS < 6000
	LOCAL = 100
CASE IS < 10000
	LOCAL = 200
;LV4
CASE IS < 30000
	LOCAL = 1000
;LV5
CASE IS < 60000
	LOCAL = 2000
;LV6
CASE IS < 100000
	LOCAL = 3000
;LV7
CASE IS < 150000
	LOCAL = 5000
;LV8
CASE IS < 250000
	LOCAL = 8000
;LV9
CASE IS < 500000
	LOCAL = 12000
;LV10
CASE IS < 1000000
	LOCAL = 16000
;LV11
CASE IS < 2500000
	LOCAL = 20000
;LV12
CASE IS < 5000000
	LOCAL = 25000
;LV13
CASE IS < 10000000
	LOCAL = 30000
;LV14
CASE IS < 50000000
	LOCAL = 35000
;LV15
CASE IS < 100000000
	LOCAL = 40000
CASEELSE
	LOCAL = 50000
ENDSELECT

;快CVABM绝顶BＯＮＵS
SELECTCASE ARG
CASE 0
	LOCAL += EX:(ARG:1):C绝顶*1000 + EX:(ARG:1):尿道绝顶*1000 + JUEL:(ARG:1):20
CASE 1
	LOCAL += EX:(ARG:1):V绝顶*1000 + JUEL:(ARG:1):21
CASE 2
	LOCAL += EX:(ARG:1):A绝顶*1000 + JUEL:(ARG:1):22
CASE 14
	LOCAL += EX:(ARG:1):B绝顶*1000 + JUEL:(ARG:1):23
CASE 15
	LOCAL += EX:(ARG:1):M绝顶*1000 + JUEL:(ARG:1):24
ENDSELECT

;媚药によるペナルティ
SIF EXP_GAIN(58, ARG:1)
	LOCAL /= 1 + EXP_GAIN(58, ARG:1)

RETURNF LOCAL

