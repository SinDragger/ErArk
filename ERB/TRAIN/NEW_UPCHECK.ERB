;-------------------------------------------------
;调教指令实行前之参数之記录
;TCVAR:100～119を"调教指令实行前之重要参数之記录"用として确保してある
;-------------------------------------------------
@SAVE_PALAM_BEFORECOM
#DIM LCOUNT
FOR LCOUNT, 1, CHARANUM
	;射精or绝顶槽之开始值＆増加量
	TCVAR:LCOUNT:100 = BASE:LCOUNT:绝顶
	TCVAR:LCOUNT:101 = 0
	;母乳槽之开始值＆増加量
	TCVAR:LCOUNT:102 = BASE:LCOUNT:喷乳
	TCVAR:LCOUNT:103 = 0
	;触手射精槽之开始值＆増加量
	TCVAR:LCOUNT:104 = BASE:LCOUNT:触手
	TCVAR:LCOUNT:105 = 0
	;龙射精槽之开始值＆増加量
	TCVAR:LCOUNT:106 = BASE:LCOUNT:龙
	TCVAR:LCOUNT:107 = 0
	;精力之开始值＆减少量
	TCVAR:LCOUNT:108 = BASE:LCOUNT:精力
	TCVAR:LCOUNT:109 = 0
	;受精确率
	TCVAR:LCOUNT:110 = COND("受精确率", LCOUNT)
NEXT


;-------------------------------------------------
;PALAM之増减を表示させる（P润は表示しない）
;对应する珠之増加分も表示するように变更
;ARGにはTARGETかASSIしか入らない
;-------------------------------------------------
@NEW_UPCHECK_PALAM, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;こ之回合で之増减を行う前之PALAM之值之記录に使う
#DIM PALAM_PRE, 16
#DIM PALAM_NUM
;ARGが助手か？
#DIM IS_ASSI

;Ｗ逆推之时には名前を表示させる
SIF TEQUIP:Ｗ逆推 && SELECTCOM:1 >= 0
	PRINTFORML 　＜%CALLNAME:ARG%＞

SELECTCASE ARG
CASE ASSI
	IS_ASSI = 1
	SWAP TARGET, ASSI
	SWAP SELECTCOM, SELECTCOM:1
	SWAP PREVCOM, PREVCOM:1

	CALL CALC_SOURCE
CASEELSE
	IS_ASSI = 0
ENDSELECT

PRINTL 

MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 16
	PALAM_PRE:LCOUNT = PALAM:LCOUNT
	PALAM:LCOUNT = MIN(PALAM_LV(16), PALAM:LCOUNT + CUP:LCOUNT - CDOWN:LCOUNT)
NEXT

FOR LCOUNT, 0, 16
	SELECTCASE LCOUNT
	CASE 0 TO 2
		PALAM_NUM = LCOUNT
	CASE 3
		PALAM_NUM = 14
	CASE 4
		PALAM_NUM = 15
	CASEELSE
		PALAM_NUM = LCOUNT - 2
	ENDSELECT

	SIF CUP:PALAM_NUM == 0 && CDOWN:PALAM_NUM == 0
		CONTINUE

	PRINTFORM 　%TEXT_LJ(PALAMNAME:PALAM_NUM, 8)% = {PALAM_PRE:PALAM_NUM, 8} + 

	SELECTCASE GET_PALAMLV(CUP:PALAM_NUM)
	CASE 3
		SETCOLOR DEF_COLOR("黄色")
	CASE IS >= 4
		SETCOLOR DEF_COLOR("粉红色")
	ENDSELECT
	PRINTFORM {CUP:PALAM_NUM, 7}
	RESETCOLOR

	PRINTFORM  - {CDOWN:PALAM_NUM, 7} = {PALAM:PALAM_NUM, 8}　

	IF GET_JUEL(PALAM_NUM)
		PRINTFORM %PALAMNAME:PALAM_NUM%之珠＋
		SETCOLOR DEF_COLOR("黄色")
		PRINTFORM {GET_JUEL(PALAM_NUM), 5}
		RESETCOLOR
	ENDIF
	PRINTL 
NEXT

;变动したPALAMを表示すた场合には改行
SIF LINECOUNT != MEMO_LINECOUNT
	PRINTL 

IF IS_ASSI
	SWAP TARGET, ASSI
	SWAP SELECTCOM, SELECTCOM:1
	SWAP PREVCOM, PREVCOM:1
ENDIF

;-------------------------------------------------
;表示を整形したUPCHECK
;-------------------------------------------------
@NEW_UPCHECK
#DIM MEMO_LINECOUNT
#DIM LCOUNT
#DIM BASE_PRE, 10
#DIM BASE_AFT, 10
#DIM UP_BASE, 10
#DIM DOWN_BASE, 10
#DIM BASE_MAX, 10
#DIMS NAME_BASE, 10

;改行をするかどうかに用いる
MEMO_LINECOUNT = LINECOUNT

CALL NEW_UPCHECK_PALAM, TARGET

SIF TEQUIP:Ｗ逆推 && SELECTCOM:1 >= 0
	CALL NEW_UPCHECK_PALAM, ASSI

SIF LINECOUNT == MEMO_LINECOUNT
	PRINTL 

;PLAYER之阴茎之P润之处理
CALL SETFLAG, "P润变动处理", MASTER
IF COND("３P")
	SWAP SELECTCOM, SELECTCOM:2
	CALL SETFLAG, "P润变动处理", ASSI
	SWAP SELECTCOM, SELECTCOM:2
ENDIF

VARSET BASE_PRE
VARSET BASE_AFT
VARSET UP_BASE
VARSET DOWN_BASE
VARSET BASE_MAX
VARSET NAME_BASE

;射精or绝顶
BASE_AFT:0 = BASE:MASTER:绝顶
BASE_PRE:0 = TCVAR:MASTER:100
UP_BASE:0 = MAX(TCVAR:MASTER:101, 0)
DOWN_BASE:0 = BASE_PRE:0 + UP_BASE:0 - BASE_AFT:0
BASE_MAX:0 = MAXBASE:MASTER:绝顶

NAME_BASE:0 = \@ PENIS(MASTER) ? 射精 # 绝顶 \@

;精力
BASE_AFT:1 = BASE:MASTER:精力
BASE_PRE:1 = TCVAR:MASTER:108
DOWN_BASE:1 = TCVAR:MASTER:109
UP_BASE:1 = MAX(BASE_AFT:1 + UP_BASE:1 - BASE_PRE:1, 0)
NAME_BASE:1 = 精力

;射精or绝顶（助手）
IF ASSI
	BASE_AFT:2 = BASE:ASSI:绝顶
	BASE_PRE:2 = TCVAR:ASSI:100
	UP_BASE:2 = MAX(TCVAR:ASSI:101, 0)
	DOWN_BASE:2 = BASE_PRE:2 + UP_BASE:2 - BASE_AFT:2
	BASE_MAX:2 = MAXBASE:ASSI:绝顶
	NAME_BASE:2 = \@ PENIS(ASSI) ? 射精 # 绝顶 \@

	;精力（助手）
	BASE_AFT:3 = BASE:ASSI:精力
	BASE_PRE:3 = TCVAR:ASSI:108
	DOWN_BASE:3 = TCVAR:ASSI:109
	UP_BASE:3 = MAX(BASE_AFT:3 + UP_BASE:3 - BASE_PRE:3, 0)
	NAME_BASE:3 = 精力(助)
ENDIF

;射精（调教对象）
IF PENIS(TARGET)
	BASE_AFT:4 = BASE:绝顶
	BASE_PRE:4 = TCVAR:100
	UP_BASE:4 = MAX(TCVAR:101, 0)
	DOWN_BASE:4 = BASE_PRE:4 + UP_BASE:4 - BASE_AFT:4
	BASE_MAX:4 = MAXBASE:绝顶
	NAME_BASE:4 = 射精(调)
ENDIF

;精力（调教对象）
BASE_AFT:5 = BASE:精力
BASE_PRE:5 = TCVAR:108
DOWN_BASE:5 = TCVAR:109
UP_BASE:5 = MAX(BASE_AFT:5 + UP_BASE:5 - BASE_PRE:5, 0)
NAME_BASE:5 = 精力(调)

;母乳（调教对象）
IF TALENT:泌乳体質
	BASE_AFT:6 = BASE:喷乳
	BASE_PRE:6 = TCVAR:102
	UP_BASE:6 = MAX(TCVAR:103, 0)
	DOWN_BASE:6 = BASE_PRE:6 + UP_BASE:6 - BASE_AFT:6
	BASE_MAX:6 = MAXBASE:喷乳
	NAME_BASE:6 = 母乳(调)
ENDIF
;射精（触手）
IF TEQUIP:触手
	BASE_AFT:7 = BASE:MASTER:触手
	BASE_PRE:7 = TCVAR:MASTER:104
	DOWN_BASE:7 = TFLAG:15*MAXBASE:MASTER:触手
	UP_BASE:7 = MAX(BASE_AFT:7 + UP_BASE:7 - BASE_PRE:7, 0)
	NAME_BASE:7 = 射精(触)
ENDIF
;射精（龙）
IF SELECTCOM == 304 || SELECTCOM == 305
	BASE_AFT:8 = BASE:MASTER:龙
	BASE_PRE:8 = TCVAR:MASTER:106
	DOWN_BASE:8 = TFLAG:9 * MAXBASE:MASTER:龙
	UP_BASE:8 = MAX(BASE_AFT:8 + UP_BASE:8 - BASE_PRE:8, 0)
	NAME_BASE:8 = 射精(ド)
ENDIF

MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 9
	SIF NAME_BASE:LCOUNT == ""
		CONTINUE
	SIF UP_BASE:LCOUNT == 0 && DOWN_BASE:LCOUNT == 0
		CONTINUE

	PRINTFORM 　%TEXT_LJ(NAME_BASE:LCOUNT, 8)% = {BASE_PRE:LCOUNT, 8} + 

	;射精之时などは色を变える
	IF BASE_MAX:LCOUNT
		SELECTCASE UP_BASE:LCOUNT
		CASE IS >= BASE_MAX:LCOUNT*4/5
			SETCOLOR DEF_COLOR("粉红色")
		CASE IS >= BASE_MAX:LCOUNT/4
			SETCOLOR DEF_COLOR("黄色")
		ENDSELECT
	ENDIF

	PRINTFORM {UP_BASE:LCOUNT, 7}

	RESETCOLOR

	PRINTFORML  - {DOWN_BASE:LCOUNT, 7} = {BASE_AFT:LCOUNT, 8}　%NAME_BASE:LCOUNT%
NEXT

SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

CFLAG:490 = 0

IF BASE:PLAYER:精力 <= 0 && ASSIPLAY && STATE("击沈", ASSI)
	PRINTL  
	PRINTFORMW ★调教者%CALLNAME:MASTER%交替了。★
	BASE:ASSI:气力 = 0
	TEQUIP:逆推 = 0
	CALL SETFLAG, "Ｗ逆推解除"
	CALL REMOVE_STATE, ASSI, "恍惚"
	ASSIPLAY = 0
	PLAYER = MASTER
ENDIF
