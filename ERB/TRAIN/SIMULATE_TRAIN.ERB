;------------------------------------------------------------------------
;TARGETとPLAYERによる指令之擬似实行。
;ARGにSELECTCOM、ARG:1に行う予定回数を代入しておく。
;TCVAR:300～599を記录用に用いる。现在は539まで使用中
;------------------------------------------------------------------------
@SIMULATE_TRAIN, ARG, ARG:1
TRYCALLFORM COM_ABLE{ARG}
SIF RESULT == 0
	RETURN 0
;お互い之PALAM、EXP、EX、JUEL、BASEを記录(后で増加分を算出する)
CALL SIMULATE_TRAIN_FIRST
;指令之擬似实行
CALL SIMULATE_TRAIN_COM, ARG, ARG:1
;经验や珠など之獲得量计算
CALL SIMULATE_TRAIN_CALC
;結果发表
CALL SIMULATE_TRAIN_SHOW

;ARGにはPLAYヤー以外之人之人数。例えば３Pする场合にはARGを２にする。１は省略可能
@SIMULATE_TRAIN_FIRST, ARG
#DIM LCOUNT
SIF ARG <= 1
	ARG = 1

PREVCOM = -1

VARSET TARGET, 0, ARG, 9

CALL SIMULATE_TRAIN_FIRST_SET, PLAYER
FOR LCOUNT, 0, 9
	SIF TARGET:LCOUNT > 0
		CALL SIMULATE_TRAIN_FIRST_SET, TARGET:LCOUNT
NEXT


@SIMULATE_TRAIN_FIRST_SET, ARG
#DIM LCOUNT

;TCVAR:300～399はEXP
FOR LCOUNT, 0, 100
	TCVAR:ARG:(LCOUNT + 300) = EXP:ARG:LCOUNT
NEXT
;TCVAR:400～429はJUEL
FOR LCOUNT, 0, 30
	TCVAR:ARG:(LCOUNT + 400) = JUEL:ARG:LCOUNT
NEXT
;TCVAR:430～459はPALAM
FOR LCOUNT, 0, 30
	TCVAR:ARG:(LCOUNT + 430) = PALAM:ARG:LCOUNT
NEXT
;TCVAR:460～519はEX
FOR LCOUNT, 0, 60
	TCVAR:ARG:(LCOUNT + 460) = EX:ARG:LCOUNT
NEXT
;TCVAR:520～539はBASE
FOR LCOUNT, 0, 20
	TCVAR:ARG:(LCOUNT + 520) = BASE:ARG:LCOUNT
NEXT

;V润、A润、恭顺、欲情之PALAMは少なすぎると支障が出る之で底上げ
PALAM:ARG:V润 += 3000
PALAM:ARG:A润 += 3000
PALAM:ARG:恭顺 += 3000
PALAM:ARG:欲情 += 3000

;绝顶射精槽初期化
CVARSET BASE, 2, 0

@SIMULATE_TRAIN_CALC
#DIM LCOUNT

CALL SIMULATE_TRAIN_CALC_SET, PLAYER
FOR LCOUNT, 0, 9
	SIF TARGET:LCOUNT > 0
		CALL SIMULATE_TRAIN_CALC_SET, TARGET:LCOUNT
NEXT

@SIMULATE_TRAIN_CALC_SET, ARG
#DIM LCOUNT
;V润、A润、恭顺、欲情之底上げ終了
PALAM:ARG:V润 -= 3000
PALAM:ARG:A润 -= 3000
PALAM:ARG:恭顺 -= 3000
PALAM:ARG:欲情 -= 3000

;PLAYERがMASTERでないときには一部之经验之増加量を减らす
IF ARG == TARGET && PLAYER != MASTER
	FOR LCOUNT, 0, 100
		SELECTCASE EXPNAME:LCOUNT
		;増加分を８割减
		CASE "奉仕快乐经验", "嗜虐快乐经验"
			EXP:ARG:LCOUNT = TCVAR:ARG:(LCOUNT + 300) + (EXP:ARG:LCOUNT - TCVAR:ARG:(LCOUNT + 300) )/5
		ENDSELECT
	NEXT
ENDIF
;EXP関连之数值は出きった后な之で表示之簡略化之ため值を増分に书き换える
FOR LCOUNT, 0, 100
	TCVAR:ARG:(LCOUNT + 300) = EXP:ARG:LCOUNT - TCVAR:ARG:(LCOUNT + 300)
NEXT
;以下でJUEL之増加分を计算する（まずはPALAM、EX、JUEL之増分を出す）
FOR LCOUNT, 0, 30
	PALAM:ARG:LCOUNT = MAX(PALAM:ARG:LCOUNT - TCVAR:ARG:(LCOUNT + 430), 0)
NEXT
FOR LCOUNT, 0, 60
	EX:ARG:LCOUNT = EX:ARG:LCOUNT - TCVAR:ARG:(LCOUNT + 460)
NEXT
FOR LCOUNT, 20, 25
	JUEL:ARG:LCOUNT = JUEL:ARG:LCOUNT - TCVAR:ARG:(LCOUNT + 400)
NEXT
;JUEL増加量之算出(通常之半分程度)
FOR LCOUNT, 0, 16
	SIF ARG == MASTER
		BREAK

	SELECTCASE LCOUNT
	CASE 3, 11, 12, 13
		CONTINUE
	CASEELSE
		GOTJUEL:LCOUNT = MULTIPLY(GET_JUEL(LCOUNT, ARG), 50)

		;对象がMASTERでないなら獲得量を减らす
		SELECTCASE PLAYER
		CASE MASTER
		CASE ASSI
			GOTJUEL:LCOUNT /= 2
		CASEELSE
			GOTJUEL:LCOUNT /= 4
		ENDSELECT

		SIF GOTJUEL:LCOUNT < 50
			GOTJUEL:LCOUNT = 0
		JUEL:ARG:LCOUNT += GOTJUEL:LCOUNT

		;数值が出きった后な之で表示之簡略化之ため值を増分に书き换える
		TCVAR:ARG:(LCOUNT + 400) = GOTJUEL:LCOUNT
	ENDSELECT
NEXT
;増加したPALAM、EXはJUELに变えた之で、元之值に戻す
FOR LCOUNT, 0, 30
	PALAM:ARG:LCOUNT = TCVAR:ARG:(LCOUNT + 430)
NEXT
FOR LCOUNT, 0, 60
	EX:ARG:LCOUNT = TCVAR:ARG:(LCOUNT + 460)
NEXT
FOR LCOUNT, 20, 25
	JUEL:ARG:LCOUNT = TCVAR:ARG:(LCOUNT + 400)
NEXT
;梦魇は精液经验でリフレッシュ
; IF TALENT:ARG:梦魇 + TALENT:ARG:上位梦魇
; 	CALL CALC, "体力回復", TCVAR:ARG:320*30, ARG
; 	CALL CALC, "气力回復", TCVAR:ARG:320*30, ARG
; 	CALL CALC, "精力回復", TCVAR:ARG:320*2, ARG
; ENDIF

;------------------------------------------------------------------------
;ARGにはTARGET側之表示经验、ARG:1にはPLAYER側之表示经验之番号を入れる
;ただし、一晩开放など、PLAYER < 0 もある之で注意
;经验名で指定したいところだが、TCVARが番号で記录している之で速度重视で番号で指定する
;------------------------------------------------------------------------
@PRINT_SIMULATE_TRAIN, ARG, ARG:1
#DIM LCOUNT
;PLAYERとTARGETを同时に处理する
#DIM ACTOR, 4
#DIM EXP_ADD, 4

ACTOR = TARGET
ACTOR:1 = TARGET:1
ACTOR:2 = TARGET:2
ACTOR:3 = PLAYER

;それぞれ参照するも之が长すぎる之で代入
VARSET EXP_ADD

FOR LCOUNT, 0, 3
	SIF ACTOR:LCOUNT > 0
		EXP_ADD:LCOUNT = TCVAR:(ACTOR:LCOUNT):(ARG + 300)
NEXT
SIF PLAYER > 0
	EXP_ADD:3 = TCVAR:PLAYER:(ARG:1 + 300)

SIF EXP_ADD + EXP_ADD:1 + EXP_ADD:2 + EXP_ADD:3 == 0
	RETURN 0

SIF TARGET:1 == 0
	PRINTFORM 　　　　　

FOR LCOUNT, 0, 4
	SIF ACTOR:LCOUNT <= 0
		CONTINUE

	IF EXP_ADD:LCOUNT > 0
		;PLAYER之时には经验之表示が异なる
		IF LCOUNT == 3
			LOCALS = %TEXT_RJ(EXPNAME:(ARG:1), 13)%＋{EXP_ADD:LCOUNT, 3}
		ELSE
			LOCALS = %TEXT_RJ(EXPNAME:ARG, 13)%＋{EXP_ADD:LCOUNT, 3}
		ENDIF
		PRINTFORM %TEXT_LJ(LOCALS, 19)%
	ELSE
		PRINTFORM %BL(19)%
	ENDIF
NEXT
PRINTL 
;PLAYERがいない场合（一晩开放とか）も考慮する
@SIMULATE_TRAIN_SHOW
#DIM LCOUNT
#DIM LCOUNT2
;PLAYERとTARGETを同时に处理する
#DIM ACTOR, 4
#DIM JUEL_ADD, 4
;经验值を得ているかを記录
#DIM IS_EXP_ADD, 4

PRINTL 

ACTOR = TARGET
ACTOR:1 = TARGET:1
ACTOR:2 = TARGET:2
ACTOR:3 = PLAYER

;经验に变动があった角色を表示する之でチェック
VARSET IS_EXP_ADD

FOR LCOUNT2, 0, 4
	SIF ACTOR:LCOUNT2 <= 0
		CONTINUE

	FOR LCOUNT, 0, 100
		SIF TCVAR:(ACTOR:LCOUNT2):(LCOUNT + 300) == 0
			CONTINUE

		IS_EXP_ADD:LCOUNT2 = 1
		BREAK
	NEXT
NEXT

;誰も经验变动ないなら終わり
SIF IS_EXP_ADD:0 + IS_EXP_ADD:1 + IS_EXP_ADD:2 + IS_EXP_ADD:3 == 0
	RETURN 0

;名前出力
SIF ACTOR:1 == 0
	PRINTFORM 　　　　　

FOR LCOUNT, 0, 4
	SIF ACTOR:LCOUNT <= 0
		CONTINUE

	IF IS_EXP_ADD:LCOUNT
		LOCALS = [%CALLNAME:(ACTOR:LCOUNT)%]
		PRINTFORM %TEXT_LJ(TEXT_RJ(LOCALS, 14), 19)%
	ELSE
		PRINTFORM %BL(19)%
	ENDIF
NEXT
PRINTL 

;绝顶经验
CALL PRINT_SIMULATE_TRAIN, 2, 2
;童贞猎取经验
CALL PRINT_SIMULATE_TRAIN, 14, 14
;精液经验＆射精经验
CALL PRINT_SIMULATE_TRAIN, 20, 3
CALL PRINT_SIMULATE_TRAIN, 3, 20
CALL PRINT_SIMULATE_TRAIN, 17, 17
CALL PRINT_SIMULATE_TRAIN, 18, 18
CALL PRINT_SIMULATE_TRAIN, 19, 19
;喷乳经验
CALL PRINT_SIMULATE_TRAIN, 54, 54
;VA经验＆VA扩张经验
CALL PRINT_SIMULATE_TRAIN, 0, 0
CALL PRINT_SIMULATE_TRAIN, 53, 53
CALL PRINT_SIMULATE_TRAIN, 66, -1
CALL PRINT_SIMULATE_TRAIN, 1, 1
CALL PRINT_SIMULATE_TRAIN, 57, 57
CALL PRINT_SIMULATE_TRAIN, 67, -1
;接吻经验
CALL PRINT_SIMULATE_TRAIN, 70, 70
;性交经验＆插入经验
CALL PRINT_SIMULATE_TRAIN, 12, 15
CALL PRINT_SIMULATE_TRAIN, 15, 12
CALL PRINT_SIMULATE_TRAIN, 13, 16
CALL PRINT_SIMULATE_TRAIN, 16, 13
;自慰经验
CALL PRINT_SIMULATE_TRAIN, 10, 10
;百合、男同经验
CALL PRINT_SIMULATE_TRAIN, 40, 40
CALL PRINT_SIMULATE_TRAIN, 41, 41
;被虐快乐经验＆嗜虐快乐经验
CALL PRINT_SIMULATE_TRAIN, 30, 32
CALL PRINT_SIMULATE_TRAIN, 32, 30
;奉仕快乐～舐阴经验
FOR LCOUNT, 21, 29
	CALL PRINT_SIMULATE_TRAIN, LCOUNT, LCOUNT
NEXT
;喉性交经验
CALL PRINT_SIMULATE_TRAIN, 76, 76

;对象が一般人なら珠取得表示は无
IF PLAYER < 0
	PRINTL 
	RETURN 0
ENDIF

;各种珠ＧET
FOR LCOUNT2, 0, 16
	SELECTCASE LCOUNT2
	CASE 3, 11, 12, 13
		CONTINUE
	ENDSELECT
	VARSET JUEL_ADD
	FOR LCOUNT, 0, 4
		SIF ACTOR:LCOUNT > 0
			JUEL_ADD:LCOUNT = TCVAR:(ACTOR:LCOUNT):(LCOUNT2 + 400)
	NEXT
	SIF JUEL_ADD + JUEL_ADD:1 + JUEL_ADD:2 + JUEL_ADD:3 == 0
		CONTINUE
	SIF ACTOR:1 == 0
		PRINTFORM 
	FOR LCOUNT, 0, 4
		SIF ACTOR:LCOUNT <= 0
			CONTINUE
		IF JUEL_ADD:LCOUNT
			LOCALS = %BL(3)% %PALAMNAME:LCOUNT2%之珠＋{JUEL_ADD:LCOUNT, 4}
			PRINTFORM %TEXT_LJ(LOCALS, 19)%
		ELSE
			PRINTFORM %BL(19)%
		ENDIF
	NEXT
	PRINTL 
NEXT

PRINTL 

;-------------------------------------------------
;指令ARGを(ARG:1)回擬似的に计算する
;-------------------------------------------------
@SIMULATE_TRAIN_COM, ARG, ARG:1
#DIM LCOUNT
#DIM MEMO_FLAG8
#DIM MEMO_TFLAG33
#DIM MEMO_SELECTCOM
#DIM MEMO_BASE0
#DIM MEMO_BASE1
#DIM MEMO_BASE6
#DIM MEMO_BASE7

SIF ARG:1 == 0
	RETURN 0
IF COND("调教中")
	TRYCALLFORM COM_ABLE{ARG}
	SIF RESULT == 0
		RETURN 0
ENDIF

;文本出力标志(FLAG:8)と、Ｈ氛围を弄る。
MEMO_FLAG8 = FLAG:8
MEMO_TFLAG33 = TFLAG:33
MEMO_SELECTCOM = SELECTCOM

FLAG:8 = 0
TFLAG:33 = MAX(TFLAG:33, 100)
SELECTCOM = ARG

;插入标志は解除
CVARSET TEQUIP, 20, 0
CVARSET TEQUIP, 21, 0
CVARSET TEQUIP, 22, 0

CALL RESET_TFLAGS

MEMO_BASE0 = BASE:体力
MEMO_BASE1 = BASE:气力

CALL SOURCE_CHECK_SIMPLE, SELECTCOM, 0, 100, ARG:1

;選択指令と实行值补正（やる气）之确认
;PRINTFORML TCVAR:28 = {TCVAR:28}, %TRAINNAME:SELECTCOM%

MEMO_BASE6 = BASE:PLAYER:精力

;PRINTFORML BASE:PLAYER:精力 = {BASE:PLAYER:精力}, BASE:PLAYER:绝顶 = {BASE:PLAYER:绝顶}, COND("绝顶", PLAYER) = {COND("绝顶", PLAYER)}
IF COND("绝顶", PLAYER)
	TCVAR:PLAYER:5 = CALCF("绝顶之原因", TARGET, SELECTCOM)
	CALL CALC, "绝顶させた处理", TARGET
	CALL CALC, "绝顶した处理", PLAYER
	;精力减少控えめ
	BASE:PLAYER:精力 = MAX(MEMO_BASE6 - COND("绝顶", PLAYER)*2, 0)
ENDIF

;体力气力之减少は少なめにする
BASE:体力 = MAX(MEMO_BASE0 - 20*ARG:1, 1)
BASE:气力 = MAX(MEMO_BASE1 - 10*ARG:1, 0)
;体力气力回復
IF  TALENT:吸精体質
	CALL CALC, "体力回復", COND("绝顶", PLAYER) * 10
	CALL CALC, "气力回復", COND("绝顶", PLAYER) * 10
ENDIF
;CUPをPALAMに移す
FOR LCOUNT, 0, 17
	PALAM:LCOUNT = MIN(PALAM_LV(16), PALAM:LCOUNT+CUP:LCOUNT-CDOWN:LCOUNT)
NEXT

;体力气力精力が减りすぎていた时之处理
BASE:体力 = MAX(BASE:体力, 1)
BASE:气力 = MAX(BASE:气力, 0)
BASE:精力 = MAX(BASE:精力, 1)

BASE:PLAYER:体力 = MAX(BASE:PLAYER:体力, 1)
BASE:PLAYER:气力 = MAX(BASE:PLAYER:气力, 0)
BASE:PLAYER:精力 = MAX(BASE:PLAYER:精力, 1)

FLAG:8 = MEMO_FLAG8
TFLAG:33 = MEMO_TFLAG33
SELECTCOM = MEMO_SELECTCOM


;-------------------------------------------------
;角色ARGが主要な指令を实行できるかを全部调べる
;-------------------------------------------------
@CHECK_COM_ABLE, ARG
#DIM LCOUNT
#DIM MEMO_TEQUIP30

SWAP TARGET, ARG
MEMO_TEQUIP30 = TEQUIP:逆推

;初期化
CSTR:实行可能指令 = /

FOR LCOUNT, 0, 200
	SELECTCASE LCOUNT
	;主要指令之選别
	CASE 1, 3 TO 6, 9, 10, 13, 14, 60, 61, 65 TO 68, 70, 71, 75, 80 TO 83, 90 TO 99, 104, 105, 120, 121, 122, 125 TO 127, 140 TO 144, 150
		;指令实行済みでないと行わない
		SIF FIRSTCOM(LCOUNT) == 0
			CONTINUE

		SELECTCASE LCOUNT
		;条件が少々厳しい指令
		CASE 60, 61, 65 TO 67
			SIF COND("ベテラン：V性爱") == 0
				CONTINUE
		CASE 70, 71, 75
			SIF COND("ベテラン：A性爱") == 0
				CONTINUE
		ENDSELECT

		TRYCALLFORM COM_ABLE{LCOUNT}
		SIF RESULT
			CSTR:实行可能指令 = %CSTR:实行可能指令%%TRAINNAME:LCOUNT%/

		SELECTCASE LCOUNT
		;逆推状态も现る
		CASE 1, 4, 6, 9, 65, 75, 83, 90 TO 99, 120, 126, 150
			TEQUIP:逆推 = 1
			TRYCALLFORM COM_ABLE{LCOUNT}
			SIF RESULT
				CSTR:实行可能指令 = %CSTR:实行可能指令%逆推%TRAINNAME:LCOUNT%/
			TEQUIP:逆推 = 0
		ENDSELECT

	ENDSELECT
NEXT

;PRINTFORMW %CSTR:实行可能指令%

TEQUIP:逆推 = MEMO_TEQUIP30
SWAP TARGET, ARG


;-------------------------------------------------
;指令ARGSを角色ARGが实行できるかを参照し、实行可能なら１を返す。だめなら０
;-------------------------------------------------
@CANTRAIN(ARGS, ARG)
#FUNCTION

SIF ARG < 0
	RETURNF 0

LOCALS = /%ARGS%/

LOCAL = 0

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

RETURNF STRCOUNT(CSTR:ARG:实行可能指令, LOCALS)


;-------------------------------------------------
;@CANTRAIN之复数版
;指令ARGSを角色ARG:0～ARG:2が实行できるかを参照し、实行可能な人数を返す
;ARG:Xが0之时にTARGET:X > 0ならばTARGET:Xを代入する…ようはTARGET:0～TARGET:2について调べるときにはARGは0で良い
;-------------------------------------------------
@CANTRAINS(ARGS, ARG, ARG:1, ARG:2)
#FUNCTION
#DIM LCOUNT

SIF ARG < 0
	RETURNF 0

LOCALS = /%ARGS%/

LOCAL = 0

FOR LCOUNT, 0, 3
	SIF ARG:LCOUNT == 0 && TARGET:LCOUNT > 0
		ARG:LCOUNT = TARGET:LCOUNT

	SIF ARG:LCOUNT > 0
		LOCAL += STRCOUNT(CSTR:(ARG:LCOUNT):实行可能指令, LOCALS)
NEXT

RETURNF LOCAL
