@SHOW_STATUS
REDRAW 0
;CLEARLINE使用的行数清理
TFLAG:31 = LINECOUNT

;是否自动拖鞋（第一回合启用）
IF CONFIG("是否室内自动脱鞋") && (NUM("回合数") == 0 || PREVCOM == 171)
	CALL SET_SHOES, TARGET
	SIF ASSI
		CALL SET_SHOES, ASSI
ENDIF

DRAWLINE
CALL SHOW_STATUS_MAIN

CALL SETFLAG, "助手对象之恋慕外し开始"

;@COMORDERで求める实行值之初期化
CVARSET TCVAR, 26, 0
CVARSET TCVAR, 27, 0

FLAG:238 = 1


@SHOW_STATUS_MAIN
;季节、年表示部分
;PRINTFORM {FLAG:872}年{FLAG:871}日目 %TEXTS("日付表示")%
PRINT  Ｈ氛围：
CALL TEXT, "Ｈ氛围ハート"
;Ｈ氛围記录
TFLAG:34 = MOOD()
PRINTL 
PRINTFORM 调教者:%NAME:PLAYER% 
IF PENIS(PLAYER)
	SELECTCASE SIZE("阴茎", PLAYER)
	CASE 0

		PRINTFORM [小
	CASE 1

		PRINTFORM [普
	CASE 2

		PRINTFORM [巨根
	CASE 3

		PRINTFORM [马尺寸
	CASE 4

		PRINTFORM [手臂尺寸
	CASE 5
		PRINTFORM [龙根
	CASEELSE

		PRINTFORM [规格外
	ENDSELECT

	PRINTFORM (尺寸LV{SIZE("阴茎", PLAYER)})
	SIF TALENT:PLAYER:龙之血
		PRINTFORM %UNI("龙")%
	PRINTFORM ]

	;PRINTFORM P润%TEXTS("调教参数之LV", GET_PALAM_LV(PALAM:PLAYER:P润) )%
	CALL PRINT_BAR, "PALAM", PALAM:PLAYER:P润, 0, 4
	;PRINTFORM %PALAM_NUM(PALAM:PLAYER:P润)%
ENDIF
IF INSERT("插入", PLAYER)
	VARSET LOCALS
	IF INSERT("V双枪插入", PLAYER)
		LOCALS = V双枪插入
	ELSEIF INSERT("A双枪插入", PLAYER)
		LOCALS = A双枪插入
	ELSEIF INSERT("二穴插入", PLAYER)
		LOCALS = 二穴插入
	ENDIF
	IF STRLENS(LOCALS) == 0
		IF INSERT("A插入", PLAYER)
			PRINTFORM [A%CONDS("体位", PLAYER)%]
		ELSEIF INSERT("尿道插入", PLAYER)
			PRINTFORM [尿道%CONDS("体位", PLAYER)%]
		ELSE
			PRINTFORM [%CONDS("体位", PLAYER)%]
		ENDIF
	ENDIF
ENDIF
IF TEQUIP:Ｗ逆推
	PRINT [W双逆推中]

ELSEIF TEQUIP:六九式
	PRINTFORM [\@ TEQUIP:逆推 ? 逆推 # \@六九式(%TRAIN_NAME_STANDARD(TEQUIP:PLAYER:六九式)%)中]
ELSEIF TEQUIP:逆推 == 0
	SIF TEQUIP:接吻
		PRINT [间隙就接吻]
ENDIF
SIF TEQUIP:高潮管理
	PRINT [高潮管理]
;SIF STATE("恍惚", PLAYER)
;	PRINT [恍惚]
;SIF STATE("脱力", PLAYER)
;	PRINT [脱力]

SELECTCASE TEQUIP:PLAYER:媚药
CASE IS >= 200
	PRINT [媚药(激)]
CASE IS >= 150
	PRINT [媚药(强)]
CASE IS > 50
	PRINT [媚药]
CASE IS > 0
	PRINT [媚药(弱)]
ENDSELECT
SIF GETBIT(TEQUIP:PLAYER:药品, 0)
	PRINT [镇静剂]
SIF TEQUIP:PLAYER:排卵诱发剂
	PRINT [排卵诱发剂]
IF TEQUIP:运命检閲
	PRINT [运命检閲:
	LOCALS = 
	SIF COND("运命检閲：爱抚")
		LOCALS += "爱抚＆"
	SIF COND("运命检閲：V")
		LOCALS += "V＆"
	SIF COND("运命检閲：胸")
		LOCALS += "胸＆"
	SIF COND("运命检閲：接吻")
		LOCALS += "接吻＆"
	SIF COND("运命检閲：A")
		LOCALS += "A＆"
	SIF COND("运命检閲：咏唱")
		LOCALS += "咏唱＆"
	STRLENS LOCALS
	SUBSTRING LOCALS, 0, RESULT - 2
	PRINTFORM %RESULTS%]
ENDIF
SIF TEQUIP:MASTER:薫香
	PRINTFORM [%TEXTS("薫香", TEQUIP:MASTER:薫香)%]

;SIF ITEM:精液储蓄量
;	PRINTFORM 　（魔法瓶：{ITEM:精液储蓄量}）
PRINTL 

;射精槽設定
CALL SETFLAG, "射精槽", TARGET
CALL SETFLAG, "射精槽", MASTER
SIF ASSI>0
	CALL SETFLAG, "射精槽", ASSI


CALL SHOW_STATUS_CHARA, TARGET
SIF TEQUIP:Ｗ逆推
	CALL SHOW_STATUS_CHARA, ASSI

IF PENIS(MASTER)
	PRINT 主人：射精
ELSE
	PRINT 主人：绝顶
ENDIF
CALL PRINT_BAR, "溜め槽", BASE:MASTER:绝顶, MAXBASE:MASTER:绝顶, 26
PRINTFORM ({BASE:MASTER:绝顶}/{MAXBASE:MASTER:绝顶})

;主人精力表示
PRINTFORM 　精力({BASE:MASTER:精力}/{MAXBASE:MASTER:精力})
;溜まっている
SIF BASE:MASTER:精力 >= MAXBASE:MASTER:精力 && PENIS(MASTER)
	CALL PRINT_COLORTEXT, "(たまっている) ", DEF_COLOR("粉红色心形")
PRINTL 
;助手がいて、ダウンしていないなら表示
IF ASSI > 0 && STATE("击沈", ASSI) == 0 && TEQUIP:Ｗ逆推 == 0
	IF PENIS(ASSI)
		PRINT 助手：射精

	ELSE
		PRINT 助手：绝顶

	ENDIF
	CALL PRINT_BAR, "溜め槽", BASE:ASSI:绝顶, MAXBASE:ASSI:绝顶, 26
	PRINTFORM ({BASE:ASSI:绝顶}/{MAXBASE:ASSI:绝顶})
	;助手精力表示
	PRINT 　精力
	PRINTFORM ({BASE:ASSI:精力}/{MAXBASE:ASSI:精力})
	PRINTL 
ENDIF

@SHOW_STATUS_CHARA, ARG
#DIM MEMO_TARGET

MEMO_TARGET = TARGET
TARGET = ARG

PRINTFORM %NAME:TARGET% 
CALL TEXT, "生理周期", TARGET
;开心
; SELECTCASE MARK:机嫌
; CASE 1
; 	PRINT (观望)

; CASE 2
; 	PRINT (无干劲)

; ENDSELECT
PRINTFORM (好\@ TEQUIP:Ｗ逆推 ? # 感觉 \@:{CFLAG:2} 奉\@ TEQUIP:Ｗ逆推 ? # 仕快乐 \@:{EXP:侍奉快乐经验}) 

CALL SHOW_EQUIP_2

BASE:体力 = MAX(BASE:体力, 0)
BASE:气力 = MAX(BASE:气力, 0)

PRINT 体力
CALL PRINT_BAR, "减少槽", BASE:体力, MAXBASE:体力, 32
PRINTFORML ({BASE:体力}/{MAXBASE:体力}) 
PRINT 气力
CALL PRINT_BAR, "减少槽", BASE:气力, MAXBASE:气力, 32
PRINTFORM ({BASE:气力}/{MAXBASE:气力}) 
;精力表示
PRINTFORML 精力({BASE:精力}/{MAXBASE:精力}) 

CALL NEW_PRINT_PALAM

IF PENIS(TARGET)
	PRINTFORM 射精
	CALL PRINT_BAR, "溜め槽", BASE:绝顶, MAXBASE:绝顶, 24
	PRINTFORM ({BASE:绝顶}/{MAXBASE:绝顶})　
ENDIF
IF TALENT:泌乳体质
	SIF MAXBASE:喷乳 == 0
		MAXBASE:喷乳 = 10000
	PRINTFORM 母乳
	CALL PRINT_BAR, "溜め槽", BASE:喷乳, MAXBASE:喷乳, 24
	PRINTFORM ({BASE:喷乳}/{MAXBASE:喷乳})
ENDIF

SIF PENIS(TARGET) || TALENT:泌乳体质
	PRINTL 

IF EX:处女丧失 || EX:C绝顶 || EX:尿道绝顶 || EX:V绝顶 || EX:A绝顶 || EX:B绝顶 || EX:M绝顶 || EX:射精 || EX:喷乳 
	PRINT < 
	SIF EX:处女丧失
		CALL PRINT_COLORTEXT, "处女丧失 ", DEF_COLOR("粉红色心形")
	SIF EX:尿道绝顶
		PRINTFORM 尿道绝顶:{EX:尿道绝顶}回 
	SIF EX:C绝顶
		PRINTFORM C绝顶:{EX:C绝顶}回 
	SIF EX:V绝顶
		PRINTFORM V绝顶:{EX:V绝顶}回 
	SIF EX:A绝顶
		PRINTFORM A绝顶:{EX:A绝顶}回 
	SIF EX:B绝顶
		PRINTFORM B绝顶:{EX:B绝顶}回 
	SIF EX:M绝顶
		PRINTFORM M绝顶:{EX:M绝顶}回 
	SIF EXP_GAIN(3)
		PRINTFORM 射精:{EXP_GAIN(3)}回 
	SIF EXP_GAIN(54)
		PRINTFORM 喷乳:{EXP_GAIN(54)}回 
	; SIF EX:能量吸收
	; 	PRINTFORM 吸能:{EX:能量吸收}回 
	PRINT >
	PRINTL 
ENDIF
IF PALAM:Extacy
	PRINTFORM Extacy:{PALAM:Extacy} 
	SELECTCASE PALAM:连续绝顶COMBO数
	CASE IS >= 100
		PRINTFORM … 连续绝顶 {PALAM:连续绝顶COMBO数}COMBO！！！！ 
	CASE IS >= 50
		PRINTFORM … 连续绝顶 {PALAM:连续绝顶COMBO数}COMBO！！！ 
	CASE IS >= 20
		PRINTFORM … 连续绝顶 {PALAM:连续绝顶COMBO数}COMBO！！ 
	CASE IS >= 10
		PRINTFORM … 连续绝顶 {PALAM:连续绝顶COMBO数}COMBO！！ 
	CASE IS >= 2
		PRINTFORM … 连续绝顶 {PALAM:连续绝顶COMBO数}COMBO！ 
	ENDSELECT
	PRINTL 
ENDIF

CALL CHECK_SATISFACTION, TARGET

;性欲
IF MAXBASE:性欲
	PRINT 性欲
	IF BASE:性欲 >= MAXBASE:性欲
		PRINT [
		IF BASE:性欲 >= MAXBASE:性欲*2
			SETCOLOR DEF_COLOR("粉红色心形")
			PRINT 大满足！

		ELSE
			SETCOLOR DEF_COLOR("黄色")
			PRINT 满足！

		ENDIF
		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "", BASE:性欲, MAXBASE:性欲, 4
	ENDIF
	PRINTFORM ({BASE:性欲}/{MAXBASE:性欲}) 
ENDIF

;精液成瘾
IF MAXBASE:精液欲
	PRINT 精液欲
	IF BASE:精液欲 >= MAXBASE:精液欲
		PRINT [
		IF BASE:精液欲 >= MAXBASE:精液欲*2
			SETCOLOR DEF_COLOR("粉红色心形")
			PRINT 大满足！

		ELSE
			SETCOLOR DEF_COLOR("黄色")
			PRINT 满足！

		ENDIF
		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "白色槽", BASE:精液欲, MAXBASE:精液欲, 4
	ENDIF
	PRINTFORM ({BASE:精液欲}/{MAXBASE:精液欲}) 
ENDIF
;胎内精液量
IF TALENT:性别 != 1
	PRINTFORM 胎内精液量
	IF COND("胎内之精液量") >= 100000
		PRINT [
		SETCOLOR DEF_COLOR("粉红色心形")
		PRINT 满满的

		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "白色槽", COND("胎内之精液量"), 100000, 4
	ENDIF
	SELECTCASE COND("胎内之精液量")
	CASE IS >= 10000
		PRINTFORM ({COND("胎内之精液量")/1000}kp)
	CASEELSE
		PRINTFORM ({COND("胎内之精液量")}p)
	ENDSELECT
	;PRINTFORM (孕娠确率{COND("受精确率")}％)
ENDIF

;SIF MAXBASE:性欲 || MAXBASE:精液欲 || TALENT:性别 != 1
;	PRINTL 

IF CONFIG("画面表示：尿意槽")
	PRINT 尿意
	IF BASE:尿意 >= MAXBASE:尿意
		PRINT [
		IF BASE:尿意 >= MAXBASE:尿意*2
			SETCOLOR DEF_COLOR("粉红色心形")
			PRINT 限界！
		ELSE
			SETCOLOR DEF_COLOR("黄色")
			PRINT 要漏了

		ENDIF
		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "白色槽", BASE:尿意, MAXBASE:尿意, 4
	ENDIF
	;PRINTFORM ({BASE:尿意}/{MAXBASE:尿意}) 
ENDIF
IF CONFIG("画面表示：排泄欲槽")
	PRINT 排泄欲
	IF BASE:排泄欲 >= MAXBASE:排泄欲
		PRINT [
		SETCOLOR DEF_COLOR("粉红色心形")
		PRINT 限界！
		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "白色槽", BASE:排泄欲, MAXBASE:排泄欲, 4
	ENDIF
	;PRINTFORM ({BASE:排泄欲}/{MAXBASE:排泄欲}) 
ENDIF

;SIF CONFIG("画面表示：尿意槽") || CONFIG("画面表示：排泄欲槽")
	PRINTL 

CALL SHOW_EQUIP_1

PRINTL 

TARGET = MEMO_TARGET


@SHOW_EQUIP_2
;状況之表示
;调教对象には使わない、特殊な着脱式之道具を追加した场合はここに追加をしてください
;（场所变更、マスターや助手が使用する道具等）

IF PENIS(TARGET)
	SELECTCASE SIZE("阴茎", TARGET)
	CASE 0
		PRINTFORM [小
	CASE 1

		PRINTFORM [普
	CASE 2

		PRINTFORM [巨根
	CASE 3

		PRINTFORM [马尺寸
	CASE 4

		PRINTFORM [手臂尺寸
	CASE 5
		PRINTFORM [龙根
	CASEELSE

		PRINTFORM [規格外
	ENDSELECT

	PRINTFORM (尺寸LV{SIZE("阴茎", TARGET)})
	SIF TALENT:龙之血
		PRINTFORM %UNI("龙")%
	PRINTFORM ]
	;PRINTFORM 茎润%TEXTS("调教参数之LV", GET_PALAM_LV(PALAM:P润) )%
	;CALL PRINT_BAR, "PALAM", PALAM:P润, 0, 4
ENDIF
;暂时注释发情期
IF 0 && COND("发情期")
	PRINT [
	SETCOLOR DEF_COLOR("粉红色心形")
	PRINTFORM 发情中
	RESETCOLOR
	PRINT ]
ENDIF

SIF STATE("小根术")
	PRINT [小根术]
SIF STATE("大根术")
	PRINTFORM [大根术]
SIF STATE("高敏术")
	PRINT [高敏术]
SIF STATE("快感术")
	PRINT [快感术]
IF STATE("精强术")
	PRINT [精强术
	SIF STATE("精强术") == 2
		PRINT *2
	SIF STATE("精强术") >= 3
		PRINT *3
	PRINT ]
ENDIF
IF STATE("魅强术")
	PRINT [魅强术
	SIF STATE("魅强术") == 2
		PRINT *2
	SIF STATE("魅强术") >= 3
		PRINT *3
	PRINT ]
ENDIF
IF STATE("防强术")
	PRINT [防强术
	SIF STATE("防强术") == 2
		PRINT *2
	SIF STATE("防强术") >= 3
		PRINT *3
	PRINT ]
ENDIF
SIF STATE("皮肤硬化")
	PRINT [皮肤硬化]
SIF STATE("虚弱术")
	PRINT [虚弱术]
SIF STATE("倍化术")
	PRINT [倍化术]

SIF STATE("具现", TARGET)
	PRINT [具现]
; IF NAMES("食事", FLAG:40) == "木天蓼酒" && TALENT:种族 == 6
; 	PRINT [木天蓼]

IF STATE("烂醉", TARGET)
	PRINT [烂醉]
ELSEIF STATE("醉酒", TARGET)
	PRINT [饮酒]

ENDIF
SIF TEQUIP:称賛
	CALL PRINT_COLORTEXT, "[开心]", DEF_COLOR("粉红色心形")
SIF EX:忍住了没丢
	CALL PRINT_COLORTEXT, "[绝顶边缘]", DEF_COLOR("粉红色心形")
SIF TEQUIP:摄像机
	PRINTFORM [摄影中(剩余{10 - TEQUIP:摄像机 + 1}轮)]
SIF COND("人里")
	PRINT [人类城镇]

SIF COND("森林")
	PRINT [森林]
SIF COND("花園")
	PRINT [花园]

SIF COND("水边")
	PRINT [水边]
SIF COND("海")
	PRINT [海]
SIF TEQUIP:屋内露出PLAY
	PRINT [馆内]
IF TEQUIP:女王大人PLAY
	IF TALENT:性别 == 1
		PRINTFORM [SMPLAY LV{TEQUIP:女王大人PLAY}]
	ELSE
		PRINTFORM [女王大人PLAY LV{TEQUIP:女王大人PLAY}]
	ENDIF
ENDIF
SIF TEQUIP:羞恥PLAY
	PRINT [羞耻PLAY中]
IF TEQUIP:浴室PLAY
	SELECTCASE TEQUIP:MASTER:沐浴剂
	CASE 1
		PRINT [泡泡浴]

	CASE 2
		PRINT [秘汤温泉浴]

	CASE 3
		PRINT [碳酸浴]

	CASE 4
		PRINT [花瓣浴]

	CASE 5
		PRINT [盐水浴]

	CASE 6
		PRINT [桧水浴]

	CASE 7
		PRINT [神秘浴剂]

	CASE 30
		PRINT [润滑液浴]

	CASE 31
		PRINT [媚药浴]

	CASE 79
		PRINT [精液浴]

	CASEELSE
		PRINT [浴室]

	ENDSELECT
ENDIF
SIF TEQUIP:新婚PLAY
	PRINT [新婚]
SIF TEQUIP:触手
	PRINT [触手]
SIF TEQUIP:爱之圈套
	PRINT [爱之圈套中]
IF TEQUIP:深喉口交 == PLAYER
	PRINT [深喉中]

ELSEIF TEQUIP:深喉口交
	PRINTFORM [深喉中(%CALLNAME:(TEQUIP:深喉口交)%)]
ENDIF
IF TEQUIP:Ｗ逆推
	;PRINT [W双逆推中]

ELSEIF TEQUIP:六九式
	PRINTFORM [\@ TEQUIP:逆推 ? 逆推 # \@六九式(%TRAIN_NAME_STANDARD(TEQUIP:六九式)%)中]
ELSEIF TEQUIP:逆推
	PRINT [逆推
	IF TEQUIP:PLAYER:拘束
		PRINT 拘束
	ELSEIF TEQUIP:顔面骑乘
		PRINT 顔面骑乘
	ENDIF
	PRINT 中]

	SIF TEQUIP:接吻
		PRINT [间隙就接吻]

ENDIF

SIF STATE("脱力", TARGET)
	PRINT [脱力]


IF INSERT("插入")
	PRINT [
	VARSET LOCALS
	IF INSERT("V双枪插入")
		LOCALS = V双枪插入
	ELSEIF INSERT("A双枪插入")
		LOCALS = A双枪插入
	ELSEIF INSERT("二穴插入")
		LOCALS = 二穴插入
	ENDIF
	IF STRLENS(LOCALS) == 0
		IF INSERT("插入对象") != PLAYER
			IF PLAYER == MASTER
				LOCALS:1 = (助手)
			ELSEIF PLAYER == ASSI
				LOCALS:1 = (主人)
			ENDIF
		ENDIF

		IF INSERT("A插入")
			LOCALS = A%CONDS("体位")%%LOCALS:1%
		ELSEIF INSERT("尿道插入")
			LOCALS = 尿道%CONDS("体位")%%LOCALS:1%
		ELSE
			LOCALS = %CONDS("体位")%%LOCALS:1%
		ENDIF
	ENDIF
	IF COND("偏爱的体位中")
		SETCOLOR DEF_COLOR("粉红色心形")
		LOCALS = %LOCALS%☆
	ENDIF
	PRINTFORM %LOCALS%
	RESETCOLOR
	PRINT ]
ELSEIF INSERT("插入中")
	IF INSERT("A插入中")
		PRINTFORM [肛门插入中(%CALLNAME:INSERT("插入中")%)]
	ELSEIF INSERT("尿道插入中")
		LOCALS = [尿道插入中(%CALLNAME:INSERT("插入中")%)]
	ELSE
		LOCALS = [阴道插入中(%CALLNAME:INSERT("插入中")%)]
	ENDIF
ENDIF
PRINTL  
;换装表示
CALL PRINT_DRESS, TARGET, "短缩表示"
PRINTL 

@SHOW_EQUIP_1
;使用中之道具を表示する
VARSET LOCALS
SIF TEQUIP:爱抚
	LOCALS = %LOCALS%[爱抚]
SIF TEQUIP:甜言蜜语 >= 1 && TEQUIP:甜言蜜语 <= 8
	LOCALS = %LOCALS%[甜言蜜语]
SIF TEQUIP:排卵诱发剂
	LOCALS = %LOCALS%[排卵诱发剂]

SELECTCASE TEQUIP:媚药
CASE IS >= 200
	LOCALS = %LOCALS%[媚药(激)]
CASE IS >= 150
	LOCALS = %LOCALS%[媚药(强)]
CASE IS > 50
	LOCALS = %LOCALS%[媚药]
CASE IS > 0
	LOCALS = %LOCALS%[媚药(弱)]
ENDSELECT

SIF TEQUIP:利尿剂
	LOCALS = %LOCALS%[利尿剂]
IF TEQUIP:肌肉松弛剂
	LOCALS = %LOCALS%[肌肉松弛剂
	SIF GETBIT(TEQUIP:肌肉松弛剂, 0)
		LOCALS = %LOCALS%(A)
	SIF GETBIT(TEQUIP:肌肉松弛剂, 1)
		LOCALS = %LOCALS%(尿道)
	SIF GETBIT(TEQUIP:肌肉松弛剂, 2)
		LOCALS = %LOCALS%(V)
	SIF GETBIT(TEQUIP:肌肉松弛剂, 3)
		LOCALS = %LOCALS%(子宮口)
	LOCALS = %LOCALS%]
ENDIF
IF TEQUIP:按摩棒 && TEQUIP:触手
	IF SIZE("按摩棒", TARGET) >= 3
		LOCALS = %LOCALS%[超粗触手插入]
	ELSE
		LOCALS = %LOCALS%[触手插入]
	ENDIF
ELSEIF TEQUIP:按摩棒
	IF COND("双头按摩棒")
		LOCALS = %LOCALS%[双头按摩棒]
	ELSE
		SELECTCASE SIZE("按摩棒", TARGET)
		CASE 5
			LOCALS = %LOCALS%[马尺寸按摩棒]
		CASE 4
			LOCALS = %LOCALS%[腕型按摩棒]
		CASE 3
			LOCALS = %LOCALS%[超粗按摩棒]
		CASEELSE
			LOCALS = %LOCALS%[按摩棒]
		ENDSELECT
	ENDIF
ENDIF

SIF TEQUIP:导尿管
	LOCALS = %LOCALS%[导尿管]

IF TEQUIP:肛用按摩棒 && TEQUIP:触手
	LOCALS = %LOCALS%[A触手插入]
ELSEIF TEQUIP:肛用按摩棒
	IF COND("肛珠")
		LOCALS = %LOCALS%[肛珠]
	ELSE
		SELECTCASE SIZE("肛用按摩棒", TARGET)
		CASE 5
			LOCALS = %LOCALS%[A马尺寸按摩棒]

		CASE 4
			LOCALS = %LOCALS%[A腕型按摩棒]
		CASE 3
			LOCALS = %LOCALS%[A超粗按摩棒]
		CASEELSE
			LOCALS = %LOCALS%[肛用按摩棒]
		ENDSELECT
	ENDIF
ENDIF

IF TEQUIP:阴蒂夹 && TEQUIP:触手
	LOCALS = %LOCALS%[触手蹂躏阴部]
ELSEIF TEQUIP:阴蒂夹
	LOCALS = %LOCALS%[阴蒂夹]
ENDIF

IF TEQUIP:乳头夹 && TEQUIP:触手
	LOCALS = %LOCALS%[触手蹂躏乳头]
ELSEIF TEQUIP:乳头夹
	LOCALS = %LOCALS%[乳头夹]
ENDIF

IF TEQUIP:搾乳机 && TEQUIP:触手
	LOCALS = %LOCALS%[触手搾乳]
ELSEIF TEQUIP:搾乳机
	LOCALS = %LOCALS%[搾乳器]
ENDIF

SIF TEQUIP:91 && TEQUIP:触手
	LOCALS = %LOCALS%[触手乳头奸中]

IF TEQUIP:飞机杯 && TEQUIP:触手
	LOCALS = %LOCALS%[触手蹂躏阴茎]
ELSEIF TEQUIP:飞机杯
	LOCALS = %LOCALS%[飞机杯]
ENDIF

SIF TEQUIP:跳蛋插入
	LOCALS = %LOCALS%[跳蛋插入中]

IF TEQUIP:拘束 && TEQUIP:触手
	LOCALS = %LOCALS%[触手紧缚]
ELSEIF TEQUIP:拘束
	LOCALS = %LOCALS%[%BINDS(TEQUIP:拘束)%]
ENDIF

IF COND("精液灌肠")
	LOCALS = %LOCALS%[精液灌肠]
ELSEIF TEQUIP:灌肠 && TEQUIP:触手
	LOCALS = %LOCALS%[触手灌肠]
ELSEIF TEQUIP:灌肠
	IF COND("温泉灌肠")
		LOCALS = %LOCALS%[温泉灌肠
	ELSEIF COND("空气灌肠")
		LOCALS = %LOCALS%[空气灌肠
	ELSE
		LOCALS = %LOCALS%[灌肠
	ENDIF
	SIF COND("肛用按摩棒")
		LOCALS = %LOCALS%＋肛用按摩棒
	SIF TEQUIP:扩张扩张气囊 == 2
		LOCALS = %LOCALS%＋A扩张扩张气囊
	LOCALS = %LOCALS%]
ELSEIF TEQUIP:扩张扩张气囊 == 2
	LOCALS = %LOCALS%[A扩张扩张气囊]
ENDIF

SIF TEQUIP:扩张扩张气囊 == 1
	LOCALS = %LOCALS%[V扩张扩张气囊]
SIF TEQUIP:触手口辱
	LOCALS = %LOCALS%[触手口辱]
SIF TEQUIP:眼罩
	LOCALS = %LOCALS%[眼罩]
IF TEQUIP:口枷 == 1
	LOCALS = %LOCALS%[球形口枷]
ELSEIF TEQUIP:口枷 == 2
	LOCALS = %LOCALS%[环形口枷]
ENDIF
SIF TEQUIP:淋浴
	LOCALS = %LOCALS%[淋浴]
IF TEQUIP:演奏中
	IF TEQUIP:演奏中 == 1
		LOCALS = %LOCALS%[平稳之歌]
	ELSEIF TEQUIP:演奏中 == 2
		LOCALS = %LOCALS%[罗曼司曲]
	ENDIF
ENDIF

SIF STRLENS(LOCALS)
	PRINTFORML 使用中(%CALLNAME:TARGET%):%LOCALS%


@STAIN_INFO
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0
PRINTL 
PRINTFORML 以下是各部位污物的状态。

SIF TEQUIP:逆推 == 0
	PRINTFORML 可根据需要清除手、口等部位的污物。
PRINTFORML （洗浴中・出浴状态下不容易沾上污物，易于实施侍奉类的调教）

;誰かが真性包茎
IF COND("真性包茎") || COND("真性包茎", MASTER) || COND("真性包茎", ASSI)
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORML ※真性包茎之人はPに常に<P恥垢>汚れが有ます。洗っても掃除しても落ちません
	RESETCOLOR
ENDIF

CALL STAIN_INFO_SHOW, MASTER
CALL STAIN_INFO_SHOW, TARGET
CALL STAIN_INFO_SHOW, ASSI
DRAWLINE
IF TEQUIP:逆推
	WAIT
ELSE
	PRINTBUTTON "[Enter] 关闭", "Enter"

	ONEINPUTS

	SELECTCASE RESULTS
	CASE "0"
		STAIN:MASTER:M = 0
	CASE "1"
		STAIN:MASTER:手 = 0
	CASE "2"
		IF CHECK_ADD_STAIN("P", MASTER)
			CALL RESET_STAIN_EX, "P", MASTER
			SIF INSERT("插入中", MASTER)
				CALL RESET_INSERT, MASTER
		ENDIF
	CASE "3"
		STAIN:M = 0
	CASE "4"
		STAIN:手 = 0
	CASE "5"
		IF CHECK_ADD_STAIN("P", TARGET)
			CALL RESET_STAIN_EX, "P", TARGET
			SIF INSERT("插入中", TARGET)
				CALL RESET_INSERT, TARGET
		ENDIF
	CASE "6"
		SIF ASSI
			STAIN:ASSI:M = 0
	CASE "7"
		SIF ASSI
			STAIN:ASSI:手 = 0
	CASE "8"
		IF ASSI
			IF CHECK_ADD_STAIN("P", ASSI)
				CALL RESET_STAIN_EX, "P", ASSI
				SIF INSERT("插入中", ASSI)
					CALL RESET_INSERT, ASSI
			ENDIF
		ENDIF
	CASEELSE
		REDRAW 1
		RETURN 0
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

REDRAW 1

@STAIN_INFO_SHOW, ARG
#DIM LCOUNT

SIF ARG <= 0
	RETURN 0
IF TEQUIP:ARG:浴室上り
	;ここはTARGET之值を现る
	IF TARGET && TEQUIP:浴室PLAY
		LOCALS = □ %NAME:ARG%（入浴中）□
	ELSE
		LOCALS = □ %NAME:ARG%（浴室上り）□
	ENDIF
ELSE
	LOCALS = □ %NAME:ARG% □
ENDIF
CALL DRAWLINES, LOCALS

;選択肢に使う
SELECTCASE ARG
CASE MASTER
	LOCAL = 0
CASE TARGET
	LOCAL = 3
CASE ASSI
	LOCAL = 6
ENDSELECT

FOR LCOUNT, 0, 6
	LOCALS = 
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "V")
		LOCALS += "<V>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "V恥垢")
		LOCALS += "<V恥垢>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "P")
		LOCALS += "<P>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "P恥垢")
		LOCALS += "<P恥垢>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "精液")
		LOCALS += "<精>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "A")
		LOCALS += "<A>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "乳")
		LOCALS += "<乳>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "尿")
		LOCALS += "<尿>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "触手")
		LOCALS += "<触手>"
	;SIF LCOUNT == 4 && (COND("外观：肠洗浄", ARG) || EX:ARG:排泄 >= 2)
	;	LOCALS += "（洗浄済み）"

	SELECTCASE LCOUNT
	CASE 0
		PRINTFORM 　口：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:逆推 == 0 && LOCALS != ""
			PRINTFORM [{LOCAL + LCOUNT}] 把嘴\@ ARG == MASTER ? 擦干净 # 擦干净 \@
	CASE 1
		PRINTFORM 　手：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:逆推 == 0 && LOCALS != ""
			PRINTFORM [{LOCAL + LCOUNT}] 擦手指
	CASE 2
		SIF PENIS(ARG) == 0
			CONTINUE
		PRINTFORM 　P：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:逆推 == 0 && CHECK_ADD_STAIN("P", ARG)
			PRINTFORM [{LOCAL + LCOUNT}] 擦干净阴茎 \@ INSERT("插入中", ARG) ? (会解除插入) # \@
	CASE 3
		SIF TALENT:ARG:性别 == 1
			CONTINUE
		PRINTFORM 　V：%TEXT_LJ(LOCALS, 32)%
	CASE 4
		PRINTFORM 　A：%TEXT_LJ(LOCALS, 32)%
	CASE 5
		;SIF TALENT:ARG:性别 == 1
		;	CONTINUE
		PRINTFORM 　B：%TEXT_LJ(LOCALS, 32)%
	ENDSELECT


	PRINTL 
NEXT
PRINTL 
