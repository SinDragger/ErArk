@COM_ARCANA, ARG
;秘法之咏唱

CALL PRINT_TRAIN_NAME(ARG)

CALL MAGIC_CASTER_SELECT, ARG

SIF REFUSE_CHECK()
	RETURN 0

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

CALL KOJO_MESSAGE_COM
;Ｗ逆推等で之口上处理
CALL KOJO_MESSAGE_COM_ASSI

CALL SETFLAG, "今回调教指令"

;-------------------------------------------------
;秘法之事件一括处理
;ARG=秘法番号、ARG:1=咏唱者、ARG:2=对象
;调教指令で呼ばれた際には@MAGIC_CASTER_SELECTを経る之でARG:1とARG:2は自动的に決定される(TCVAR:22とTCVAR:23から逆算)が、他之场所で呼ぶ際にはARG:1とARG:2は必ず指定すること
;文本出力にはPRINT_STRを用いる
;-------------------------------------------------
@TRAIN_MESSAGE_ARCANA, ARG, ARG:1, ARG:2
#DIM LCOUNT
;咏唱者
#DIM CASTER
;对象
#DIM MAGIC_TARGET
;出力する文本
#DIMS MESSAGE
#DIMS MESSAGE_RESIST

;秘法咏唱者标志（ARGは秘法番号）
IF ARG:1 > 0
	TCVAR:(ARG:1):23 = ARG
ELSE
	ARG:1 = FINDCHARA(TCVAR:23, ARG)
ENDIF
;秘法对象标志（ARGは秘法番号）
IF ARG:2 > 0
	TCVAR:(ARG:2):22 = ARG
ELSE
	ARG:2 = FINDCHARA(TCVAR:22, ARG)
ENDIF
;括弧を多用する之を避けるために代入
CASTER = ARG:1
MAGIC_TARGET = ARG:2


MESSAGE = 
MESSAGE_RESIST = 

SELECTCASE ARG
;秘法
CASE 230 TO 259
	MESSAGE = %MESSAGE%＜%TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)%＞_L_
CASE 312
	MESSAGE = %MESSAGE%＜具现＞_
ENDSELECT
SELECTCASE ARG
;束缚术
CASE 240
	SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
	CASE "束缚术"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%咏唱了%TRAINNAME:ARG%_W_
	CASE "试炼之根"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%的根须在地面上爬动！_L_%CALLNAME:MAGIC_TARGET%的身体被根须捉住了！_W_
	CASE "蛇尾缠卷"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%用尾巴缠住了%CALLNAME:MAGIC_TARGET%之全身！_W_
	CASE "女王之捆缚"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%用鞭子缠住了%CALLNAME:MAGIC_TARGET%！_W_
	CASE "缠绕之网"
		MESSAGE = %CALLNAME:CASTER%把丝线缠绕在%CALLNAME:MAGIC_TARGET%身上！_W_
	CASE "强缚术"
		MESSAGE = %CALLNAME:CASTER%强行压倒了%CALLNAME:MAGIC_TARGET%！_L_%CALLNAME:MAGIC_TARGET%的身体要被皮带捆住了！_W_
	CASE "石化之魔眼"
		MESSAGE = %CALLNAME:CASTER%用妖异的视线注视着%CALLNAME:MAGIC_TARGET%！_W_
	CASE "拘束代码"
		MESSAGE = %CALLNAME:CASTER%发射出无数的代码！_L_%CALLNAME:MAGIC_TARGET%的身体就要被束缚住了！_W_
	CASE "奇幻之旅"
		MESSAGE = %MESSAGE%「吹・ｖ黴・ゆ・ｯり等啓ｍ・凍・麾槙・・A金-黴・痘u・hｕ操÷逐枕装・br>哩循魔…！」_W_
		MESSAGE = %MESSAGE%不可思议的声音从%CALLNAME:CASTER%口中出现！_L_%CALLNAME:MAGIC_TARGET%眼前的景象渐渐扭曲！_W_
	ENDSELECT
	SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
	CASE "束缚术", "石化之魔眼"
		MESSAGE_RESIST = 对%CALLNAME:MAGIC_TARGET%没有效果！
	CASEELSE
		MESSAGE_RESIST = 被%CALLNAME:MAGIC_TARGET%迅速地挡开了！
	ENDSELECT
;秘法
CASE 230 TO 259
	MESSAGE = %MESSAGE%%CALLNAME:CASTER%咏唱了%TRAINNAME:ARG%…_W_
	MESSAGE_RESIST = 对%CALLNAME:MAGIC_TARGET%没有效果！
CASEELSE
	MESSAGE = %MESSAGE%%CALLNAME:CASTER%使用了%TRAINNAME:ARG%能力…_W_
	MESSAGE_RESIST = 对%CALLNAME:MAGIC_TARGET%没有效果！
ENDSELECT

IF CONDS("反击", CASTER) == "秘法"
	SELECTCASE ARG
	CASE 230, 231, 240, 243
		CALL PRINT_STR, MESSAGE
		MESSAGE = 
		PRINTFORML 试着抵抗吗？
		PRINTFORML [ 0] 是的
		PRINTFORML [ 1] 不要
		IF CONFIG("限制时间撤废")
			CALL INPUT_SELECT, 2
			IF RESULT == 0
				PRINTFORML ＜抗拒成功！＞
				PRINTFORMW %MESSAGE_RESIST%
				MESSAGE_RESIST = 抗拒
			ENDIF
		ELSE
			TINPUT MAX(4000 + 200*(ABL:MAGIC_TARGET:ＬＶ - ABL:CASTER:ＬＶ), 2000), 1, 1
			IF RESULT == 0 && LIMIT(70 + 3*(ABL:MAGIC_TARGET:ＬＶ - ABL:CASTER:ＬＶ), 50, 95) > RAND:100
				PRINTFORML ＜抗拒成功！＞
				PRINTFORMW %MESSAGE_RESIST%
				MESSAGE_RESIST = 抗拒
			ELSEIF RESULT == 0
				PRINTFORML ＜抗拒失败＞
			ENDIF
		ENDIF
	ENDSELECT
ENDIF

IF MESSAGE_RESIST != "抗拒"
	SELECTCASE ARG
	CASE 230
		IF STATE("大根术", MAGIC_TARGET)
			IF TALENT:MAGIC_TARGET:被动技：大根术 == 0
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的阴茎尺寸恢复了原状！_W_
				CALL REMOVE_STATE, MAGIC_TARGET, "大根术"
			ENDIF
		ELSE
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的阴茎竟然变小了！_W_
			CALL ADD_STATE, MAGIC_TARGET, "小根术"
		ENDIF
	CASE 231
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的身体变得敏感了！_W_
		CALL ADD_STATE, MAGIC_TARGET, "高敏术"
	CASE 232
		;PRINTFORM %CALLNAME:MAGIC_TARGET%的精力强化
		;SELECTCASE STATE("精强术", MAGIC_TARGET)
		;CASE 1
		;	PRINT 更进一步了
		;CASE 2
		;	PRINT 到达极限
		;ENDSELECT
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%之精力变强了！_W_
		CALL ADD_STATE, MAGIC_TARGET, "精强术"
	CASE 233
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的魅力提升了！_W_
		CALL ADD_STATE, MAGIC_TARGET, "魅强术"
	CASE 234
		;PRINTFORM %CALLNAME:MAGIC_TARGET%的精神力强化
		;SELECTCASE STATE("防强术", MAGIC_TARGET)
		;CASE 1
		;	PRINT 更进一步了
		;CASE 2
		;	PRINT 到达极限
		;ENDSELECT
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%之精神力变强了！_W_
		CALL ADD_STATE, MAGIC_TARGET, "防强术"
	CASE 235
		SIF BASE:MASTER:斯道克 < MAXBASE:MASTER:斯道克
			MESSAGE = %MESSAGE%%CALLNAME:MASTER%的弹药量回复了１！_W_
		SELECTCASE MAXBASE:MASTER:精力 - BASE:MASTER:精力
		CASE IS >= 20
			MESSAGE = %MESSAGE%%CALLNAME:MASTER%的精力回复了20！_W_
		CASE IS != 0
			MESSAGE = %MESSAGE%%CALLNAME:MASTER%的精力完全回复！_W_
		ENDSELECT
		CALL CALC, "精力回復", 20, MASTER
		BASE:MASTER:斯道克 = MIN(BASE:MASTER:斯道克 + 1, MAXBASE:MASTER:斯道克)
	CASE 236
		SELECTCASE MAXBASE:体力 - BASE:体力
		;回復するまでもない
		CASE IS <= 0
			MESSAGE = %MESSAGE%对%CALLNAME:MAGIC_TARGET%没有效果！_W_
		CASE IS <= 300
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的体力完全回复了！_W_
			CALL CALC, "体力回復", 300, MAGIC_TARGET
		CASEELSE
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的体力回复了300！_W_
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%体力+300 ({BASE:MAGIC_TARGET:体力} → {BASE:MAGIC_TARGET:体力 + 300} /{MAXBASE:MAGIC_TARGET:体力})_W_
		ENDSELECT
		CALL CALC, "体力回復", 300, MAGIC_TARGET
	CASE 237
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%能够连续行动了！_W_
		CALL ADD_STATE, MAGIC_TARGET, "倍化术"
	CASE 238
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%提升的能力恢复了原状！_W_
		CALL REMOVE_STATE, MAGIC_TARGET, "グン"
	CASE 239
		SIF STATE("束缚术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的身体解除了束缚！_W_
		SIF TALENT:MAGIC_TARGET:被动技：小根术 == 0 && STATE("小根术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的阴茎恢复了原来的尺寸！_W_
		SIF TALENT:MAGIC_TARGET:被动技：高敏术 == 0 && STATE("高敏术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%敏感的神经镇静下来了！_W_
		SIF STATE("快感术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%摆脱了幻觉！_W_
		IF STATE("恍惚", MAGIC_TARGET) || STATE("醉酒", MAGIC_TARGET) || STATE("烂醉", MAGIC_TARGET) || STATE("脱力", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%恢复了清醒！_W_
			;MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%脱离了逆推状态！_W_
		ENDIF
		SIF STATE("虚弱术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%取回了原本的实力！_W_
		CALL REMOVE_STATE, MAGIC_TARGET, "バッド"
	CASE 240
		IF CONDS("反击", CASTER) == "秘法"
			SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
			CASE "奇幻之旅"
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%失去了活动身体的自由！_W_
				CALL ADD_STATE, MAGIC_TARGET, "束缚术：手"
				CALL ADD_STATE, MAGIC_TARGET, "束缚术：脚"
			CASEELSE
				IF COND("指之使用", MAGIC_TARGET) && STATE("束缚术：手", MAGIC_TARGET) == 0
					MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的两手被封住了！_W_
					CALL ADD_STATE, MAGIC_TARGET, "束缚术：手"
				ENDIF
				IF COND("脚之使用", MAGIC_TARGET) && STATE("束缚术：脚", MAGIC_TARGET) == 0
					MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的两腿被封住了！_W_
					CALL ADD_STATE, MAGIC_TARGET, "束缚术：脚"
				ENDIF
				IF ABL:CASTER:Ｓ属性 && ABL:CASTER:欲望 >= 3 && PENIS(MAGIC_TARGET) && STATE("束缚术：Ｐ", MAGIC_TARGET) == 0 && TEQUIP:MAGIC_TARGET:31 == 0
					MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的阴茎被封住了！_W_

					SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
					CASE "束缚术"
						MESSAGE = %MESSAGE%受到看不见的缠绕，射精被阻止了！_W_
					CASE "石化之魔眼"
						MESSAGE = %MESSAGE%看不见的力量阻止了射精！_W_
						MESSAGE = %MESSAGE%而且，%CALLNAME:MAGIC_TARGET%的阴茎被变得如同石头一样坚硬！_W_
						CALL ADD_STATE, MAGIC_TARGET, "皮肤硬化"
					CASEELSE
						MESSAGE = %MESSAGE%因为受到缠绕，无法射精！_W_
					ENDSELECT

					CALL ADD_STATE, MAGIC_TARGET, "束缚术：Ｐ"
				ENDIF
			ENDSELECT
		ENDIF
	CASE 241
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%全能力提升！_W_
		CALL ADD_STATE, MAGIC_TARGET, "精强术"
		CALL ADD_STATE, MAGIC_TARGET, "魅强术"
		CALL ADD_STATE, MAGIC_TARGET, "防强术"
	CASE 242
		IF STATE("小根术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的阴茎大小恢复了原状！_W_
			CALL REMOVE_STATE, MAGIC_TARGET, "小根术"
		ELSE
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的阴茎竟然变大了！_W_
			CALL ADD_STATE, MAGIC_TARGET, "大根术"
		ENDIF
	CASE 243
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%被梦魇的幻觉侵袭！_W_
		CALL ADD_STATE, MAGIC_TARGET, "快感术"
	CASE 312
		IF STATE("具现", MAGIC_TARGET)
			CALL REMOVE_STATE, MAGIC_TARGET, "具现"
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%通过具现得到的阴茎消失了…_W_
			CALL SETFLAG, "具现化ＯＦＦ标志", MAGIC_TARGET
		ELSE
			CALL ADD_STATE, MAGIC_TARGET, "具现"
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%的股间竟然迅猛地长出了阴茎！_W_
			SELECTCASE SIZE("阴茎", MAGIC_TARGET)
			CASE IS >= 3
				MESSAGE = %MESSAGE%凶恶的大
			CASE 2
				MESSAGE = %MESSAGE%惹人喜爱的尺寸的
			ENDSELECT
			MESSAGE = %MESSAGE%阴茎在股间竟然迅猛地长出了_W_
			CALL EXP50_CHECK, MAGIC_TARGET
			CALL SETFLAG, "具现化ＯＮ标志", MAGIC_TARGET
		ENDIF
	ENDSELECT
ENDIF

BASE:CASTER:精力 -= MAGIC_EP(ARG, CASTER)
TCVAR:CASTER:109 += MAGIC_EP(ARG, CASTER)
MESSAGE = %MESSAGE%%CALLNAME:CASTER%之精力-{MAGIC_EP(ARG, CASTER)}   ({BASE:CASTER:精力}/{MAXBASE:CASTER:精力})_W_
CALL PRINT_STR, MESSAGE

;-------------------------------------------------
;秘法や特殊能力を管理する
;-------------------------------------------------
@MAGIC_CASTER_SELECT, ARG
SELECTCASE MAGIC_USER(ARG, "指令")
;唱えられる人は一人だけ
CASE 1, 2, 4
	IF ASSI && USE_MAGIC(ARG, "指令", ASSI)
		LOCAL = ASSI
	ELSEIF USE_MAGIC(ARG, "指令", MASTER)
		LOCAL = MASTER
	ELSEIF USE_MAGIC(ARG, "指令")
		LOCAL = TARGET
	ENDIF

	TCVAR:LOCAL:23 = ARG

	;唱える人がTARGET之时には止められる大人にしてみる
	IF LOCAL == TARGET
		PRINTFORML %CALLNAME:LOCAL%({BASE:LOCAL:精力}/{MAXBASE:LOCAL:精力})消费{MAGIC_EP(ARG, LOCAL)}精力\@ ARG >= 230 && ARG <= 259 ? 咏唱 # 使用 \@%TRAINNAME:ARG%。
		PRINTFORML 这样好吗？
		PRINTFORML  [ 0] 好的
		PRINTFORML  [ 1] 还是算了
		CALL INPUT_SELECT, 2
		IF RESULT == 1
			TFLAG:14 = 1
			RETURN 0
		ENDIF
	ENDIF
CASEELSE
	PRINTFORML 让谁\@ ARG >= 230 && ARG <= 259 ? 咏唱 # 使用 \@%TRAINNAME:ARG%呢？（%TEXTS("秘法解说", ARG)%）
	PRINTL 

	IF ASSI && USE_MAGIC(ARG, "指令", ASSI)
		LOCALS = %CALLNAME:ASSI%(助手)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% 精力({BASE:ASSI:精力}/{MAXBASE:ASSI:精力})　消费精力:{MAGIC_EP(ARG, ASSI)}
	ENDIF
	IF USE_MAGIC(ARG, "指令", MASTER)
		LOCALS = %CALLNAME:MASTER%(主人)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% 精力({BASE:MASTER:精力}/{MAXBASE:MASTER:精力})　消费精力:{MAGIC_EP(ARG, MASTER)}
	ENDIF
	SIF USE_MAGIC(ARG, "指令")
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% 精力({BASE:精力}/{MAXBASE:精力})　消费精力:{MAGIC_EP(ARG, TARGET)}
	PRINTL [100] 返回

	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && ASSI && USE_MAGIC(ARG, "指令", ASSI)
		TCVAR:ASSI:23 = ARG
	ELSEIF RESULT == 1 && USE_MAGIC(ARG, "指令", MASTER)
		TCVAR:MASTER:23 = ARG
	ELSEIF RESULT == 2 && USE_MAGIC(ARG, "指令")
		TCVAR:23 = ARG
	ELSE
		RESTART
	ENDIF
ENDSELECT

CALL MAGIC_TARGET_SELECT, ARG


;-------------------------------------------------
;誰に使用するかを選択
;-------------------------------------------------
@MAGIC_TARGET_SELECT, ARG
SELECTCASE TRAINNAME:ARG
;MASTER之み
CASE "斯道克"
	TCVAR:MASTER:22 = ARG
;TARGET之み
CASE "回复术", "解强化术", "快感术"
	TCVAR:22 = ARG

CASE "小根术"
	VARSET LOCAL

	;阴茎有＆大根术がかかっていない＆被动技：大根术でない ことが条件
	SIF ASSI && PENIS(ASSI) && STATE("小根术", ASSI) == 0 && TALENT:ASSI:被动技：大根术 == 0
		SETBIT LOCAL, 0
	SIF PENIS(MASTER) && STATE("小根术", MASTER) == 0 && TALENT:MASTER:被动技：大根术 == 0
		SETBIT LOCAL, 1
	SIF PENIS(TARGET) && STATE("小根术", TARGET) == 0 && TALENT:被动技：大根术 == 0
		SETBIT LOCAL, 2

	PRINTL 使谁当做对象?

	IF GETBIT(LOCAL, 0)
		LOCALS = %CALLNAME:ASSI%(助手)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% 现在：%CONDS("阴茎サイズ", ASSI)% \@ STATE("大根术", ASSI) ? 大根术影響中 # \@
	ENDIF
	IF GETBIT(LOCAL, 1)
		LOCALS = %CALLNAME:MASTER%(主人)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% 现在：%CONDS("阴茎サイズ", MASTER)% \@ STATE("大根术", MASTER) ? 大根术影響中 # \@
	ENDIF
	SIF GETBIT(LOCAL, 2)
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% 现在：%CONDS("阴茎サイズ")% \@ STATE("大根术", TARGET) ? 大根术影響中 # \@
	PRINTL [100] 返回
	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && GETBIT(LOCAL, 0)
		TCVAR:ASSI:22 = ARG
	ELSEIF RESULT == 1 && GETBIT(LOCAL, 1)
		TCVAR:MASTER:22 = ARG
	ELSEIF RESULT == 2 && GETBIT(LOCAL, 2)
		TCVAR:22 = ARG
	ELSE
		RESTART
	ENDIF
CASE "大根术"
	VARSET LOCAL

	SIF ASSI && PENIS(ASSI) && STATE("大根术", ASSI) == 0 && TALENT:ASSI:被动技：小根术 == 0
		SETBIT LOCAL, 0
	SIF PENIS(MASTER) && STATE("大根术", MASTER) == 0 && TALENT:MASTER:被动技：小根术 == 0
		SETBIT LOCAL, 1
	SIF PENIS(TARGET) && STATE("大根术", TARGET) == 0 && TALENT:被动技：小根术 == 0
		SETBIT LOCAL, 2

	PRINTL 使谁当做对象?

	IF GETBIT(LOCAL, 0)
		LOCALS = %CALLNAME:ASSI%(助手)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% 现在：%CONDS("阴茎サイズ", ASSI)% \@ STATE("小根术", ASSI) ? 小根术影響中 # \@
	ENDIF
	IF GETBIT(LOCAL, 1)
		LOCALS = %CALLNAME:MASTER%(主人)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% 现在：%CONDS("阴茎サイズ", MASTER)% \@ STATE("小根术", MASTER) ? 小根术影響中 # \@
	ENDIF
	SIF GETBIT(LOCAL, 2)
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% 现在：%CONDS("阴茎サイズ")% \@ STATE("小根术", TARGET) ? 小根术影響中 # \@
	PRINTL [100] 返回
	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && GETBIT(LOCAL, 0)
		TCVAR:ASSI:22 = ARG
	ELSEIF RESULT == 1 && GETBIT(LOCAL, 1)
		TCVAR:MASTER:22 = ARG
	ELSEIF RESULT == 2 && GETBIT(LOCAL, 2)
		TCVAR:22 = ARG
	ELSE
		RESTART
	ENDIF
CASE "具现"
	VARSET LOCAL
	SIF ASSI && (TALENT:ASSI:性别女 || STATE("具现", ASSI) )
		SETBIT LOCAL, 0
	SIF TALENT:MASTER:性别女 || STATE("具现", MASTER)
		SETBIT LOCAL, 1
	SIF TALENT:性别女 || STATE("具现", TARGET)
		SETBIT LOCAL, 2
	PRINTL 以谁为对象？
	IF GETBIT(LOCAL, 0)
		LOCALS = %CALLNAME:ASSI%(助手)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% \@ TALENT:ASSI:性别女 ? 性别女 # 具现中 \@
	ENDIF
	IF GETBIT(LOCAL, 1)
		LOCALS = %CALLNAME:MASTER%(主人)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% \@ TALENT:MASTER:性别女 ? 性别女 # 具现中 \@
	ENDIF
	SIF GETBIT(LOCAL, 2)
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% \@ TALENT:性别女 ? 性别女 # 具现中 \@
	PRINTL [100] 返回
	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && GETBIT(LOCAL, 0)
		TCVAR:ASSI:22 = ARG
	ELSEIF RESULT == 1 && GETBIT(LOCAL, 1)
		TCVAR:MASTER:22 = ARG
	ELSEIF RESULT == 2 && GETBIT(LOCAL, 2)
		TCVAR:22 = ARG
	ELSE
		RESTART
	ENDIF
;除此以外はPLAYER
CASEELSE
	TCVAR:PLAYER:22 = ARG
ENDSELECT


;-------------------------------------------------
;秘法之指令番号をARGで指定して、それを誰が使える之かをチェックする
;0bit ASSI, 1bit MASTER, 2bit TARGET
;ARGSは判定する时之状況で、指令選択、反击时で区别する
;-------------------------------------------------
@MAGIC_USER(ARG, ARGS)
#FUNCTION
#DIM LCOUNT
VARSET LOCAL

;誰が唱えられるかをチェック
;0bitでASSI咏唱可能、1bit でMASTER咏唱可能、2bitでTARGET咏唱可能
FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE MASTER, TARGET, ASSI
	CASEELSE
		CONTINUE
	ENDSELECT
	SIF USE_MAGIC(ARG, ARGS, LCOUNT) == 0
		CONTINUE

	SELECTCASE LCOUNT
	CASE ASSI
		SETBIT LOCAL, 0
	CASE MASTER
		SETBIT LOCAL, 1
	CASE TARGET
		SETBIT LOCAL, 2
	ENDSELECT
NEXT

RETURNF LOCAL

;-------------------------------------------------
;角色编号ARG:1が秘法之指令番号ARGを使える之かをチェックする
;ARGSは判定する时之状況で、指令選択、反击时で区别する
;-------------------------------------------------
@USE_MAGIC(ARG, ARGS, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
SIF TEQUIP:(ARG:1):眼罩 || TEQUIP:(ARG:1):拘束 || TEQUIP:(ARG:1):口枷 || TEQUIP:(ARG:1):演奏中
	RETURNF 0
;口か目が限制されてるとダメ
SIF COND("口之使用", ARG:1) == 0 || COND("视力", ARG:1) == 0
	RETURNF 0
;消费精力

SIF BASE:(ARG:1):精力 <= MAGIC_EP(ARG, ARG:1)
	RETURNF 0

SELECTCASE ARG
CASE 312
	SIF TALENT:(ARG:1):具现 == 0
		RETURNF 0
CASEELSE
	SIF TALENT:(ARG:1):ARG == 0 && TALENT:(ARG:1):魔法使 != 2
		RETURNF 0
ENDSELECT

SELECTCASE ARG:1
CASE ASSI, MASTER
	RETURNF 1
CASEELSE
	;反发刻印が1以下かつ顺从３ＬＶor屈服刻印２ＬＶ
	SELECTCASE ARGS
	CASE "指令"
		SELECTCASE TRAINNAME:ARG
		CASE "魅强术", "防强术", "倍化术", "全强术", "快感术"
			RETURNF 0
		CASE "大根术"
			IF COND("ベテラン：Ｖ性爱", ARG:1) && SIZE("阴茎", PLAYER) <= ABL:(ARG:1):Ｖ扩张
			ELSEIF COND("ベテラン：Ａ性爱", ARG:1) && SIZE("阴茎", PLAYER) <= ABL:(ARG:1):Ａ扩张
			ELSE
				RETURNF 0
			ENDIF
		CASE "解弱化术"
			SIF TEQUIP:(ARG:1):逆推 && MARK:(ARG:1):屈服刻印 <= 2
				RETURNF 0
		ENDSELECT
		SIF MARK:(ARG:1):反发刻印 >= 2
			RETURNF 0
		SIF ABL:(ARG:1):顺从 < 3 && MARK:(ARG:1):屈服刻印 < 2
			RETURNF 0
		RETURNF 1
	CASE "反击"
		;精力には余裕を５程度残す
		SIF BASE:(ARG:1):精力 < 5 + MAGIC_EP(ARG, ARG:1)
			RETURNF 0
		SELECTCASE TRAINNAME:ARG
		CASE "小根术"
			SIF SIZE("阴茎", PLAYER) >= MAX(2, ABL:(ARG:1):Ｖ扩张) && TALENT:(ARG:1):好色 == 0 && ABL:(ARG:1):顺从 < 3
				RETURNF 1
		CASE "高敏术"
			SIF STATE("高敏术", PLAYER) == 0
				RETURNF 1
		CASE "精强术"
			SIF STATE("精强术", ARG:1) < 3
				RETURNF 1
		CASE "魅强术"
			SIF STATE("魅强术", ARG:1) < 3
				RETURNF 1
		CASE "防强术"
			SIF STATE("防强术", ARG:1) < 3
				RETURNF 1
		CASE "回复术"
			SIF BASE:(ARG:1):体力 < MIN(1500, MAXBASE:(ARG:1):体力 - 300)
				RETURNF 1
		CASE "倍化术"
			SIF STATE("倍化术", ARG:1) == 0
				RETURNF 1
		CASE "解强化术"
			SIF STATE("グン", PLAYER) && TALENT:(ARG:1):好色 == 0 && ABL:(ARG:1):顺从 < 3 && MARK:(ARG:1):反发刻印
				RETURNF 1
		CASE "束缚术"
			SIF STATE("束缚术", PLAYER) == 0
				RETURNF 1
		CASE "全强术"
			SIF STATE("精强术", ARG:1) < 3
				RETURNF 1
			SIF STATE("魅强术", ARG:1) < 3
				RETURNF 1
			SIF STATE("防强术", ARG:1) < 3
				RETURNF 1
		CASE "大根术"
			SIF COND("ベテラン：Ｖ性爱", ARG:1) && SIZE("阴茎", PLAYER) < ABL:(ARG:1):Ｖ扩张
				RETURNF 1
			SIF COND("ベテラン：Ａ性爱", ARG:1) && SIZE("阴茎", PLAYER) < ABL:(ARG:1):Ａ扩张
				RETURNF 1
		CASE "快感术"
			SIF STATE("快感术", PLAYER) == 0
				RETURNF 1
		ENDSELECT
	ENDSELECT
ENDSELECT
RETURNF 0

;---------------------------------------------------------------------
;ARG:1がARG番之秘法を唱える之に必要な精力
;ARG:1が省略されている场合には基本消費量を返すが、ARG:1 > 0ならばARG:1が魔法使かどうかを现て消費量を减らす
;---------------------------------------------------------------------
@MAGIC_EP(ARG, ARG:1)
#FUNCTION
VARSET LOCAL
SELECTCASE ARG
CASE 230
	LOCAL = 2
CASE 231
	LOCAL = 4
CASE 232
	LOCAL = 3
CASE 233
	LOCAL = 4
CASE 234
	LOCAL = 3
CASE 235
	LOCAL = 30
CASE 236
	LOCAL = 8
CASE 237
	LOCAL = 4
CASE 238
	LOCAL = 3
CASE 239
	LOCAL = 7
CASE 240
	LOCAL = 8
CASE 241
	LOCAL = 9
CASE 242
	LOCAL = 4
CASE 243
	LOCAL = 8
CASE 312
	LOCAL = 8
ENDSELECT

SIF ARG:1 <= 0
	RETURNF LOCAL

;魔法使だと秘法使用时之疲労减少
SELECTCASE ARG
CASE 230 TO 259
	SELECTCASE TALENT:(ARG:1):魔法使
	CASE IS >= 2
		LOCAL -= MIN((LOCAL+1)/2, 10)
	CASE 1
		LOCAL = MAX(MULTIPLY(LOCAL, 75), 1)
	ENDSELECT
ENDSELECT

RETURNF LOCAL


@LEARNING_MAGIC, ARG, ARGS
#DIM LCOUNT

SELECTCASE ARGS
CASE "个人授业"
	PRINTL 
	PRINTFORML 教授哪一个？
CASEELSE
	PRINTL 
	PRINTFORML 学习哪一个？ - 习得之珠×{JUEL:ARG:习得}/{NUM("宝珠：秘法习得", ARG)}
ENDSELECT

VARSET LOCAL
FOR LCOUNT, 230, 230 + NUM("存在秘法")
	SIF TALENT:ARG:LCOUNT || TALENTNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE LCOUNT
	CASE 235, 240
		SIF ARG == MASTER
			CONTINUE
	ENDSELECT
	PRINTFORML  [{LCOUNT - 230, 2}] %TEXT_LJ(TALENTNAME:LCOUNT, 14)% %TEXTS("秘法解说", LCOUNT)%
	LOCAL:LCOUNT = 1
NEXT

PRINTFORML [100] 不学

$INPUT_LOOP
INPUT

LOCAL = RESULT + 230

IF RESULT == 100
	RETURN 0
ELSEIF RESULT >= 0 && RESULT < NUM("存在秘法") && LOCAL:LOCAL

ELSE
	GOTO INPUT_LOOP
ENDIF

SELECTCASE ARGS
CASE "个人授业"
	TALENT:ARG:LOCAL = 1
CASEELSE
	PRINTFORMW %NAME:ARG%习得了%TALENTNAME:LOCAL%。
	JUEL:ARG:习得 -= NUM("宝珠：秘法习得", ARG)
	TALENT:ARG:LOCAL = 1
	SIF COND("秘法习得可能", ARG)
		RESTART
ENDSELECT

RETURN LOCAL

@COM230
CALL COM_ARCANA, 230
RETURN 0

@TRAIN_MESSAGE_COM230
CALL TRAIN_MESSAGE_ARCANA, 230

@COM231
CALL COM_ARCANA, 231
RETURN 0

@TRAIN_MESSAGE_COM231
CALL TRAIN_MESSAGE_ARCANA, 231

@COM232
CALL COM_ARCANA, 232
RETURN 0

@TRAIN_MESSAGE_COM232
CALL TRAIN_MESSAGE_ARCANA, 232

@COM233
CALL COM_ARCANA, 233
RETURN 0

@TRAIN_MESSAGE_COM233
CALL TRAIN_MESSAGE_ARCANA, 233

@COM234
CALL COM_ARCANA, 234
RETURN 0

@TRAIN_MESSAGE_COM234
CALL TRAIN_MESSAGE_ARCANA, 234

@COM235
CALL COM_ARCANA, 235
RETURN 0

@TRAIN_MESSAGE_COM235
CALL TRAIN_MESSAGE_ARCANA, 235

@COM236
CALL COM_ARCANA, 236
RETURN 0

@TRAIN_MESSAGE_COM236
CALL TRAIN_MESSAGE_ARCANA, 236

@COM237
CALL COM_ARCANA, 237
RETURN 0

@TRAIN_MESSAGE_COM237
CALL TRAIN_MESSAGE_ARCANA, 237

@COM238
CALL COM_ARCANA, 238
RETURN 0

@TRAIN_MESSAGE_COM238
CALL TRAIN_MESSAGE_ARCANA, 238

@COM239
CALL COM_ARCANA, 239
RETURN 0

@TRAIN_MESSAGE_COM239
CALL TRAIN_MESSAGE_ARCANA, 239

@COM240
CALL COM_ARCANA, 240
RETURN 0

@TRAIN_MESSAGE_COM240
CALL TRAIN_MESSAGE_ARCANA, 240

@COM241
CALL COM_ARCANA, 241
RETURN 0

@TRAIN_MESSAGE_COM241
CALL TRAIN_MESSAGE_ARCANA, 241

@COM242
CALL COM_ARCANA, 242
RETURN 0

@TRAIN_MESSAGE_COM242
CALL TRAIN_MESSAGE_ARCANA, 242

@COM243
CALL COM_ARCANA, 243
RETURN 0

@TRAIN_MESSAGE_COM243
CALL TRAIN_MESSAGE_ARCANA, 243

@EQUIP_COM243
SIF STATE("快感术")
	CALL SOURCE_COM0, 80
IF ASSI && STATE("快感术", ASSI) && PLAYER != ASSI
	SWAP TARGET, ASSI
	CALL SOURCE_COM0, 80
	SWAP TARGET, ASSI
ENDIF
IF STATE("快感术", PLAYER)
	BASE:PLAYER:绝顶 += MAXBASE:PLAYER:绝顶/5
	TCVAR:PLAYER:101 += MAXBASE:PLAYER:绝顶/5
ENDIF
RETURN 1

@EQUIP_COM243_2
LOCALS = 
SIF STATE("快感术")
	LOCALS = %CALLNAME:TARGET%
SIF STATE("快感术", PLAYER)
	LOCALS = %LOCALS%\@ STATE("快感术") ? 和 # \@%CALLNAME:PLAYER%
SIF ASSI && STATE("快感术", ASSI) && PLAYER != ASSI
	LOCALS = %LOCALS%\@ STATE("快感术") + STATE("快感术", PLAYER) ? 和 # \@%CALLNAME:ASSI%

SIF LOCALS == ""
	RETURN 0

PRINTL ＜快感术＞
IF STATE("快感术", PLAYER)
	PRINTFORML %LOCALS%陷入了幻觉！
ELSE
	PRINTFORML %LOCALS%在幻觉的作用下发出娇喘！
ENDIF

RETURN 1

