;-------------------------------------------------
;各种标志を設定する関数
;LOCALSは基本的にSPLIT之みで使う
;-------------------------------------------------
@SETFLAG, ARGS, ARG, ARG:1, ARG:2
#DIM LCOUNT, 2
#DIM MEMO_LINECOUNT
#DIM DYNAMIC MEMO, 10
#DIMS DYNAMIC MEMOS, 10

MEMO_LINECOUNT = LINECOUNT

SELECTCASE ARGS
CASE "绝顶之記录"
	SIF ARG < 0
		RETURN 0
	SIF ARG == 0 && TARGET
		ARG = TARGET

	;どこ之绝顶かで記录するも之を取捨選択。まだ开发中

;ARG:1がARG:2之ARG（种别）を插入た
;"V插入阴茎", 3, TARGET, PLAYER　ならTARGETがPLAYER之大きさ3之阴茎を插入た场合となる
;関数之书式变更之可能性ある感じだ…
CASE "V插入阴茎", "V插入按摩棒", "V插入拳交", "V插入按摩棒"
	;ARG:2はマイナス之值を取り得る
	SIF ARG < 0 || ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET
		ARG:1 = TARGET
	SIF ARG:2 == 0 && PLAYER
		ARG:2 = PLAYER

	;插入たも之：{大きさ}＠入れた人＃{日付}＊{回数}
	SELECTCASE ARGS
	CASE "V插入阴茎"
		MEMOS = 阴茎：%TOSTR(ARG)%＠%MEMOS%＃%TOSTR(DAY)%＊
	CASE "V插入按摩棒"
		MEMOS = 按摩棒：%TOSTR(ARG)%＠%MEMOS%＃%TOSTR(DAY)%＊
	CASE "V插入拳交"
		MEMOS = 拳交：%TOSTR(ARG)%＠%MEMOS%＃%TOSTR(DAY)%＊
	CASE "V插入按摩棒"
		MEMOS = 按摩棒：%TOSTR(ARG)%＠%MEMOS%＃%TOSTR(DAY)%＊
	ENDSELECT

	VARSET LOCALS

	;記录を分解する
	SPLIT CSTR:(ARG:1):V插入履历, "/", LOCALS

	;MEMOS:9に一旦記录する
	MEMOS:9 = 

	;記录は最大80种程度（加算が起きたも之は先头に来る。つまり昔之对象と之記录は消える）
	FOR LCOUNT, 0, 80
		SIF LOCALS:LCOUNT == ""
			BREAK

		;今回と同じ对象之も之かどうか
		IF MEMOS != "" && MEMOS == SUBSTRING(LOCALS:LCOUNT, 0, STRLENS(MEMOS) )
			;～＊回数 となっている之で"回数"部分を抜き出して、１を加算する
			MEMOS:1 = %SUBSTRING(LOCALS:LCOUNT, STRLENS(MEMOS), -1)%
			LOCALS:LCOUNT = %MEMOS%%TOSTR(TOINT(MEMOS:1) + 1)%

			;MEMOSは加算して用済みな之で初期化
			MEMOS = 

			;記录する文字列を再構築
			MEMOS:9 = %LOCALS:LCOUNT%/%MEMOS:9%
		ELSE
			MEMOS:9 = %MEMOS:9%%LOCALS:LCOUNT%/
		ENDIF
		SIF STRLENS(MEMOS:9) > 1950
			BREAK
	NEXT

	;初次之对象ならMEMOSは文字列が入っている
	SIF MEMOS != ""
		MEMOS:9 = %MEMOS%1/%MEMOS:9%

	CSTR:(ARG:1):V插入履历 = %MEMOS:9%

	;确认用
	;PRINTFORML CSTR:V插入履历 = %CSTR:(ARG:1):V插入履历%

CASE "A插入阴茎", "A插入按摩棒", "A插入拳交", "A插入按摩棒"
	;ARG:2はマイナス之值を取り得る
	SIF ARG < 0 || ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET
		ARG:1 = TARGET
	SIF ARG:2 == 0 && PLAYER
		ARG:2 = PLAYER

	;入れた人をMEMOSで記录
	SELECTCASE ARG:2
	CASE -4
		MEMOS = 一般人
	CASE IS < 0
		RETURN 0
	CASEELSE
		MEMOS = 一般人
	ENDSELECT

	;插入たも之：{大きさ}＠入れた人＃{日付}＊{回数}
	SELECTCASE ARGS
	CASE "A插入阴茎"
		MEMOS = 阴茎：%TOSTR(ARG)%＠%MEMOS%＃%TOSTR(DAY)%＊
	CASE "A插入按摩棒"
		MEMOS = 按摩棒：%TOSTR(ARG)%＠%MEMOS%＃%TOSTR(DAY)%＊
	CASE "A插入拳交"
		MEMOS = 拳交：%TOSTR(ARG)%＠%MEMOS%＃%TOSTR(DAY)%＊
	CASE "A插入按摩棒"
		MEMOS = 按摩棒：%TOSTR(ARG)%＠%MEMOS%＃%TOSTR(DAY)%＊
	ENDSELECT

	VARSET LOCALS

	;記录を分解する
	SPLIT CSTR:(ARG:1):A插入履历, "/", LOCALS

	;MEMOS:9に一旦記录する
	MEMOS:9 = 

	;記录は最大80种程度（加算が起きたも之は先头に来る。つまり昔之对象と之記录は消える）
	FOR LCOUNT, 0, 80
		SIF LOCALS:LCOUNT == ""
			BREAK

		;今回と同じ对象之も之かどうか
		IF MEMOS != "" && MEMOS == SUBSTRING(LOCALS:LCOUNT, 0, STRLENS(MEMOS) )
			;～＊回数 となっている之で"回数"部分を抜き出して、１を加算する
			MEMOS:1 = %SUBSTRING(LOCALS:LCOUNT, STRLENS(MEMOS), -1)%
			LOCALS:LCOUNT = %MEMOS%%TOSTR(TOINT(MEMOS:1) + 1)%

			;MEMOSは加算して用済みな之で初期化
			MEMOS = 

			;記录する文字列を再構築
			MEMOS:9 = %LOCALS:LCOUNT%/%MEMOS:9%
		ELSE
			MEMOS:9 = %MEMOS:9%%LOCALS:LCOUNT%/
		ENDIF
		SIF STRLENS(MEMOS:9) > 1950
			BREAK
	NEXT

	;初次之对象ならMEMOSは文字列が入っている
	SIF MEMOS != ""
		MEMOS:9 = %MEMOS%1/%MEMOS:9%

	CSTR:(ARG:1):A插入履历 = %MEMOS:9%

	;确认用
	;PRINTFORML CSTR:A插入履历 = %CSTR:(ARG:1):A插入履历%

;ARG:1がARG:2にARG之量だけ中出等された
CASE "膣内精液経験", "肛内精液経験", "饮精経験", "乳内精液経験", "膀胱精液経験"
	;ARG:2はマイナス之值を取り得る
	SIF ARG < 0 || ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET
		ARG:1 = TARGET
	SIF ARG:2 == 0 && PLAYER
		ARG:2 = PLAYER

	IF ARG:1 == ARG:2
		PRINTFORM 受けと攻めが同じ人物です！申し訳ない之ですが出错報告をお願いします……
		FORCEWAIT
	ENDIF
	;精液之量をMEMO:1に代入する
	SELECTCASE ARG
	CASE 1
		MEMO:1 = 3 * MEMO
	CASE 2
		MEMO:1 = 5 * MEMO
	CASE 3
		MEMO:1 = 8 * MEMO
	CASE 4
		MEMO:1 = 12 * MEMO
	CASE 5
		MEMO:1 = 18 * MEMO
	CASE IS >= 6
		MEMO:1 = (ARG - 2) * 6 * MEMO
	ENDSELECT
	MEMO:1 = MULTIPLY(MEMO:1, 100 + RAND:21 - RAND:21)

	;PRINTFORML MEMOS = %MEMOS%, ARG = {ARG}, MEMO = {MEMO}, MEMO:1 = {MEMO:1}

	VARSET LOCALS

	SELECTCASE ARGS
	CASE "膣内精液経験"
		EXP:(ARG:1):膣内精液経験 += ARG * MEMO
		SIF TALENT:(ARG:1):孕娠
			BASE:(ARG:1):胎儿へ之栄養 += ARG * MEMO

		;孕娠用判定
		CALL CHECK_PREGNANT, ARG:1, ARG * MEMO, MEMOS

		IF COND("调教中")
			CALL ADD_STAIN, ARG:1, "V", "精液"
			EX:(ARG:1):V中出 += 1
			NOWEX:(ARG:1):V中出 += 1
			;液体追加之ソース
			SIF ARG:1 != MASTER
				SOURCE:(ARG:1):液体追加 += ARG * 100 * MEMO
		ENDIF

		;記录を分解する
		SPLIT CSTR:(ARG:1):11, "/", LOCALS
	CASE "肛内精液経験"
		EXP:(ARG:1):肛内精液経験 += ARG * MEMO

		IF COND("调教中")
			CALL ADD_STAIN, ARG:1, "A", "精液"
			EX:(ARG:1):A中出 += 1
			NOWEX:(ARG:1):A中出 += 1
			;液体追加之ソース
			SIF ARG:1 != MASTER
				SOURCE:(ARG:1):A液体追加 += ARG * 200 * MEMO
		ENDIF

		;記录を分解する
		SPLIT CSTR:(ARG:1):12, "/", LOCALS
	CASE "饮精経験"
		EXP:(ARG:1):饮精経験 += ARG * MEMO

		IF COND("调教中")
			CALL ADD_STAIN, ARG:1, "M", "精液"
			EX:(ARG:1):口内射精 += 1
			NOWEX:(ARG:1):口内射精 += 1
		ENDIF
		;記录を分解する
		SPLIT CSTR:(ARG:1):13, "/", LOCALS
	CASE "乳内精液経験"
		EXP:(ARG:1):乳内精液経験 += ARG * MEMO

		IF COND("调教中")
			CALL ADD_STAIN, ARG:1, "B", "精液"
			EX:(ARG:1):乳内射精 += 1
			NOWEX:(ARG:1):乳内射精 += 1
		ENDIF
		;記录を分解する
		SPLIT CSTR:(ARG:1):14, "/", LOCALS
	CASE "膀胱精液経験"
		EXP:(ARG:1):膀胱精液経験 += ARG * MEMO

		IF COND("调教中")
			EX:(ARG:1):尿道内射精 += 1
			NOWEX:(ARG:1):尿道内射精 += 1
			BASE:(ARG:1):尿意 += ARG * 500 * MEMO
		ENDIF
		;記录を分解する
		SPLIT CSTR:(ARG:1):15, "/", LOCALS
	ENDSELECT

	MEMOS = %MEMOS%：

	;MEMOS:9に一旦記录する
	MEMOS:9 = 

	;記录は３０人まで（加算が起きたも之は先头に来る。つまり30人越しになった对象と之記录は消える）
	FOR LCOUNT, 0, 30
		SIF LOCALS:LCOUNT == ""
			BREAK
		;今回と同じ对象之も之かどうか
		IF MEMOS != "" && MEMOS == SUBSTRING(LOCALS:LCOUNT, 0, STRLENS(MEMOS) )
			;对象：量 となっている之で"量"部分を抜き出して、今回之数值を加算する
			MEMOS:1 = %SUBSTRING(LOCALS:LCOUNT, STRLENS(MEMOS), -1)%
			LOCALS:LCOUNT = %MEMOS%%TOSTR(TOINT(MEMOS:1) + MEMO:1)%

			;MEMOSは加算して用済みな之で初期化
			MEMOS = 

			;記录する文字列を再構築
			MEMOS:9 = %LOCALS:LCOUNT%/%MEMOS:9%
		ELSE
			MEMOS:9 = %MEMOS:9%%LOCALS:LCOUNT%/
		ENDIF
	NEXT

	;初次之对象ならMEMOSは文字列が入っている
	SIF MEMOS != ""
		MEMOS:9 = %MEMOS%%TOSTR(MEMO:1)%/%MEMOS:9%

	SELECTCASE ARGS
	CASE "膣内精液経験"
		CSTR:(ARG:1):11 = %MEMOS:9%
	CASE "肛内精液経験"
		CSTR:(ARG:1):12 = %MEMOS:9%
	CASE "饮精経験"
		CSTR:(ARG:1):13 = %MEMOS:9%
	CASE "乳内精液経験"
		CSTR:(ARG:1):14 = %MEMOS:9%
	CASE "膀胱精液経験"
		CSTR:(ARG:1):15 = %MEMOS:9%
	ENDSELECT

	;确认用
	;PRINTFORML CSTR:11 = %CSTR:(ARG:1):11%
	;PRINTFORML CSTR:12 = %CSTR:(ARG:1):12%
	;PRINTFORML CSTR:13 = %CSTR:(ARG:1):13%
	;PRINTFORML CSTR:14 = %CSTR:(ARG:1):14%
	;PRINTFORMW CSTR:15 = %CSTR:(ARG:1):15%


CASE "中意的体位"
	SIF ARG < 0 || ARG:1 < 0
		RETURN 0

	;CSTR:2を使用する

	VARSET LOCALS
	;それぞれ之要素に分解。記录しておける要素数之最大值は90个(CSTRは全角で約1000文字ぐらいまで)
	SPLIT CSTR:2, "/", LOCALS
	;一旦CSTR:2を初期化
	CSTR:2 = 

	;感觉が上がるとコロコロ变わるだけになりがちな之で、变化する最大值は３とする
	ARG:1 = LIMIT(RAND:(ARG:1)/2, 1, 3)

	;記录された要素を、今回増える分だけ先に消去しておく
	FOR LCOUNT, 0, 90 - ARG:1
		CSTR:2 = %CSTR:2%%LOCALS:LCOUNT%/
	NEXT

	SELECTCASE ARG
	CASE 65, 75
		IF TEQUIP:逆推
			FOR LCOUNT, 0, ARG:1
				CSTR:2 = 逆推%TRAINNAME:ARG%/%CSTR:2%
			NEXT
		ELSE
			FOR LCOUNT, 0, ARG:1
				CSTR:2 = %TRAINNAME:ARG%/%CSTR:2%
			NEXT
		ENDIF
	CASE 83
		IF TEQUIP:逆推
			FOR LCOUNT, 0, ARG:1
				CSTR:2 = 逆推尿道骑乘位/%CSTR:2%
			NEXT
		ELSE
			FOR LCOUNT, 0, ARG:1
				CSTR:2 = %TRAINNAME:ARG%/%CSTR:2%
			NEXT
		ENDIF
	CASEELSE
		FOR LCOUNT, 0, ARG:1
			CSTR:2 = %TRAINNAME:ARG%/%CSTR:2%
		NEXT
	ENDSELECT

CASE "阴茎观察"
	SIF ARG < 0 || ARG:1 < 0
		RETURN 0
	;ARGに阴茎があるか
	SIF PENIS(ARG) == 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET
		ARG:1 = TARGET

	;初経験
	IF CSTR:(ARG:1):1 == ""
		CSTR:(ARG:1):1 = %CSTR:(ARG:1):1%
		RETURN 1
	ENDIF

	VARSET LOCALS
	;すでにARG之阴茎を观察済みかを调べる
	SPLIT CSTR:(ARG:1):1, "/", LOCALS

	;一旦CSTR:1を初期化
	CSTR:(ARG:1):1 = 

	;观察済みなら１にする
	LOCAL = 0

	FOR LCOUNT, 0, 100
		SIF LOCALS:LCOUNT == ""
			BREAK
		;观察済み之阴茎之持ち主が馆内に居る？
		SIF TO_NUMBER(TOINT(LOCALS:LCOUNT) ) > 0
			CSTR:(ARG:1):1 = %CSTR:(ARG:1):1%%LOCALS:LCOUNT%/
	NEXT

	SIF LOCAL == 0
		CSTR:(ARG:1):1 = %CSTR:(ARG:1):1%%TOSTR(BASE:ARG:识别番号)%/
CASE "包茎之皮を剥く"
	;ARGに假性包茎之阴茎があるか
	SIF COND("假性包茎", ARG) == 0
		RETURN 0
	SETBIT TALENT:ARG:阴茎, 20
CASE "包茎之皮を戻す"
	CLEARBIT TALENT:ARG:阴茎, 20
CASE "识别番号"
	;SIF BASE:ARG:识别番号
		RETURN 0
CASE "饮尿"
	IF ARG:1 == 0
		SELECTCASE ARG
		CASE TARGET
			ARG:1 = PLAYER
		CASEELSE
			ARG:1 = TARGET
		ENDSELECT
	ENDIF

	IF PENIS(ARG:1)
		STAIN:(ARG:1):P = SET_STAIN(STAIN:(ARG:1):P, "尿")
	ELSE
		STAIN:(ARG:1):V = SET_STAIN(STAIN:(ARG:1):V, "尿")
	ENDIF
	STAIN:ARG:M = SET_STAIN(STAIN:ARG:M, "尿")
	TEQUIP:(ARG:1):利尿剂 = 0
	BASE:(ARG:1):尿意 = 0
	NOWEX:(ARG:1):放尿 += 1
	SIF SELECTCOM != 144
		EX:(ARG:1):放尿 += 1
	EXP:(ARG:1):圣水経験 += 1
	EXP:ARG:饮尿経験 += 1
	;饮尿标志
	SETBIT TCVAR:ARG:10, 25

;打扫３种はARGがARG:1を打扫する
CASE "打扫口交"
	IF ARG:1 == 0
		SELECTCASE ARG
		CASE TARGET
			ARG:1 = PLAYER
		CASEELSE
			ARG:1 = TARGET
		ENDSELECT
	ENDIF

	;打扫口交标志
	SETBIT TCVAR:ARG:10, 4

	;A打扫口交标志
	SIF CHECK_STAIN(STAIN:(ARG:1):P, "A") && EXP_GAIN(16, ARG:1)
		SETBIT TCVAR:ARG:10, 5

	;尿打扫口交标志
	SIF CHECK_STAIN(STAIN:(ARG:1):P, "尿") && PREVEX("放尿", ARG:1)
		SETBIT TCVAR:ARG:10, 44

	;口交経験は他で得ているだろうがBＯＮＵSで更に１追加
	EXP:ARG:口交経験 += 1
	EXP:ARG:打扫口交経験 += 1
	SIF CHECK_STAIN(STAIN:(ARG:1):P, "精液")
		EXP:ARG:精液経験 += 1

	;ARG之口⇔ARG:1之P之汚れが移动
	CALL EQ_STAIN, ARG, ARG:1, 0, 2

	;PRINTFORML CHECK_STAIN(STAIN:(ARG:1):P, "P恥垢") = {CHECK_STAIN(STAIN:(ARG:1):P, "P恥垢")}
	;そ之后ARG:1之P之汚れリセット…P恥垢があると挙动が复杂
	IF CHECK_STAIN(STAIN:(ARG:1):P, "P恥垢")
		IF (ARG == PLAYER && CHECK_COM("恥垢打扫口交", 4) ) || (ARG == TARGET && CHECK_COM("恥垢打扫口交", 91) )
			EXP:ARG:打扫口交経験 += 1
			SETBIT TCVAR:ARG:10, 46
			CALL RESET_STAIN_EX, "P", ARG:1
		ELSE
			CALL RESET_STAIN_EX, "P", ARG:1
			STAIN:(ARG:1):P = SET_STAIN(STAIN:(ARG:1):P, "P恥垢")
		ENDIF
	ELSE
		CALL RESET_STAIN_EX, "P", ARG:1
	ENDIF

	;PRINTFORML CHECK_STAIN(STAIN:(ARG:1):P, "P恥垢") = {CHECK_STAIN(STAIN:(ARG:1):P, "P恥垢")}

	PALAM:(ARG:1):P润 += 500

CASE "打扫舐阴"
	IF ARG:1 == 0
		SELECTCASE ARG
		CASE TARGET
			ARG:1 = PLAYER
		CASEELSE
			ARG:1 = TARGET
		ENDSELECT
	ENDIF

	;打扫舐阴标志
	SETBIT TCVAR:ARG:10, 7

	;尿打扫舐阴标志
	SIF CHECK_STAIN(STAIN:(ARG:1):V, "尿") && PREVEX("放尿", ARG:1)
		SETBIT TCVAR:ARG:10, 45

	;舐阴経験は他で得ているだろうがBＯＮＵSで更に１追加
	EXP:ARG:舐阴経験 += 1
	EXP:ARG:打扫舐阴経験 += 1
	SIF CHECK_STAIN(STAIN:(ARG:1):V, "精液")
		EXP:ARG:精液経験 += 1

	;ARG之口⇔ARG:1之V之汚れが移动
	CALL EQ_STAIN, ARG, ARG:1, 0, 3

	;PRINTFORML CHECK_STAIN(STAIN:(ARG:1):V, "V恥垢") = {CHECK_STAIN(STAIN:(ARG:1):V, "V恥垢")}
	;そ之后ARG:1之V之汚れリセット…V恥垢があると挙动が复杂
	IF CHECK_STAIN(STAIN:(ARG:1):V, "V恥垢")
		IF (ARG == PLAYER && CHECK_COM("恥垢打扫舐阴", 1) ) || (ARG == TARGET && CHECK_COM("恥垢打扫舐阴", 98) )
			EXP:ARG:打扫舐阴経験 += 1
			SETBIT TCVAR:ARG:10, 47
			CALL RESET_STAIN_EX, "V", ARG:1
		ELSE
			CALL RESET_STAIN_EX, "V", ARG:1
			STAIN:(ARG:1):V = SET_STAIN(STAIN:(ARG:1):V, "V恥垢")
		ENDIF
	ELSE
		CALL RESET_STAIN_EX, "V", ARG:1
	ENDIF

	;PRINTFORML CHECK_STAIN(STAIN:(ARG:1):V, "V恥垢") = {CHECK_STAIN(STAIN:(ARG:1):V, "V恥垢")}

CASE "打扫舐A"
	IF ARG:1 == 0
		SELECTCASE ARG
		CASE TARGET
			ARG:1 = PLAYER
		CASEELSE
			ARG:1 = TARGET
		ENDSELECT
	ENDIF

	;打扫舐A标志
	SETBIT TCVAR:ARG:10, 43

	;舐A经验は他で得ているだろうがBＯＮＵSで更に１追加
	EXP:ARG:舐A经验 += 1
	EXP:ARG:打扫舐A经验 += 1
	SIF CHECK_STAIN(STAIN:(ARG:1):A, "精液")
		EXP:ARG:精液経験 += 1

	;ARG之口⇔ARG:1之A之汚れが移动
	CALL EQ_STAIN, ARG, ARG:1, 0, 4
	;そ之后ARG:1之A之汚れリセット
	CALL RESET_STAIN_EX, "A", ARG:1
CASE "饮用母乳"
	SIF ARG:1 == 0 && TARGET
		ARG:1 = TARGET
	STAIN:(ARG:1):B = SET_STAIN(STAIN:(ARG:1):B, "母乳")
	STAIN:ARG:M = SET_STAIN(STAIN:ARG:M, "母乳")
	CALL EQ_STAIN, ARG, ARG:1, 0, 5
	BASE:(ARG:1):喷乳 = LIMIT(BASE:(ARG:1):喷乳 - 7000, 0, MAXBASE:(ARG:1):喷乳)
	EXP:(ARG:1):授乳経験 += 1
	EXP:ARG:饮乳経験 += 1
	;小孩子は経験值獲得
	SIF COND("成长可能", ARG)
		EXP:ARG:経験值 += 5
	;精力も回復
	IF MAXBASE:ARG:精力 > BASE:ARG:精力
		BASE:ARG:精力 = LIMIT(BASE:ARG:精力 + 1 + TALENT:(ARG:1):吸精体質, 0, MAXBASE:ARG:精力)
		TCVAR:ARG:109 += 1 + TALENT:(ARG:1):吸精体質
		PRINTFORML 精力+{1 + TALENT:(ARG:1):吸精体質} (%CALLNAME:ARG%)
	ENDIF
CASE "大被同眠"
	SETBIT CFLAG:95, 3
	SETBIT CFLAG:MASTER:95, 3
CASE "Ｗ逆推"
	;调教者が助手之时には禁止とする
	SIF TEQUIP:Ｗ逆推 || ASSI <= 0 || PLAYER != MASTER
		RETURN 0
	TEQUIP:逆推 = 1
	TEQUIP:ASSI:逆推 = 1
	;Ｗ逆推状态（アクティブな方が1）
	TEQUIP:Ｗ逆推 = 1
	TEQUIP:ASSI:Ｗ逆推 = 2
	;助手之前回指令に相当
	PREVCOM:1 = -1
	;摄影は止めちゃう
	TEQUIP:摄像机 = 0
CASE "Ｗ逆推解除"
	SIF ASSI <= 0
		RETURN 0
	CVARSET TEQUIP, 30, 0
	CVARSET TEQUIP, 32, 0
	SELECTCOM:1 = -1
	PREVCOM:1 = -1
	SIF INSERT("插入对象", ASSI) == PLAYER
		CALL RESET_INSERT, ASSI, PLAYER
CASE "射精＆绝顶チェック"
	LOCAL = PLAYER
	PLAYER = ARG
	;高潮管理されてる？
	TEQUIP:ARG:高潮管理 = MAX(TEQUIP:ARG:高潮管理 - 1, 0)

	SIF TFLAG:90 >= 1 && TFLAG:90 <= 9
		TCVAR:ARG:2 = 1

	;射精を堪えられる快感を算出
	BASE:ARG:快感许容值男 += TCVAR:ARG:101

	;高潮管理状态
	IF TEQUIP:ARG:高潮管理 > 0
		TCVAR:ARG:1 = 0
	ELSEIF COND("射精禁止中", ARG)
		TCVAR:ARG:1 = 0
	ELSE
		TCVAR:ARG:1 = CALCF("绝顶", ARG)
	ENDIF

	SIF COND("绝顶", ARG) == 0
		TCVAR:ARG:3 = 0

	;TARGETとASSI(これはＷ逆推之时)之绝顶标志
	IF COND("绝顶", ARG)
		TCVAR:ARG:5 = CALCF("绝顶之原因", TARGET, SELECTCOM)
		;二人で逆推た时之助手に对する射精标志
		TCVAR:ARG:6 = CALCF("绝顶之原因", ASSI, SELECTCOM:1)
	ENDIF
	PLAYER = LOCAL
CASE "射精＆绝顶事件标志"
	LOCALS = 
	IF SHOOT("冲击")
		LOCALS = 绝顶：冲击
	ELSEIF SHOOT("脚")
		LOCALS = 绝顶：脚
	ELSEIF SHOOT("素股")
		LOCALS = 绝顶：素股
	ELSEIF SHOOT("口")
		LOCALS = 绝顶：口
	ELSEIF SHOOT("胸")
		LOCALS = 绝顶：胸
	ELSEIF SHOOT("手")
		LOCALS = 绝顶：手
	ELSEIF SHOOT("V中出")
		LOCALS = 绝顶：V中出
	ELSEIF SHOOT("A中出")
		LOCALS = 绝顶：A中出
	ELSEIF SHOOT("接吻")
		LOCALS = 绝顶：接吻
	ELSEIF SHOOT("顔面骑乘")
		LOCALS = 绝顶：顔面骑乘
	ELSEIF SHOOT("两乳夹头")
		LOCALS = 绝顶：两乳夹头
	ELSEIF SHOOT("逆舐阴")
		LOCALS = 绝顶：逆舐阴
	ELSEIF SHOOT("逆强暴")
		LOCALS = 绝顶：逆强暴
	ELSEIF SHOOT("逆肛交")
		LOCALS = 绝顶：逆肛交
	ELSEIF SHOOT("舐肛奉侍")
		LOCALS = 绝顶：舐肛奉侍
	ELSEIF SHOOT("助手")
		LOCALS = 绝顶：助手
	ELSEIF SHOOT("调教者が性别女")
		LOCALS = 绝顶：调教者が性别女
	ENDIF

	SIF LOCALS != ""
		CALL SET_EV, TARGET, LOCALS
CASE "奉仕嗜虐判定"
	;TCVAR:29之值によって奉仕or嗜虐快乐を決める
	;0bit弱奉仕, 1bit奉仕, 2bit弱嗜虐, 3bit嗜虐
	SELECTCASE ARG
	CASE 6, 177
		SETBIT TCVAR:29, 0
	CASE 1, 4, 9, 11, 144, 145
		SIF TEQUIP:逆推
			SETBIT TCVAR:29, 3
	CASE 95, 121, 126
		IF TEQUIP:逆推
			SETBIT TCVAR:29, 3
		ELSE
			SETBIT TCVAR:29, 0
			SETBIT TCVAR:29, 2
		ENDIF
	CASE 65, 75, 80, 90 TO 107, 120, 125, 150
		IF TEQUIP:逆推
			SETBIT TCVAR:29, 2
		ELSE
			SETBIT TCVAR:29, 1
		ENDIF
	CASE 15, 176, 178, 280
		SETBIT TCVAR:29, 2
	CASE 122, 127, 140
		SETBIT TCVAR:29, 0
	CASE 138
		SIF CHECK_COM("口内放尿") && CHECK("口内放尿")
			SETBIT TCVAR:29, 1
	ENDSELECT
CASE "排泄"
	IF COND("液体灌肠") + COND("精液灌肠")
		IF COND("肛塞")
			PRINTFORM 拔掉栓塞之后，%CALLNAME:TARGET%
			SELECTCASE BASE:排泄物
			CASE 0
				PRINTFORML 体内先前注入的液体带着%TEXTR("肠内的", "强烈的")%臭味，从肛门喷射了出来。
			CASE IS <= 2
				PRINTFORML 从肛门喷撒出了肠内被\@ COND("温泉灌肠") ? 大量的小便 # 灌肠液 \@稀释过的污物。
			CASE IS <= 6
				PRINTFORML 肠内充满着的大量\@ CHECK_STAIN(STAIN:A, "精液") ? 混合着精液的 # \@污物从肛门激烈地喷射了出来。
			CASEELSE
				PRINTFORML 在灌肠的刺激下，排泄出了大量\@ CHECK_STAIN(STAIN:A, "精液") ? 混合着精液 # \@的软粪，随后又一边放屁，一边从肛门拉出了有点便秘的硬块，积成了小山样的一堆。
			ENDSELECT
		ENDIF
		SELECTCASE MIN(ABL:M属性, (ABL:露阴癖 + 1)/2)
		CASE 0
			PRINTFORMW %CALLNAME:TARGET%露出苦闷和耻辱的表情…
		CASE 1
			PRINTFORMW %CALLNAME:TARGET%在\@ PLAYER == MASTER && TALENT:恋慕 ? 深爱的人 # 别人 \@面前排粪，在羞耻的同时，也感到了些微的成就感…
		CASE 2
			PRINTFORMW %CALLNAME:TARGET%在感到难为情的同时，浮现出了如释重负的表情…
		CASE 3
			PRINTFORMW %CALLNAME:TARGET%品味着排泄物通过肛门的感觉，不时地发出娇艳的喘息声…
		CASE 4
			PRINTFORMW %CALLNAME:TARGET%精神恍惚地享受着在\@ PLAYER == MASTER && TALENT:恋慕 ? 最爱的人 # 别人 \@面前排泄的恥辱与解放感…
		CASE IS >= 5
			PRINTFORML %CALLNAME:TARGET%尽情地品尝着排泄感与羞耻心。
			IF TALENT:性别 == 1 || INSERT("V插入") || RAND:2
				PRINTFORMW 沉浸在快感中，露出了松弛淫荡的表情，放出大量污物的肛门满溢着解放感…
			ELSE
				PRINTFORMW 沉浸在快感中，露出了松弛淫荡的表情。从肛门放出大量污物的同时，腔口也滴答滴答地流出了爱液…
			ENDIF
		ENDSELECT
		;排泄标志
		IF COND("精液灌肠")
			NOWEX:排泄 = 1
		ELSE
			NOWEX:排泄 = COND("液体灌肠")
		ENDIF
		EX:排泄 += NOWEX:排泄
	ELSE
		PRINTFORMW 一拔掉栓塞，%CALLNAME:TARGET%的菊穴便发出了惊人的放屁声…
	ENDIF
	TEQUIP:肛用按摩棒 = 0
	CLEARBIT TEQUIP:扩张扩张气囊, 1
	TEQUIP:灌肠 = 0
	PALAM:A润 /= 2
	BASE:排泄欲 = 0
	BASE:排泄物 = 0
	;汚れリセット
	STAIN:A = SET_STAIN(0, "A")
CASE "调教者交代"
	;本格的に交代したならポジション解消
	IF ARG
		;前Ｔ调教者はリセット
		TFLAG:50 = 0
		CVARSET TEQUIP, 1, 0
		CVARSET TEQUIP, 69, 0
	ENDIF
	INVERTBIT ASSIPLAY, 0
	SWAP PLAYER, PLAYER:1
	SWAP SELECTCOM, SELECTCOM:2
	SWAP PREVCOM, PREVCOM:2
CASE "对象交代"
	;交代したならポジション解消
	;前Ｔ对象はリセット
	TFLAG:49 = 0
	IF PLAYER == ASSI
		PLAYER = TARGET
	ELSE
		PLAYER:1 = TARGET
	ENDIF
	;插入标志解除
	IF TEQUIP:Ｗ逆推 == 0
		CVARSET TEQUIP, 20, 0
		CVARSET TEQUIP, 21, 0
		CVARSET TEQUIP, 22, 0
	ENDIF
	CVARSET TEQUIP, 69, 0
	;Ｗ逆推之立场
	SWAP TEQUIP:Ｗ逆推, TEQUIP:ASSI:Ｗ逆推
	;前回实行指令を入れ替え
	SWAP SELECTCOM, SELECTCOM:1
	SWAP PREVCOM, PREVCOM:1
	SWAP TARGET, ASSI
	;３P解消
	SWAP SELECTCOM:2, SELECTCOM:3
	SWAP PREVCOM:2, PREVCOM:3

CASE "尿道扩张"
	;ARGに插入されるも之之大きさが代入されている
	;尿道扩张度之要素によって异なる挙动が起きる
	SIF CSTR:3 == "" || CSTR:4 == ""
		RETURN 0
	;最小之要素以下之太さなら扩张は起きない
	SIF ARG <= MIN_STR(CONDS("尿道扩张度") )
		RETURN 0

	;LOCALは扩张する進行度
	SELECTCASE ABL:PLAYER:技巧
	CASE IS >= 7
		LOCAL = 3
	CASE IS >= 4
		LOCAL = 2
	CASEELSE
		LOCAL = 1
	ENDSELECT

	;TALENT:扩张补正や调教知识(性器扩张)があると扩张する之が格段に乐
	;调教知识によるBＯＮＵS
	SELECTCASE TALENT_NAME(185, TALENT:PLAYER:调教知识, PLAYER)
	CASE "调教知识(性器扩张)"
		LOCAL += 4
	ENDSELECT

	;连续实行
	SIF PREVCOM == SELECTCOM
		LOCAL = LOCAL*3 + 1

	;尿道扩张之壁にぶつかると扩张する速度が急速に钝化
	SELECTCASE CALCF("尿道径")
	CASE IS >= CALCF("尿道扩张之限界")*6/5
		RETURN 0
	CASE IS >= CALCF("尿道扩张之限界")
		LOCAL /= 4
	CASE IS >= CALCF("尿道扩张之壁")
		LOCAL /= 2
	ENDSELECT

	SIF CONFIG("尿道を扩张し易く")
		LOCAL = 1 + LOCAL*3

	SIF LOCAL <= 0
		RETURN 0

	VARSET LOCALS
	;どっち之尿道か
	IF PENIS(TARGET)
		;それぞれ之要素に分解
		SPLIT CSTR:4, "/", LOCALS
	ELSE
		;それぞれ之要素に分解
		SPLIT CSTR:3, "/", LOCALS
	ENDIF

	FOR LCOUNT:1, 0, LOCAL
		;LOCALSはLOCALS:0～LOCALS:9まで
		FOR LCOUNT, 0, 10
			;文字列を数字化して比较
			SIF TOINT(LOCALS:LCOUNT) > MIN_STR(CONDS("尿道扩张度") )
				CONTINUE
			SIF ARG <= TOINT(LOCALS:LCOUNT)
				CONTINUE

			LOCALS:LCOUNT = {ARG}
			BREAK
		NEXT
	NEXT

	EXP:尿道扩张経験 += 1

	;どっち之尿道か
	IF PENIS(TARGET)
		CSTR:4 = 
		FOR LCOUNT, 0, 10
			CSTR:4 = %CSTR:4%%LOCALS:LCOUNT%/
		NEXT
	ELSE
		CSTR:3 = 
		FOR LCOUNT, 0, 10
			CSTR:3 = %CSTR:3%%LOCALS:LCOUNT%/
		NEXT
	ENDIF
CASE "V扩张", "A扩张"
	;ARGに插入されるも之之大きさ、ARG:1に硬さが代入されている

	VARSET LOCAL
	;调教知识によるBＯＮＵS(损伤しづらくなる)
	SELECTCASE TALENT_NAME(185, TALENT:PLAYER:调教知识, PLAYER)
	CASE "调教知识(性器扩张)"
		LOCAL -= 1
	ENDSELECT

	;まずは损伤を现る
	SELECTCASE ARGS
	CASE "V扩张"
		LOCAL += ARG - ABL:V扩张 - 1
		IF ARG:1 <= 2 || TALENT:史莱姆 || SELECTCOM == 106 || CUP:苦痛 < 3000 || (SELECTCOM == 304 && TEQUIP:逆推)
		ELSEIF TALENT:V损伤 < LOCAL
			SIF EXP:V损伤経験 < LOCAL
				EXP:V损伤経験 = LOCAL

			TALENT:V损伤 = LOCAL
			;Ｈ氛围激减
			TFLAG:33 -= 30 * LOCAL
		ENDIF
	CASE "A扩张"
		LOCAL += ARG - ABL:A扩张 - 1
		IF ARG:1 <= 2 || TALENT:史莱姆 || SELECTCOM == 107 || CUP:苦痛 < 3000 || (SELECTCOM == 305 && TEQUIP:逆推)
		ELSEIF TALENT:A损伤 < LOCAL
			SIF EXP:A损伤経験 < LOCAL
				EXP:A损伤経験 = LOCAL

			TALENT:A损伤 = LOCAL
			;Ｈ氛围激减
			TFLAG:33 -= 30 * LOCAL
		ENDIF
	ENDSELECT

	;续いて扩张経験之獲得
	SIF ARG < 2 - TALENT:妖精
		RETURN 0

	SELECTCASE ARGS
	CASE "V扩张"
		LOCAL:1 = ARG - ABL:V扩张
		;EXP:V扩张经验が0-4なら5, 5-19なら20,…と返す
		LOCAL:2 = EXP_LV(GET_EXP_LV(EXP:V扩张经验) + 1)
		LOCAL:3 = 53
	CASE "A扩张"
		LOCAL:1 = ARG - ABL:A扩张
		LOCAL:2 = EXP_LV(GET_EXP_LV(EXP:A扩张经验) + 1)
		LOCAL:3 = 57
	ENDSELECT

	;插入するも之之大きさ-扩张LVを现る。值が大きいほど扩张経験増加量が多い
	;なお、(插入するも之之大きさ-扩张LV)が１以下之场合で插入するも之が柔らかい时(硬さが２以下)は扩张経験増加は１まで
	SELECTCASE LOCAL:1
	CASE IS <= -2
		IF ARG >= 4
			LOCAL = 1
		ELSE
			RETURN 0
		ENDIF
	CASE -1
		IF ARG >= 3
			LOCAL = 1
		ELSE
			RETURN 0
		ENDIF
	CASE 0
		LOCAL = 1
	CASE 1
		;四捨五入
		LOCAL = (LOCAL:2 + 5)/10
		;插入するも之之硬さ
		SELECTCASE ARG:1
		CASE 1
			LOCAL = 1
		CASE 2
			LOCAL = MIN(LOCAL, 2)
		ENDSELECT
	CASE 2
		LOCAL = (LOCAL:2 + 1)/3
	CASE IS >= 3
		LOCAL = LOCAL:2
	ENDSELECT

	;硬い场合には大きさがかなり関係する
	SIF ARG:1 >= 3
		LOCAL = MAX(LOCAL, ARG)

	LOCAL = LIMIT(LOCAL, 1, 20)

	;大きく上がる之は一度之调教で一度だけ
	;基本的には入れるも之之大きさ之绝对值に依存する
	IF EXP_GAIN(LOCAL:3)
		;大きさごとに、２度目以降之上升时に上げられる扩张経験之最大值が決まっている
		;(大きさ, 上限) = (1, 1), (2, 5), (3, 20), …, (n, EXP_LV(n - 1) )
		SIF EXP:(LOCAL:3) >= MAX(EXP_LV(ARG - 1), 1) && ARG <= 2 - TALENT:妖精
			RETURN 0

		SELECTCASE LOCAL:1
		CASE IS <= -1
			IF ARG >= 3 && ARG:1 >= 3
				LOCAL = 1
			ELSE
				RETURN 0
			ENDIF
		CASE 0, 1
			IF ARG:1 >= 3
				LOCAL = MAX(LOCAL/2, 1)
			ELSE
				LOCAL /= 2
			ENDIF
		CASE IS >= 2
			;插入するも之之硬さ
			SELECTCASE ARG:1
			CASE 1
				LOCAL = 1
			CASE 2
				LOCAL = MIN(LOCAL, 2)
			ENDSELECT
		ENDSELECT

		;(大きさ之LV, 上限) = (1, 1), (2, 2), (3, 5), (4, 10), (5, 20), …, (n, 5n)
		SELECTCASE ARG
		CASE 1
			LOCAL = MIN(LOCAL, 1)
		CASE 2
			LOCAL = MIN(LOCAL, 2)
		CASE 3
			LOCAL = MIN(LOCAL, 5)
		CASE 4
			LOCAL = MIN(LOCAL, 10)
		CASEELSE
			LOCAL = MIN(LOCAL, ARG*4)
		ENDSELECT
	ENDIF

	;调教知识による経験獲得之BＯＮＵS
	SELECTCASE TALENT_NAME(185, TALENT:PLAYER:调教知识, PLAYER)
	CASE "调教知识(性器扩张)"
		LOCAL *= 2
	ENDSELECT

	;扩张経験増加
	EXP:(LOCAL:3) += LOCAL
CASE "P润变动处理"
	SIF ARG == 0 && PLAYER > 0
		ARG = PLAYER
	SIF PENIS(ARG) == 0
		RETURN 0
	;插入中之増减量獲得は@COMF60内之@FLAG_INSERTIONで行う
	;插入以外だと3000ぐらいが限度
	IF INSERT("插入中", ARG) == 0
		SELECTCASE SELECTCOM
		;口交
		CASE 91, 99, 140, 150
			SIF PALAM:ARG:P润 < 3000
				CUP:ARG:P润 += (ABL:技巧 + ABL:侍奉精神 + ABL:舌技)*100
		;素股系
		CASE 93, 94
			SIF PALAM:ARG:P润 < 3000
				CUP:ARG:P润 += PALAM:V润 / 3
		;助手性交
		CASE 160
			CUP:ARG:P润 += PALAM:ASSI:V润 / 4
		;助手口交
		CASE 162
			SIF PALAM:ARG:P润 < 3000
				CUP:ARG:P润 += (ABL:ASSI:技巧 + ABL:ASSI:侍奉精神 + ABL:ASSI:舌技)*100
		;飞机杯
		CASE 105
			SIF PALAM:ARG:P润 < 3000
				CUP:ARG:P润 += 1000
		CASEELSE
			IF COND("打扫口交标志")
				SIF PALAM:ARG:P润 < 3000
					CUP:ARG:P润 += (ABL:技巧 + ABL:侍奉精神 + ABL:舌技)*100
			ELSEIF CUP:ARG:P润 == 0
				CDOWN:ARG:P润 += MIN(200, PALAM:ARG:P润)
			ENDIF
		ENDSELECT
	ENDIF

	;こ之回合之BASE:绝顶増分に应じてカウパーで润滑追加
	CUP:ARG:P润 += 500 * TCVAR:ARG:101 / MAXBASE:ARG:绝顶
	IF TALENT:ARG:容易湿
		CUP:ARG:P润 = UP_PALAMLV(CUP:ARG:P润)
	ELSEIF TALENT:ARG:不易湿
		CUP:ARG:P润 = DOWN_PALAMLV(CUP:ARG:P润)
	ENDIF
	PALAM:ARG:P润 = LIMIT(PALAM:ARG:P润 + CUP:ARG:P润 - CDOWN:ARG:P润, 0, 10000)

CASE "润滑追加"
	SIF ARG:1 == 0 && TARGET
		ARG:1 = TARGET
	SIF TALENT:(ARG:1):性别男 == 0
		CUP:(ARG:1):V润 += ARG
	CUP:(ARG:1):A润 += ARG
	SIF PENIS(ARG:1)
		CUP:(ARG:1):P润 += ARG
CASE "调教外润滑追加"
	SIF ARG:1 == 0 && TARGET
		ARG:1 = TARGET
	SIF TALENT:(ARG:1):性别男 == 0
		PALAM:(ARG:1):V润 += ARG
	PALAM:(ARG:1):A润 += ARG
	SIF PENIS(ARG:1)
		PALAM:(ARG:1):P润 += ARG

CASE "内衣姿になる"
	CALL SET_SHIRT, ARG
	CALL SET_BODYS, ARG
	CALL SET_OUTER, ARG
	CALL SET_SOCKS, ARG
	CALL SET_GLOVE, ARG
	CALL SET_SHOES, ARG
CASE "衣装全脱ぎ"
	CALL SET_PANTIES, ARG
	CALL SET_BRA, ARG
	CALL SET_SHIRT, ARG
	CALL SET_BODYS, ARG
	CALL SET_OUTER, ARG
	CALL SET_SOCKS, ARG
	CALL SET_GLOVE, ARG
	CALL SET_SHOES, ARG
	CALL SET_OTHER, ARG
CASE "脱光"
	CALL SET_PANTIES, ARG
	CALL SET_BRA, ARG
	CALL SET_SHIRT, ARG
	CALL SET_BODYS, ARG
	CALL SET_OUTER, ARG
CASE "露出胸部"
	SWAP TARGET, ARG
	IF EQUIP:紧身衣
		SIF CHECK_BODYS("可裸露")
			CALL CALC, "紧身衣标志追加", SETFLAG_BODYS("はだけた")
	ENDIF
	IF EQUIP:上着
		SIF CHECK_SHIRT("可裸露")
			CALL CALC, "上着标志追加", SETFLAG_SHIRT("はだけた")
	ENDIF
	IF EQUIP:ブラ
		SIF CHECK_BRA("可裸露")
			CALL CALC, "ブラ标志追加", SETFLAG_BRA("はだけた")
	ENDIF
	SWAP TARGET, ARG
CASE "ボトムずれ直し"
	CLEARBIT EQUIP:ARG:紧身衣, 61
	CLEARBIT EQUIP:ARG:外套, 61
	CLEARBIT EQUIP:ARG:胖次, 61
	TCVAR:ARG:26 = 0

CASE "ボトムを脱がす", "下半身を脱がす"
	CALL SET_PANTIES, ARG
	CALL SET_BODYS, ARG
	CALL SET_OUTER, ARG

CASE "ボトムを１枚ずらす"
	SWAP TARGET, ARG
	;紧身衣、ずらしていないか拉链开けてない
	IF EQUIP:紧身衣 && CHECK_CLO("紧身衣露出")
		IF CHECK_BODYS("拉链閉")
			PRINTFORM 拉下%BODYS(TARGET, 1)%股间的拉链
			CALL CALC, "紧身衣标志追加", SETFLAG_BODYS("拉链开")
		ELSE
			IF CHECK_CLO("紧勒") && RAND:2 == 0
				PRINTFORM 陷进了股间
			ELSE
				PRINTFORM %BODYS(TARGET, 1)%的
			ENDIF
			PRINT 挪动股布
			CALL CALC, "紧身衣标志追加", SETFLAG_BODYS("ずらした")
		ENDIF
	ELSEIF EQUIP:外套 && CHECK_OUTER("拉链閉")
		PRINTFORM 拉下%OUTER(TARGET, 1)%股间的拉链
		CALL CALC, "外套标志追加", SETFLAG_OUTER("拉链开")
	;外套、ずらしていないか穴が无い
	ELSEIF EQUIP:外套 && CHECK_CLO("运动短裤露出")
		CALL CALC, "外套标志追加", SETFLAG_OUTER("ずらした")
		IF EQUIP:胖次 && CHECK_PANTIES("能撥开")
			PRINTFORM 连内衣一起
			CALL CALC, "胖次标志追加", SETFLAG_PANTIES("ずらした")
		ENDIF
		PRINTFORM 挪开%OUTER(TARGET, 1)%
	;连裤袜(破ってない)
	ELSEIF EQUIP:外套 && CHECK_CLO("连裤袜露出")
		PRINTFORM 撕破%OUTER(TARGET, 1)%
		CALL CALC, "外套标志追加", SETFLAG_OUTER("可撕")
	;胖次、ずらしていないか拉链开けてない
	ELSEIF EQUIP:胖次 && CHECK_CLO("胖次露出")
		IF CHECK_PANTIES("拉链閉")
			PRINTFORM 拉下%PANTIES(TARGET, 1)%的拉链
			CALL CALC, "胖次标志追加", SETFLAG_PANTIES("拉链开")
		ELSE
			IF CHECK_PANTIES("丁字裤") && (SELECTCOM == 2 || SELECTCOM == 33 || (SELECTCOM >= 70 && SELECTCOM <= 79) )
				PRINT 挪动遮住肛门的带子
			ELSE
				PRINTFORM 挪开%PANTIES(TARGET, 1)%
			ENDIF
			CALL CALC, "胖次标志追加", SETFLAG_PANTIES("ずらした")
		ENDIF
	ENDIF
	SWAP TARGET, ARG
CASE "下部露出：文本有", "下部露出：文本无"
	SWAP TARGET, ARG
	VARSET LOCALS
	IF EQUIP:紧身衣
		IF CHECK_BODYS("拉链閉")
			CALL CALC, "紧身衣标志追加", SETFLAG_BODYS("拉链开")
			LOCALS = 拉下拉链
		ELSEIF CHECK_BODYS("能撥开") && !CHECK_BODYS("拉链开")
			CALL CALC, "紧身衣标志追加", SETFLAG_BODYS("ずらした")
		ENDIF
	ENDIF
	IF EQUIP:外套
		IF CHECK_OUTER("拉链閉")
			CALL CALC, "外套标志追加", SETFLAG_OUTER("拉链开")
			LOCALS = 拉下拉链
		ELSEIF PENIS(TARGET) && !CHECK_OUTER("拉链开")
			CALL SET_OUTER, TARGET
			LOCALS = 脱下%OUTER(TARGET, 1)%
		ELSEIF CHECK_OUTER("能撥开") && !CHECK_OUTER("拉链开")
			CALL CALC, "外套标志追加", SETFLAG_OUTER("ずらした")
		ELSEIF CHECK_OUTER("破ける")
			CALL CALC, "外套标志追加", SETFLAG_OUTER("可撕")
			LOCALS = 撕破%OUTER(TARGET, 1)%
		ENDIF
	ENDIF
	IF EQUIP:胖次
		IF CHECK_PANTIES("拉链閉")
			CALL CALC, "胖次标志追加", SETFLAG_PANTIES("拉链开")
			LOCALS = 拉下拉链
		ELSEIF PENIS(TARGET) && !CHECK_PANTIES("拉链开")
			CALL SET_PANTIES, TARGET
			LOCALS = 脱下%PANTIES(TARGET, 1)%
		ELSEIF CHECK_PANTIES("能撥开") && !CHECK_PANTIES("拉链开")
			CALL CALC, "胖次标志追加", SETFLAG_PANTIES("ずらした")
		ENDIF
	ENDIF

	IF ARGS == "下部露出：文本有"
		IF LOCALS != ""
			PRINTFORM %LOCALS%
		ELSEIF RAND:2 || TALENT:性别 == 1
			PRINTFORM 露出股间
		ELSE
			PRINTFORM 露出重要的部分
		ENDIF
	ENDIF

	;"下部露出させた"标志ＯＮ
	SETBIT TCVAR:10, 21

	SWAP TARGET, ARG

CASE "人物初始化"
	VARSET TCVAR:ARG:0, 0, 0, 30
	;回合初期化
	TCVAR:ARG:150 = 0
	;前之回合之插入など之状态初期化
	TCVAR:ARG:151 = 0
	;逆推确率之补正を初期化
	TCVAR:ARG:152 = 0

	;欲情值的获取
	LOCAL = EXP:ARG:绝顶経験 + BASE:ARG:欲求不满 * 20 + ABL:ARG:自慰成瘾 * 100 + RAND:100 - RAND:100
	LOCAL -= TALENT:ARG:反抗心*50 + TALENT:ARG:冷静*30 + TALENT:ARG:抵抗*30 + TALENT:ARG:否认快感*30
	LOCAL += TALENT:ARG:接受快感*30 + TALENT:ARG:好色*50 + TALENT:ARG:淫乱*100 + TALENT:ARG:恋慕*50
	LOCAL += COND("外观：V扩张", ARG)*20 + COND("外观：A扩张", ARG)*10

	SELECTCASE ABL:ARG:欲望
	CASE 2
		LOCAL += 10
	CASE 3
		LOCAL += 20
	CASE 4
		LOCAL += 40
	CASE 5
		LOCAL += 80
	ENDSELECT

	SELECTCASE CFLAG:ARG:2
	CASE IS >= 2500
		LOCAL += 300
	CASE IS >= 1500
		LOCAL += 200
	CASE IS >= 500
		LOCAL += 100
	ENDSELECT

	SIF TEQUIP:ARG:媚药
		LOCAL += 30 * TEQUIP:ARG:媚药

	LOCAL = LOCAL * (CFLAG:ARG:2 + 1000) / 2000

	SELECTCASE LOCAL
	;MAX 1000
	CASE IS < 3000
		LOCAL = LOCAL/3
	;MAX 2166
	CASE IS < 10000
		LOCAL = LOCAL/6 + 500
	CASEELSE
		LOCAL = LOCAL/12 + 1333
	ENDSELECT

	PALAM:ARG:欲情 += LIMIT(LOCAL, 0, 30000)

	;-------------------------------------------------
	;爱液处理(欲情)
	;-------------------------------------------------
	IF TALENT:ARG:性别 != 1
		LOCAL = 0
		;欲情之值に应じてV润増加
		SELECTCASE PALAM:ARG:欲情
		;MAX100
		CASE IS < 500
			LOCAL += PALAM:ARG:欲情 / 5
		;MAX500
		CASE IS < 3000
			LOCAL += (PALAM:ARG:欲情 - 500) * 4 / 25 + 100
		;MAX3000
		CASE IS < 10000
			LOCAL += (PALAM:ARG:欲情 - 3000) * 5 / 14 + 500
		CASEELSE
			LOCAL += 3000
		ENDSELECT

		LOCAL += MARK:ARG:淫紋*100

		;按摩棒を入れているなら+大きさ*100
		LOCAL += COND("外观：V扩张", ARG) * 100

		;史莱姆判定
		SIF TALENT:ARG:史莱姆
			LOCAL += 3000
		;魔导人偶判定
		SIF TALENT:ARG:魔导人偶
			LOCAL += 500

		IF TALENT:ARG:容易湿
			LOCAL = UP_PALAMLV(LOCAL)
		ELSEIF TALENT:ARG:不易湿
			LOCAL = DOWN_PALAMLV(LOCAL)
		ENDIF

		PALAM:ARG:V润 += LIMIT(LOCAL, 0, 6000)
	ENDIF


	IF COND("外观：A润滑液", ARG) && TEQUIP:ARG:润滑液 == 0 && (ITEM:润滑液 || MONEY >= ITEMPRICE:润滑液)
		PALAM:ARG:A润 += 10000
		SETBIT TEQUIP:ARG:润滑液, 0
		IF ITEM:润滑液
			ITEM:润滑液 -= 1
		ELSE
			MONEY -= ITEMPRICE:润滑液
		ENDIF
		RETURN 0
	ENDIF
	;史莱姆だけBＯＮＵS
	SIF TALENT:ARG:史莱姆
		PALAM:ARG:A润 += LIMIT(LOCAL, 0, 6000)

	LOCAL = 0
	;いくつか之条件でA润追加
	IF TALENT:ARG:肠液分泌体質
		;欲情之值に应じてA润増加
		SELECTCASE PALAM:ARG:欲情
		CASE IS < 500
		;MAX100
		CASE IS < 3000
			LOCAL += (PALAM:ARG:欲情 - 500) / 25
		;MAX500
		CASE IS < 10000
			LOCAL += (PALAM:ARG:欲情 - 3000) * 2 / 35 + 100
		CASEELSE
			LOCAL += 500
		ENDSELECT
	ENDIF
	;肛用按摩棒を入れているなら+大きさ*100
	LOCAL += COND("外观：A扩张", ARG) * 100

	;史莱姆
	IF TALENT:ARG:史莱姆
		PALAM:ARG:A润 = MAX(LOCAL, PALAM:ARG:A润)
	ELSE
		PALAM:ARG:A润 += LIMIT(LOCAL, 0, 6000)
	ENDIF

CASE "调教开始前B"
	;开始前之経験を記憶
	CALL RESET_EXP_GAIN, ARG
	;满足チェックをリセット
	CFLAG:ARG:99 = 0

	;以下はMASTERでは設定不要
	SIF ARG == MASTER
		RETURN 0

	CALL RESET_CHOSECOM, ARG
	CALL GET_EXABL, ARG
	CALL GET_EXTALENT, ARG
	;SOURCEとCUPにかかる补正を再计算
	CALL CALC, "调教补正值", ARG
CASE "回合开始"
	VARSET NOWEX:ARG:0
	VARSET TCVAR:ARG:0, 0, 0, 30
	VARSET TCVAR:ARG:0, 0, 120, 140
	VARSET CUP:ARG:0
	VARSET SOURCE:ARG:0
	VARSET DOWNBASE:ARG:0
	VARSET UP
	;换装部分之高速化标志消去
	TCVAR:ARG:26 = 0
	;EXP之増分表示之ために、现在之值を記录
	FOR LCOUNT, 0, 100
		TCVAR:ARG:(LCOUNT + 700) = EXP:ARG:LCOUNT
	NEXT
	SIF PENIS(ARG) == 0
		CALL RESET_STAIN_EX, "P", ARG
	;特殊なPLAY之初期化
	;狂野摇摆
	SIF COND("前Ｔ调教者") != PLAYER || COND("前Ｔ对象") != TARGET
		TEQUIP:ARG:特殊行动 = 0
	;双枪拳交
	SELECTCASE SELECTCOM
	CASE 141 TO 143

	CASEELSE
		CLEARBIT TEQUIP:ARG:特殊行动, 1
	ENDSELECT
	;能量吸收
	CLEARBIT TEQUIP:ARG:特殊行动, 2
	;等级吸取
	CLEARBIT TEQUIP:ARG:特殊行动, 3
	;两穴观察
	SELECTCASE SELECTCOM
	CASE 13, 14

	CASEELSE
		CLEARBIT TEQUIP:ARG:特殊行动, 4
	ENDSELECT
CASE "回合終了"
	;今回之指令で得たUPとNOWEXを保存し、SOURCE之合计值(TCVAR:120～139)を初期化する
	CALL SET_PREVUP, ARG
	CALL SET_PREVEX, ARG
	VARSET TCVAR:ARG:0, 0, 120, 140

	CALL SETFLAG, "属性", ARG
	CALL GET_EXTALENT, ARG
	CALL GET_EXABL2, ARG
	SIF PENIS(ARG) == 0
		CALL RESET_STAIN_EX, "P", ARG
	;回合追加
	SIF ARG == TARGET || ARG == MASTER || SELECTCOM:1 >= 0 || SELECTCOM:2 >= 0
		TCVAR:ARG:150 += 1
	;逆推确率补正之变动
	SIF TCVAR:ARG:152 > 0 && TEQUIP:ARG:逆推 == 0
		TCVAR:ARG:152 -= 1
	IF CONFIG("精液灌肠") && (COND("精液灌肠", ARG) || NOWEX:ARG:A中出) && BASE:ARG:排泄物 && COND("液体灌肠", ARG) < 2 && COND("空气灌肠", ARG) == 0
		BASE:ARG:排泄欲 += NOWEX:ARG:A中出*500 + NOWEX:ARG:A绝顶*200 + NOWEX:ARG:绝顶数合计*100
		;精液灌肠标志ＯＮ
		TEQUIP:ARG:灌肠 = 1p10
	ENDIF
	;いま之ところ、尿道ブジーは取り除く
	TEQUIP:ARG:尿道ブジー = 0
	;润滑液按摩
	IF GETBIT(TEQUIP:ARG:润滑液, 1)
		CLEARBIT TEQUIP:ARG:润滑液, 1
		SETBIT TEQUIP:ARG:润滑液, 2
	ENDIF
	;绝顶しているなら、连续绝顶コンボ数を变化させる
	IF NOWEX:ARG:绝顶数合计
		PALAM:ARG:连续绝顶COMBO数 += (NOWEX:ARG:C绝顶 + NOWEX:ARG:尿道绝顶 > 0) + (NOWEX:ARG:V绝顶 > 0) + (NOWEX:ARG:A绝顶 > 0) + (NOWEX:ARG:B绝顶 > 0) + (NOWEX:ARG:M绝顶 > 0)
		PALAM:ARG:补正值 += NOWEX:ARG:绝顶数合计
		EX:ARG:连续绝顶 = MAX(EX:ARG:连续绝顶, PALAM:ARG:连续绝顶COMBO数)
	;绝顶しなかった场合コンボ終了
	ELSE
		PALAM:ARG:连续绝顶COMBO数 = 0
		PALAM:ARG:补正值 /= 2
	ENDIF
	;换装部分之高速化标志消去
	TCVAR:ARG:26 = 0
	;こ之回合之插入等之状态を記录する
	CALL SET_PREVPLAY, ARG
	VARSET NOWEX:ARG:0
	VARSET TCVAR:ARG:0, 0, 0, 30
	VARSET TCVAR:ARG:0, 0, 120, 140
	VARSET CUP:ARG:0
	VARSET SOURCE:ARG:0
	VARSET DOWNBASE:ARG:0
	VARSET UP
CASE "射精槽"
		LOCAL = 10000
	;早泄or晚泄
	IF TALENT:ARG:早泄
		LOCAL = MAX(LOCAL/(TALENT:ARG:早泄 + 1), 5000)
	ELSEIF TALENT:ARG:晚泄
		LOCAL *= TALENT:ARG:晚泄 + 1
	ENDIF
	;镇静剂
	SIF GETBIT(TEQUIP:ARG:药品, 0)
		LOCAL += MAX(MULTIPLY(LOCAL, 40), 10000)
	SIF STATE("高敏术", ARG)
		LOCAL = MIN(LOCAL/2, 10000)

	;射精槽決定
	MAXBASE:ARG:绝顶 = LOCAL
	;装饰品效果（EP）
	MAXBASE:ARG:绝顶 = MULTIPLY(MAXBASE:ARG:绝顶, 100 + TALENT:ARG:EP强化)


	;射精量之決定基准…高敏术状态等でない限りはだいたい１万orMAXBASE:绝顶。射精槽が増えると勿論増えていく。
	LOCAL = MAX(MULTIPLY(MAXBASE:ARG:绝顶, 70), 10000)

	EJAC:ARG = MIN(LOCAL, MAXBASE:ARG:绝顶)
	;射精时快感许容值
	MAXBASE:ARG:快感许容值男 = EJAC:ARG * 3
CASE "性欲槽"
	BASE:ARG:性欲 = EX:ARG:绝顶数合计 + EX:ARG:射精
	SELECTCASE ABL:ARG:欲望
	CASE 0
		MAXBASE:ARG:性欲 = 0
		BASE:ARG:性欲 = 0
	CASE IS <= 2
		MAXBASE:ARG:性欲 = MAX(ABL:ARG:欲望, 1)
	CASE 3
		MAXBASE:ARG:性欲 = 3 + TALENT:ARG:好色 + TALENT:ARG:淫乱 + TALENT:ARG:梦魇 + TALENT:ARG:上位梦魇*2
	CASE 4
		MAXBASE:ARG:性欲 = 5 + TALENT:ARG:好色*2 + TALENT:ARG:淫乱*2 + TALENT:ARG:梦魇*2 + TALENT:ARG:上位梦魇*4
	CASE IS >= 5
		MAXBASE:ARG:性欲 = 7 + TALENT:ARG:好色*3 + TALENT:ARG:淫乱*3 + TALENT:ARG:梦魇*3 + TALENT:ARG:上位梦魇*6
	ENDSELECT
	MAXBASE:ARG:性欲 += MARK:ARG:淫紋*2
CASE "精液欲槽"
	MAXBASE:ARG:精液欲 = (ABL:ARG:精液成瘾 + TALENT:ARG:梦魇*2 + TALENT:ARG:上位梦魇*4)*3 + MARK:ARG:淫紋*2
	BASE:ARG:精液欲 = EXP_GAIN(20, ARG)
	IF ABL:精液成瘾 + (TALENT:梦魇+TALENT:上位梦魇 > 0)*2 < 3
		MAXBASE:ARG:精液欲 = 0
		BASE:ARG:精液欲 = 0
	ENDIF
CASE "体勢戻し"
	;顔面骑乘管理
	SELECTCASE SELECTCOM
	CASE 1, 9, 99, 144, 282
	;奉仕系指令は实行值依存
	CASE 90 TO 92, 97, 105
		SIF TALENT:性别 == 1 || (TCVAR:28 < 30 && ABL:欲望 + ABL:S属性 < 5)
			TEQUIP:顔面骑乘 = 0
	CASE 95
		SIF PENIS(PLAYER) == 0 || (TCVAR:28 < 30 && ABL:欲望 + ABL:S属性 < 5)
			TEQUIP:顔面骑乘 = 0
	CASEELSE
		TEQUIP:顔面骑乘 = 0
	ENDSELECT

	;六九式管理
	SIF CHECK_EQUIP_69(SELECTCOM) == 0
		CVARSET TEQUIP, 69, 0

	;深喉口交管理
	SELECTCASE SELECTCOM
	CASE 91, 140, 150
		;标志とTEQUIPが分かれている之は、射精后などにTEQUIP:深喉口交が０となっている时でも参照するため
		SIF TEQUIP:深喉口交
			CALL SETFLAG, "深喉口交标志", TARGET
	CASE 138
		SIF CHECK_COM("口内放尿") && TEQUIP:深喉口交
			CALL SETFLAG, "深喉口交标志", TARGET
	CASEELSE
		SELECTCASE TEQUIP:六九式
		CASE 91, 150
		CASEELSE
			TEQUIP:深喉口交 = 0
		ENDSELECT
	ENDSELECT

	;插入解除判定
	CALL CHECK_INSERT, "解除判定"

CASE "追加之SOURCE"
	VARSET LOCAL
	;百合属性による补正
	IF PENIS(PLAYER) == 0 
		LOCAL = 0
		SELECTCASE SELECTCOM
		CASE 120 TO 129
			LOCAL:1 = 50
		CASE 6, 80, 94, 96, 98
			LOCAL:1 = 200
		ENDSELECT
	;精液成瘾による补正
	ELSEIF PENIS(PLAYER) && ABL:ARG:精液成瘾
		LOCAL = ABL:ARG:精液成瘾
		SELECTCASE SELECTCOM
		CASE 120 TO 129
			LOCAL:1 = 50
		CASE 6, 90, 93 TO 100
			LOCAL:1 = 200
		CASE 65, 75, 177
			LOCAL:1 = 300
		CASE 91, 92, 140
			LOCAL:1 = 500
		ENDSELECT
	ENDIF
	IF LOCAL:1
		SELECTCASE LOCAL
		CASE 1
			SOURCE:ARG:成瘾充足 += LOCAL:1
			TIMES SOURCE:ARG:达成感, 1.50
			TIMES SOURCE:ARG:屈从, 1.50
		CASE 2
			SOURCE:ARG:成瘾充足 += LOCAL:1 * 2
			TIMES SOURCE:ARG:达成感, 2.00
			TIMES SOURCE:ARG:屈从, 2.00
		CASE 3
			SOURCE:ARG:成瘾充足 += LOCAL:1 * 3
			TIMES SOURCE:ARG:达成感, 2.50
			TIMES SOURCE:ARG:屈从, 2.50
		CASE 4
			SOURCE:ARG:成瘾充足 += LOCAL:1 * 4
			TIMES SOURCE:ARG:达成感, 3.00
			TIMES SOURCE:ARG:屈从, 3.00
		CASE 5
			SOURCE:ARG:成瘾充足 += LOCAL:1 * 5
			TIMES SOURCE:ARG:达成感, 3.50
			TIMES SOURCE:ARG:屈从, 3.50
		ENDSELECT
	ENDIF
;特殊な行动
CASE "狂野摇摆"
	IF CHECK_COM("狂野摇摆", ARG)
		SETBIT TEQUIP:ARG:特殊行动, 0
	ELSE
		CLEARBIT TEQUIP:ARG:特殊行动, 0
	ENDIF
CASE "能量吸收"
	SETBIT TEQUIP:ARG:特殊行动, 2
CASE "两穴观察"
	SETBIT TEQUIP:ARG:特殊行动, 4
CASE "助手击沈"
	BASE:ASSI:精力 = 0
	;ASSIPLAY = 0
	;PLAYER = MASTER
	;插入しているなら解除
	CALL RESET_INSERT, ASSI
	TEQUIP:ASSI:逆推 = 0
	CVARSET TEQUIP, 32, 0
	;演奏停止
	CVARSET TEQUIP, 65, 0
	IF PLAYER == ASSI
		TEQUIP:逆推 = 0
		TEQUIP:六九式 = 0
		SWAP SELECTCOM, SELECTCOM:2
		SWAP PREVCOM, PREVCOM:2
	ENDIF
	;SELECTCOM:1 = -1
	;PREVCOM:1 = -1
CASE "助手对象之恋慕外し开始"
;タイミングとしては、"调教指令之COMABLEをチェックする时(@SHOW_STATUS)"と、"指令を選んだ时(@EVENTCOM)"に恋慕が外れ、
;@SHOW_USERCOM表示时、指令之終了时(@EVENTCOMEND)、调教終了时(@EVENTEND)で復帰させます。
	SIF CONFIG("助手对象でも恋慕適用") || ASSIPLAY == 0
		RETURN 0
	SIF TALENT:相思相爱
		CALL SET_CEVENT, "助手调教で之素質变化：相思相爱外し"
	SIF TALENT:恋慕
		CALL SET_CEVENT, "助手调教で之素質变化：恋慕外し"
	TALENT:相思相爱 = 0
	TALENT:恋慕 = 0
CASE "助手对象之恋慕外し終了"
	SIF CONFIG("助手对象でも恋慕適用")
		RETURN 0
	SIF CEVENT("助手调教で之素質变化：相思相爱外し")
		TALENT:相思相爱 = 1
	SIF CEVENT("助手调教で之素質变化：恋慕外し")
		TALENT:恋慕 = 1
	CALL DEL_CEVENT_GROUP, "助手调教で之素質变化"
CASE "今回调教指令"
	IF SELECTCOM:1 >= 0
		IF SELECTCOM:1 == 1
			STR:0 = %STR:0% ＆ 顔面骑乘
		ELSEIF SELECTCOM:1 == 9
			STR:0 = %STR:0% ＆ A顔面骑乘
		ELSEIF SELECTCOM:1 >= 90 && SELECTCOM:1 <= 119 && SELECTCOM == SELECTCOM:1
			STR:0 = Ｗ%TRAINNAME:(SELECTCOM:1)%
		ELSEIF SELECTCOM:1 == 98
			STR:0 = %STR:0% ＆ V奉仕
		ELSEIF (SELECTCOM:1 == 126 && SELECTCOM == 120) || (SELECTCOM:1 == 120 && SELECTCOM == 126)
			STR:0 = 二穴插入
		;助手之行动は記述しない
		ELSEIF SELECTCOM >= 60 && SELECTCOM <= 79 && SELECTCOM:1 >= 60 && SELECTCOM:1 <= 79
		ELSE
			STR:0 = %STR:0% ＆ %TRAINNAME:(SELECTCOM:1)%
		ENDIF
	ENDIF

	;绝顶具合之表示
	;同时绝顶
	IF STRLENS(STR:9)
		STR:1 = [%STR:9%]
	;こっちは誰か一人しか绝顶していない
	ELSEIF STRLENS(STR:5) || STRLENS(STR:6) || STRLENS(STR:7)
		STR:1 = [%STR:5%%STR:6%%STR:7%]
	ELSE
		STR:1 = 
	ENDIF
	;不要な文字列を初期化
	VARSET STR, "", 2, 10

	;-------------------------------------------------
	;今回之指令を「前回之指令」とする
	;-------------------------------------------------
	PREVCOM = SELECTCOM
	PREVCOM:4 = SELECTCOM:4

	;今回選んだ指令を記录する
	CALL SET_CHOSECOM, TARGET, SELECTCOM
	;助手も参加している场合には、助手之前回指令＆選択した指令も記录
	CALL SET_CHOSECOM, ASSI, SELECTCOM:1

CASE "调教終了"
	BASE:ARG:精力 = MAX(BASE:ARG:精力, 0)
	CALL CALC, "中意的体位", ARG

;ここで之标志は、キチンと认识しているか、がポイント
CASE "处女丧失标志"
	SETBIT TCVAR:ARG:10, 0
CASE "处女篡夺标志"
	SETBIT TCVAR:ARG:10, 1
CASE "童贞丧失标志"
	SETBIT TCVAR:ARG:10, 2
CASE "童贞猎取标志"
	SETBIT TCVAR:ARG:10, 3
;上之ほうにある"打扫口交"内でもONになる
CASE "打扫口交标志"
	SETBIT TCVAR:ARG:10, 4
;上之ほうにある"打扫口交"内でもONになる
CASE "A打扫口交标志"
	SETBIT TCVAR:ARG:10, 5
CASE "自慰标志"
	SETBIT TCVAR:ARG:10, 6
;上之ほうにある"打扫舐阴"内でもONになる
CASE "打扫舐阴标志"
	SETBIT TCVAR:ARG:10, 7
CASE "子宮刺激标志"
	SETBIT TCVAR:ARG:10, 8
CASE "A奉仕标志"
	SETBIT TCVAR:ARG:10, 9
CASE "针术标志"
	SETBIT TCVAR:ARG:10, 10
CASE "剃毛标志"
	SETBIT TCVAR:ARG:10, 11
CASE "P束缚术弱体标志"
	SETBIT TCVAR:ARG:10, 12
CASE "子宮插入标志"
	SETBIT TCVAR:ARG:10, 13
CASE "轻轻擦拭标志"
	SETBIT TCVAR:ARG:10, 14
CASE "阴蒂穿环标志"
	SETBIT TCVAR:ARG:10, 15
CASE "阴唇穿环标志"
	SETBIT TCVAR:ARG:10, 16
CASE "乳头穿环标志"
	SETBIT TCVAR:ARG:10, 17
CASE "肚脐穿环标志"
	SETBIT TCVAR:ARG:10, 18
CASE "穿环ＯＮ标志"
	SETBIT TCVAR:ARG:10, 19
CASE "镇痛麻醉标志"
	SETBIT TCVAR:ARG:10, 20
;これについては"下部露出：文本有", "下部露出：文本无"内でも同大人之标志立てを行う
CASE "下部露出させた"
	SETBIT TCVAR:ARG:10, 21
CASE "A处女丧失标志"
	SETBIT TCVAR:ARG:10, 22
CASE "A处女篡夺标志"
	SETBIT TCVAR:ARG:10, 23
CASE "饮用母乳标志"
	SETBIT TCVAR:ARG:10, 24
;上之ほうにある"饮尿"内でもONになる
CASE "饮尿标志"
	SETBIT TCVAR:ARG:10, 25
CASE "同时自慰标志"
	SETBIT TCVAR:ARG:10, 26
CASE "打屁股性爱标志"
	SETBIT TCVAR:ARG:10, 27
CASE "狗狗记号"
	SETBIT TCVAR:ARG:10, 28
CASE "不小心子宮插入标志"
	SETBIT TCVAR:ARG:10, 29
CASE "舔腋标志"
	SETBIT TCVAR:ARG:10, 30
CASE "腋现せ标志"
	SETBIT TCVAR:ARG:10, 31
CASE "膣内浴尿标志"
	SETBIT TCVAR:ARG:10, 32
CASE "子宮内浴尿标志"
	SETBIT TCVAR:ARG:10, 33
CASE "温泉灌肠标志"
	SETBIT TCVAR:ARG:10, 34
CASE "乳内浴尿标志"
	SETBIT TCVAR:ARG:10, 35
CASE "深喉口交标志"
	SETBIT TCVAR:ARG:10, 36
CASE "插入前暴发标志"
	SETBIT TCVAR:ARG:10, 37
CASE "具现化ＯＮ标志"
	SETBIT TCVAR:ARG:10, 38
CASE "具现化ＯＦＦ标志"
	SETBIT TCVAR:ARG:10, 39

CASE "尿道性感标志"
	SETBIT TCVAR:ARG:10, 40
CASE "膀胱内浴尿标志"
	SETBIT TCVAR:ARG:10, 41
CASE "追加接吻标志"
	SETBIT TCVAR:ARG:10, 42
;上之ほうにある"打扫舐A"内でもONになる
CASE "打扫舐A标志"
	SETBIT TCVAR:ARG:10, 43
;上之ほうにある"打扫口交"内でもONになる
CASE "尿打扫口交标志"
	SETBIT TCVAR:ARG:10, 44
;上之ほうにある"打扫舐阴"内でもONになる
CASE "尿打扫舐阴标志"
	SETBIT TCVAR:ARG:10, 45
CASE "恥垢打扫口交标志"
	SETBIT TCVAR:ARG:10, 46
;上之ほうにある"打扫舐阴"内でもONになる
CASE "恥垢打扫舐阴标志"
	SETBIT TCVAR:ARG:10, 47
CASE "舔足标志"
	SETBIT TCVAR:ARG:10, 48
CASE "受精确认标志"
	SETBIT TCVAR:ARG:10, 49
CASE "性转换标志"
	SETBIT TCVAR:ARG:10, 51
CASE "夹紧标志"
	SETBIT TCVAR:ARG:10, 53

;-------------------------------------------------
;以下は调教以外
;-------------------------------------------------
CASE "一文字関数初期化"
	VARSET A
	VARSET B
	VARSET C
	VARSET D
	VARSET E
	VARSET F
	VARSET G
	VARSET H
	VARSET I
	VARSET J
	VARSET K
	VARSET L
	VARSET M
	VARSET N
	VARSET O
	VARSET P
	VARSET Q
	VARSET R
	VARSET S
	VARSET T
	VARSET U
	VARSET V
	VARSET W
	VARSET X
	VARSET Y
	VARSET Z
CASE "主动夜袭主动朝袭終了"
	CALL RESET_EXP_GAIN, ARG
	CALL GET_EXABL2, ARG
	CALL RESETFLAG_CLO, ARG
	VARSET TCVAR:ARG:0, 0, 0, 30
CASE "属性"
	;对属性的获取
	;CALL GET_EXTALENT, ARG
	;BASE:ARG:属性 = LOCAL
CASE "恋慕"
	PRINTFORMW %CALLNAME:TARGET%用不自然的目光看向这里……
	PRINTFORMW %CALLNAME:TARGET%从%CALLNAME:MASTER%的行为中感受到了愉悦……
	SETCOLOR DEF_COLOR("粉红色心形")
	PRINTFORMW %CALLNAME:TARGET%取得了【恋慕】
	TALENT:恋慕 = 1
	IF TALENT:反抗心
		SETCOLOR DEF_COLOR("黄色")
		PRINTFORMW %CALLNAME:TARGET%不再具有【反抗心】了
		TALENT:反抗心 = 0
	ENDIF
	RESETCOLOR
	;欲望、顺从、屈服刻印之上升による[压抑][抵抗]之消滅をチェック
	CALL YOKUBO_UP_CHECK, ARGS
	;堕とした人数。既に淫乱だとなし
	SIF TALENT:淫乱 == 0
		FLAG:30 += 1
	CALL SETFLAG, "角色攻略"
CASE "淫乱"
	PRINTFORMW %CALLNAME:TARGET%用不自然的目光看向这里……
	PRINTFORMW %CALLNAME:TARGET%沉溺在%CALLNAME:MASTER%带来的快乐中……
	SETCOLOR DEF_COLOR("粉红色心形")
	PRINTFORMW %CALLNAME:TARGET%取得了【淫乱】
	TALENT:淫乱 = 1
	RESETCOLOR
	;欲望、顺从、屈服刻印之上升による[压抑][抵抗]之消滅をチェック
	CALL YOKUBO_UP_CHECK, ARGS
	;堕とした人数。既に恋慕だとなし
	SIF TALENT:恋慕 == 0
		FLAG:30 += 1
	CALL SETFLAG, "角色攻略"
CASE "売却价格"
	LOCAL = TARGET
	TARGET = ARG
	;CFLAG:41に能力による价值が代入される
	CALL CALC, "売却价格：能力"
	;CFLAG:42に殆ど变动之ない素質による价值之倍率が代入される
	CALL CALC, "売却价格：基礎"
	;CFLAG:43に獲得or丧失しうる素質による价值之倍率が代入される
	CALL CALC, "売却价格：变动素質"
	;修正前之综合评价点数
	CFLAG:44 = CFLAG:41 / 100 * CFLAG:42 / 1000 * CFLAG:43 / 1000 * 100
	;倍々で増えないように价格を调整
	LOCAL:1 = CFLAG:44
	FOR LCOUNT, 0, 10
		LOCAL:2 = 100000 * POWER(10, LCOUNT)
		SIF LOCAL:1 <= LOCAL:2
			BREAK
		LOCAL:1 = (LOCAL:1 - LOCAL:2)/4 + LOCAL:2
	NEXT
	CFLAG:40 = LOCAL:1
	TARGET = LOCAL
CASE "测量胸部"
	FOR LCOUNT, 1, CHARANUM
		IF TALENT:LCOUNT:巨乳
			BASE:LCOUNT:胸部 = 0
			BASE:LCOUNT:罩杯 = 0
		ENDIF
	NEXT
CASE "道具标志调整"
	VARSET ITEMSALES
	FOR LCOUNT, 0, 100
		IF ITEM:LCOUNT <= 0
			ITEM:LCOUNT = 0
			ITEMSALES:LCOUNT = 1
		ENDIF
	NEXT
	;消耗道具
	VARSET ITEMSALES, 1, 30, 50
	FOR LCOUNT, 30, 50
		SIF ITEM:LCOUNT >= 99
			ITEMSALES:LCOUNT = 0
	NEXT
	;魔法瓶
	SIF ITEM:魔法瓶 == 0
		ITEM:精液储蓄量 = 0
CASE "角色攻略"
	;阴蒂ア之記录(上位梦魇などにも个别对应)
	LOCAL = NO:TARGET + 1099
	IF !GETBIT(FLAG:LOCAL, CFLAG:8)
		SETBIT FLAG:LOCAL, CFLAG:8
		FLAG:31 += 1
	ENDIF
CASE "一般人对象之性行为初期設定"
	ADDCHARA 80
	PLAYER = CHARANUM-1
	CALL GET_EXABL, PLAYER
	CALL GET_EXTALENT, PLAYER
	SIF RAND:10 == 0
		TALENT:PLAYER:童贞 = 0
	;精力セット
	MAXBASE:PLAYER:精力 = 10
	EJAC:PLAYER = 5000
	BASE:PLAYER:精力 = MAXBASE:PLAYER:精力
	BASE:PLAYER:绝顶 = 0
	TEQUIP:逆推 = 1
	;计算开始
	CALL SIMULATE_TRAIN_FIRST
CASE "一般人对象之性行为設定：对象が女"
	TALENT:PLAYER:性别 = 1

CASE "一般人对象之性行为終了"
	;経験や珠など之獲得量计算
	CALL SIMULATE_TRAIN_CALC
	PLAYER = -1
	;結果发表
	SIF FLAG:8
		CALL SIMULATE_TRAIN_SHOW
	;表示されていた角色を白紙に戻す
	DELCHARA CHARANUM-1
	PLAYER = MASTER
	TEQUIP:逆推 = 0
CASE "胸部設定"
	;アンダー胸部之值には 身长*0.432 (～20代之平均值)を用いる。ただし、腰部以下にはならない。
	;胸部之值はアンダー胸部+胸部罩杯で算出する。
	;AAA（5cm）、AA（7.5cm)、A（10cm）、B（12.5cm）、C（15cm）、D（17.5cm）、E（20cm）、F（22.5cm）、G（25cm）、
	;H（27.5cm)、I（30cm)、J（32.5cm)、K（35cm)、L（37.5cm)、M（40cm)、N（42.5cm)、O（45cm)…… 
	;とだいたい2.5cm単位で罩杯が推移するも之とする。

	;超乳(：31～38)
	ELSEIF TALENT:ARG:巨乳 >= 3
		CALL CALC, "超乳化", ARG
	;爆乳(：24～30)
	ELSEIF TALENT:ARG:巨乳 == 2
		CALL CALC, "爆乳化", ARG
	;巨乳（E～ForGorH：19～23）
	ELSEIF TALENT:ARG:巨乳 == 1
		CALL CALC, "巨乳化", ARG
	;贫乳（～A：～10）
	ELSEIF TALENT:ARG:贫乳
		CALL CALC, "贫乳化", ARG
	;普通（B～D:11～18）
	ELSE
		CALL CALC, "普乳化", ARG
	ENDIF
CASE "胸部之素質設定"
	SELECTCASE BASE:ARG:罩杯
	CASE IS <= NUM("贫乳之上限")
		TALENT:ARG:贫乳 = 1
		TALENT:ARG:巨乳 = 0
	CASE IS <= NUM("普乳之上限")
		TALENT:ARG:贫乳 = 0
		TALENT:ARG:巨乳 = 0
	CASE IS <= NUM("巨乳之上限")
		TALENT:ARG:贫乳 = 0
		TALENT:ARG:巨乳 = 1
	CASE IS <= NUM("爆乳之上限")
		TALENT:ARG:贫乳 = 0
		TALENT:ARG:巨乳 = 2
	CASEELSE
		TALENT:ARG:贫乳 = 0
		TALENT:ARG:巨乳 = 3
	ENDSELECT
CASE "臀部設定"
	TALENT:ARG:小臀 = 0
	TALENT:ARG:巨臀 = 0
	;お尻之大きさ
	SELECTCASE COND("巨臀", ARG)
	CASE IS >= 3
		TALENT:ARG:巨臀 = 3
	CASE 2
		TALENT:ARG:巨臀 = 2
	CASE 1
		TALENT:ARG:巨臀 = 1
	CASEELSE
		SIF COND("小臀", ARG)
			TALENT:ARG:小臀 = 1
	ENDSELECT
CASE "阴茎详细設定"
		RETURN 0
CASE "阴毛と腋毛"
	;阴毛之設定(コンフィグONで設定されてないとき)
	SIF CONFIG("阴毛と腋毛") == 0
		RETURN 0
	;コンフィグで生えない設定
	IF COND("阴毛之設定", ARG) == 0
		BASE:ARG:阴毛 = 100
		SIF CONFIG("腋毛")
			BASE:ARG:腋毛 = 100
		RETURN 0
	ENDIF
	;阴毛之初期設定
	IF BASE:ARG:阴毛 == 0
		;性别男なら自然生长
		IF TALENT:ARG:性别 == 1 && COND("男孩子？", ARG) == 0
			BASE:ARG:阴毛 = 600
		;魔导人偶or５％で白虎
		ELSEIF TALENT:ARG:魔导人偶 || RAND:20 == 0
			BASE:ARG:阴毛 = 100
		;性变态なら、稀に光溜溜
		ELSEIF TALENT:ARG:性变态 && RAND:20 == 0
			BASE:ARG:阴毛 = 300
		;小孩子や娇小
		ELSEIF TALENT:ARG:外表年龄 == 0 || (TALENT:ARG:娇小 && RAND:3)
			BASE:ARG:阴毛 = 400
			;娇小なら、高确率で白虎
			SIF TALENT:ARG:娇小 && RAND:5 == 0
				BASE:ARG:阴毛 = 100
		;保守なら、お手入れに抵抗があったり
		ELSEIF TALENT:ARG:保守 && RAND:10
			IF RAND:5
				BASE:ARG:阴毛 = 600
			ELSE
				BASE:ARG:阴毛 = 700
			ENDIF
		;食い込んでいる衣装之时or好色なら、高确率で处理完毕み
		ELSEIF CHECK_CLO("紧勒", ARG)
			BASE:ARG:阴毛 = 500
		ELSEIF TALENT:ARG:好色 && COND("梦魇", ARG) && RAND:10
			BASE:ARG:阴毛 = 500
		ELSEIF TALENT:ARG:好色 && COND("梦魇", ARG) == 0 && RAND:10 == 0
			BASE:ARG:阴毛 = 500
		ELSE
			BASE:ARG:阴毛 = 600
		ENDIF
	ENDIF
	;腋毛之初期設定
	IF CONFIG("腋毛") && BASE:ARG:腋毛 == 0
		;性别男なら处理しない
		IF TALENT:ARG:性别 == 1 && COND("男孩子？", ARG) == 0
			SELECTCASE TALENT:ARG:外表年龄
			CASE IS >= 3
				BASE:ARG:腋毛 = 700
			CASE 2
				BASE:ARG:腋毛 = 400
			CASEELSE
				BASE:ARG:腋毛 = 100
			ENDSELECT
		;阴毛が无いなら腋毛もありません
		ELSEIF BASE:ARG:阴毛 == 0 || RAND:20 == 0
			BASE:ARG:腋毛 = 100
		;小孩子や少女なら、高确率で无毛orつるつる
		ELSEIF TALENT:ARG:外表年龄 <= 1 || (TALENT:ARG:娇小 && RAND:3)
			SELECTCASE RAND:10 + TALENT:ARG:外表年龄*6
			CASE IS <= 7
				BASE:ARG:腋毛 = 100
			CASE 8
				BASE:ARG:腋毛 = 400
			CASE 9 TO 13
				BASE:ARG:腋毛 = 300
			CASE IS >= 14
				BASE:ARG:腋毛 = 200
			ENDSELECT
		;保守なら、お手入れに抵抗があったり
		ELSEIF TALENT:ARG:保守 && RAND:10
			BASE:ARG:腋毛 = 700
		;だらしない成熟
		ELSEIF TALENT:ARG:外表年龄 >= 3 && RAND:3 == 0
			BASE:ARG:腋毛 = 700
		;普通は剃ります
		ELSE
			BASE:ARG:腋毛 = 300
		ENDIF
		;好色or男孩子？なら超高确率で腋つるつる
		IF TALENT:ARG:好色 || COND("男孩子？", ARG)
			SELECTCASE RAND:100
			CASE IS <= 90
				BASE:ARG:腋毛 = MIN(BASE:ARG:腋毛, 300)
			CASE 91 TO 98
				BASE:ARG:腋毛 = MIN(BASE:ARG:腋毛, 200)
			ENDSELECT
		ENDIF
	ENDIF
CASE "阴毛と腋毛之設定初期化"
	CVARSET BASE, 26, 0
	CVARSET BASE, 28, 0
	CVARSET CFLAG, 368, 0
	CVARSET CFLAG, 369, 0
CASE "咖啡屋之服装"
	;FLAG:401之bitで制服を管理する
	;0～9は制服之种类
	;10～19は迷你裙之オプション
	;20～29は兔女郎装など紧身衣タイプ之も之之オプション
	;30～39は其他色々

	REDRAW 0

	PRINTFORML 请从下列服饰中选择
	PRINTFORML (点击服饰的细节设定，可将其设置为ＯＮ/ＯＦＦ)
	PRINTFORML 当前制服样式：%TEXTS("制服")%
	VARSET LOCALS
	LOCALS:0 = 女侍服
	LOCALS:1 = 妹抖服
	LOCALS:2 = 巫女风
	LOCALS:5 = 兔女郎装
	SELECTCASE CONDS("咖啡屋：制服")
	CASE "女侍服", "妹抖服"
		LOCALS:10 = 迷你裙
		LOCALS:11 = 长裙
		LOCALS:30 = 吊袜带
	CASE "巫女风"
		LOCALS:10 = 迷你裙
		LOCALS:11 = 长裙
	CASE "兔女郎"
		LOCALS:20 = 前面拉链
		LOCALS:30 = 吊袜带
	ENDSELECT
	LOCAL = 0
	FOR LCOUNT, 0, 64
		SIF LOCALS:LCOUNT == ""
			CONTINUE
		SIF GETBIT(FLAG:401, LCOUNT)
			SETCOLOR DEF_COLOR("暗黄色")
		PRINTFORMLC [{LCOUNT, 3}] \@ GETBIT(FLAG:401, LCOUNT) ? ○ # － \@ %LOCALS:LCOUNT%
		RESETCOLOR
		SELECTCASE LCOUNT%10
		CASE 2, 5, 8, 9
			PRINTL 
		CASEELSE
			SIF LOCALS:(LCOUNT+1) == ""
				PRINTL 
		ENDSELECT
	NEXT
	PRINTFORMLC [100] 好了

	CALL INPUT_SELECT, 101

	IF RESULT == 100
		IF COND("制服：女侍服")
			PRINTFORMW \@ COND("制服：吊袜带") ? 大腿上穿着吊袜带、整体上 # \@重点突出可爱的制服
		ELSEIF COND("制服：妹抖服")
			PRINTFORMW 将打工服装的决定版：妹抖服\@ COND("制服：吊袜带") ? ，连同时尚的吊袜带一起 # \@作为制服
		ELSEIF COND("制服：巫女风")
			PRINTFORMW 将紅白两色的巫女服作为制服
		ELSEIF COND("制服：兔女郎")
			PRINTFORMW 选择以釉光素材精工细作、有点Ｈ的高叉兔女郎装作为制服
			PRINTFORMW 这是将必不可少的兔耳、蝴蝶结和袖口、网袜\@ COND("制服：吊袜带") ? 以及吊袜带 # \@加以组合的魅力衣装
		ENDIF
		IF COND("制服：迷你裙")
			PRINTFORMW 而且这裙子相当短。走光这个可以有…
		ELSEIF COND("制服：长")
			PRINTFORMW 裙子比较长、给人以沉稳的感觉
		ENDIF
		IF COND("制服：拉链")
			PRINTFORMW 股间部分经过定制，设有巧妙隐藏起来的拉链
			PRINTFORMW 配套的网袜也有在三角地带留出开口
			PRINTFORMW 总之各种方便
		ENDIF
		REDRAW 1
		RETURN 0
	ELSEIF RESULT == 0
		FLAG:401 = CLEARBITS(FLAG:401, 0, 9)
		SETBIT FLAG:401, 0
		FLAG:401 = CLEARBITS(FLAG:401, 20, 29)
	ELSEIF RESULT == 1
		FLAG:401 = CLEARBITS(FLAG:401, 0, 9)
		SETBIT FLAG:401, 1
		FLAG:401 = CLEARBITS(FLAG:401, 20, 29)
	ELSEIF RESULT == 2
		FLAG:401 = CLEARBITS(FLAG:401, 0, 9)
		SETBIT FLAG:401, 2
		FLAG:401 = CLEARBITS(FLAG:401, 20, 29)
		FLAG:401 = CLEARBITS(FLAG:401, 30, 39)
	ELSEIF RESULT == 5
		FLAG:401 = CLEARBITS(FLAG:401, 0, 9)
		SETBIT FLAG:401, 5
		FLAG:401 = CLEARBITS(FLAG:401, 10, 11)
	ELSEIF RESULT == 10 && LOCALS:RESULT != ""
		INVERTBIT FLAG:401, 10
		SIF GETBIT(FLAG:401, 10)
			CLEARBIT FLAG:401, 11
	ELSEIF RESULT == 11 && LOCALS:RESULT != ""
		INVERTBIT FLAG:401, 11
		SIF GETBIT(FLAG:401, 11)
			CLEARBIT FLAG:401, 10
	ELSEIF RESULT == 20 && LOCALS:RESULT != ""
		INVERTBIT FLAG:401, 20
	ELSEIF RESULT == 30 && LOCALS:RESULT != ""
		INVERTBIT FLAG:401, 30
	ENDIF

	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDSELECT


;-------------------------------------------------
;色々と计算する関数(#FUNCTIONではない)
;-------------------------------------------------
@CALC, ARGS, ARG, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9, ARG:10, ARG:11, ARG:12, ARG:13, ARG:14, ARG:15, ARG:16, ARG:17, ARG:18, ARG:19, ARG:20, ARG:21, ARG:22
#DIM LCOUNT

SELECTCASE ARGS
CASE "体力回復"
	SIF ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET > 0
		ARG:1 = TARGET
	;上限突破しているときには就那样
	SIF BASE:(ARG:1):体力 > MAXBASE:(ARG:1):体力
		RETURN 0

	BASE:(ARG:1):体力 = MIN(BASE:(ARG:1):体力 + ARG, MAXBASE:(ARG:1):体力)
CASE "体力回復上限无视"
	SIF ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET > 0
		ARG:1 = TARGET
	BASE:(ARG:1):体力 += ARG
;时间経過之際など
CASE "体力回復上限まで"
	SIF ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET > 0
		ARG:1 = TARGET

	BASE:(ARG:1):体力 = MIN(BASE:(ARG:1):体力 + ARG, MAXBASE:(ARG:1):体力)

CASE "气力回復"
	SIF ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET > 0
		ARG:1 = TARGET
	;上限突破しているときには就那样
	SIF BASE:(ARG:1):气力 > MAXBASE:(ARG:1):气力
		RETURN 0

	BASE:(ARG:1):气力 = MIN(BASE:(ARG:1):气力 + ARG, MAXBASE:(ARG:1):气力)
CASE "气力回復上限无视"
	SIF ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET > 0
		ARG:1 = TARGET
	BASE:(ARG:1):气力 += ARG
CASE "气力回復上限まで"
	SIF ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET > 0
		ARG:1 = TARGET

	BASE:(ARG:1):气力 = MIN(BASE:(ARG:1):气力 + ARG, MAXBASE:(ARG:1):气力)

CASE "精力回復"
	SIF ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET > 0
		ARG:1 = TARGET
	;上限突破しているときには就那样
	SIF BASE:(ARG:1):精力 > MAXBASE:(ARG:1):精力
		RETURN 0

	BASE:(ARG:1):精力 = MIN(BASE:(ARG:1):精力 + ARG, MAXBASE:(ARG:1):精力)
;これは面倒なことが起きる之で基本封印
CASE "精力回復上限无视"
	SIF ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET > 0
		ARG:1 = TARGET
	BASE:(ARG:1):精力 += ARG
CASE "精力回復上限まで"
	SIF ARG:1 < 0
		RETURN 0
	SIF ARG:1 == 0 && TARGET > 0
		ARG:1 = TARGET

	BASE:(ARG:1):精力 = MIN(BASE:(ARG:1):精力 + ARG, MAXBASE:(ARG:1):精力)
CASE "随机選択"
	IF ARG == 0
		LOCAL = 0
		LOCAL:1 = 0
	ELSEIF RAND:(LOCAL:1) == 0 || ARG:1
		LOCAL = ARG
	ENDIF
	LOCAL:1 += 1
	RETURN LOCAL
CASE "胖次标志追加"
	TCVAR:26 = 0
	EQUIP:胖次 |= ARG
CASE "ブラ标志追加"
	TCVAR:26 = 0
	EQUIP:ブラ |= ARG
CASE "上着标志追加"
	TCVAR:26 = 0
	EQUIP:上着 |= ARG
CASE "迷你裙标志追加"
	TCVAR:26 = 0
	EQUIP:迷你裙 |= ARG
CASE "紧身衣标志追加"
	TCVAR:26 = 0
	EQUIP:紧身衣 |= ARG
CASE "外套标志追加"
	TCVAR:26 = 0
	EQUIP:外套 |= ARG
CASE "靴下标志追加"
	TCVAR:26 = 0
	EQUIP:靴下 |= ARG
CASE "靴标志追加"
	TCVAR:26 = 0
	EQUIP:靴 |= ARG
CASE "手套标志追加"
	TCVAR:26 = 0
	EQUIP:手套 |= ARG
CASE "调教ソース"
	FOR LCOUNT, 0, 20
		SOURCE:LCOUNT += ARG:LCOUNT * ARG:22 / 100
	NEXT
	DOWNBASE:体力 += ARG:20 * ARG:22 / 100
	DOWNBASE:气力 += ARG:21 * ARG:22 / 100
CASE "経験取得"
	;ARG:1に２以上之数字が代入されている场合には獲得経験をARG:1倍する
	;増加前之経験を記录
	FOR LCOUNT, 0, 100
		LOCAL:LCOUNT = EXP:LCOUNT
		LOCAL:(LCOUNT + 100) = EXP:PLAYER:LCOUNT
	NEXT

	;逆调教指令
	SELECTCASE ARG
	CASE 106, 107, 120 TO 129
		SWAP TARGET, PLAYER
	CASE 304, 305
		;逆龙奸
		SIF TEQUIP:逆推 || TEQUIP:PLAYER:逆推
			SWAP TARGET, PLAYER
	ENDSELECT

	SELECTCASE ARG
	CASE 0
		IF CHECK_COM("接吻")
			EXP:接吻経験 += 1
			EXP:PLAYER:接吻経験 += 1
		ENDIF
		EXP:PLAYER:手交経験 += 1
		IF CHECK_COM("润滑液按摩", ARG)
			EXP:A经验 += 1
			EXP:PLAYER:肛虐経験 += 1
		ENDIF
	CASE 1
		EXP:PLAYER:舐阴経験 += 1
	CASE 2
		EXP:A经验 += 1
		EXP:PLAYER:手交経験 += 1
		EXP:PLAYER:肛虐経験 += 1
	CASE 3
		EXP:自慰経験 += 1
		;录像摄影时、馆内露出时、野外PLAYでは経験にプラス
		SIF TEQUIP:屋内露出PLAY || TEQUIP:摄像机 || TEQUIP:野外PLAY
			EXP:自慰経験 += 1
		;媚药
		SIF TEQUIP:媚药
			EXP:自慰経験 += 1
		;绝顶した之なら、経験にBＯＮＵS
		SIF NOWEX:绝顶数合计
			EXP:自慰経験 += 1
		SIF GETBIT(CHECK_ONANIE(ARG), 1)
			EXP:V经验 += 1
		SIF GETBIT(CHECK_ONANIE(ARG), 2)
			EXP:A经验 += 1
	CASE 4
		EXP:PLAYER:口交経験 += 1
		IF TEQUIP:逆推
			EXP:PLAYER:喉性交経験 += 1
			EXP:腰技経験 += 1
		ENDIF
	CASE 5
		EXP:PLAYER:手交経験 += 1
	;接吻と熔融之吻
	CASE 6, 311
		EXP:接吻経験 += 1
		EXP:PLAYER:接吻経験 += 1
	CASE 7
		IF PREVCOM == 6
			EXP:接吻経験 += 1
			EXP:PLAYER:接吻経験 += 1
		ENDIF
		EXP:PLAYER:手交経験 += 1
	CASE 8
		EXP:V经验 += 1
		EXP:PLAYER:手交経験 += 1
	CASE 9
		EXP:A经验 += 1
		EXP:PLAYER:肛虐経験 += 1
		EXP:PLAYER:舐A经验 += 1
	CASE 10
		EXP:PLAYER:手交経験 += 1
		SIF INSERT("尿道插入", TARGET, PLAYER)
			EXP:PLAYER:自慰経験 += 1
	CASE 11
		EXP:PLAYER:乳交経験 += 1
	CASE 12
		EXP:PLAYER:足交経験 += 1
	CASE 13, 14
		EXP:露出経験 += 1
		;录像摄影、馆内露出、野外PLAYは経験等にプラス
		SIF TEQUIP:摄像机
			EXP:露出経験 += 1
		SIF TEQUIP:屋内露出PLAY || TEQUIP:野外PLAY
			EXP:露出経験 += 1
		SIF COND("两穴观察")
			EXP:露出経験 += 1
	CASE 15
		EXP:尿道経験 += 1
		;指を动かしても大丈夫になるサイズまでは扩张
		IF SIZE("指之直径", PLAYER) > MIN_STR(CONDS("尿道扩张度") )
			CALL SETFLAG, "尿道扩张", SIZE("指之直径", PLAYER)
		ELSEIF SIZE("指之直径", PLAYER)*4/3 > MIN_STR(CONDS("尿道扩张度") )
			CALL SETFLAG, "尿道扩张", MIN_STR(CONDS("尿道扩张度") ) + 1
		ENDIF
		EXP:PLAYER:刺激尿道経験 += 1
	CASE 31, 106, 201
		EXP:V经验 += 1
		;大きいと経験BＯＮＵS
		SIF SIZE("按摩棒", TARGET) >= 3
			EXP:V经验 += 1
		IF SIZE("按摩棒", TARGET) >= MAX(ABL:V扩张 - 1, 2)
			EXP:V经验 += SIZE("按摩棒", TARGET) - ABL:V扩张 + 1
			CALL SETFLAG, "V扩张", SIZE("按摩棒", TARGET), HARD("按摩棒", TARGET)
		ENDIF
		IF ARG == 201
			CALL SETFLAG, "V插入按摩棒", SIZE("按摩棒", TARGET), TARGET, -5
		ELSE
			CALL SETFLAG, "V插入按摩棒", SIZE("按摩棒", TARGET)
		ENDIF
	CASE 33, 107, 202
		EXP:PLAYER:肛虐経験 += 1
		EXP:A经验 += 1
		;大きいと経験BＯＮＵS
		IF SIZE("肛用按摩棒", TARGET) >= 3
			EXP:A经验 += 1
			EXP:PLAYER:肛虐経験 += 1
		ENDIF
		IF SIZE("肛用按摩棒", TARGET) >= MAX(ABL:A扩张 - 1, 2)
			EXP:A经验 += SIZE("肛用按摩棒", TARGET) - ABL:A扩张 + 1
			CALL SETFLAG, "A扩张", SIZE("肛用按摩棒", TARGET), HARD("肛用按摩棒", TARGET)
		ENDIF
		IF ARG == 202
			CALL SETFLAG, "A插入按摩棒", SIZE("肛用按摩棒", TARGET), TARGET, -5
		ELSE
			CALL SETFLAG, "A插入按摩棒", SIZE("肛用按摩棒", TARGET)
		ENDIF
	CASE 39
		EXP:V经验 += 1
	CASE 60 TO 69, 177, 120 TO 124
		EXP:V经验 += 1
		IF PENIS(PLAYER)
			EXP:V性交経験 += 1
			EXP:PLAYER:V插入経験 += 1
			;大きいと経験BＯＮＵS
			IF SIZE("阴茎", PLAYER) >= 2
				EXP:V经验 += 1
				EXP:V性交経験 += 1
			ENDIF
		ENDIF
		IF SIZE("阴茎", PLAYER) >= MAX(ABL:V扩张 + 1, 2)
			EXP:V经验 += SIZE("阴茎", PLAYER) - ABL:V扩张
			CALL SETFLAG, "V扩张", SIZE("阴茎", PLAYER), HARD("阴茎", PLAYER)
		ENDIF

		CALL SETFLAG, "V插入阴茎", SIZE("阴茎", PLAYER)

		SELECTCASE ARG
		CASE 63
			SIF TALENT:恋慕 && CHECK_COM("接吻")
				EXP:接吻経験 += 1
		CASE 65, 122
			EXP:腰技経験 += 1
		CASE 66
			EXP:V经验 += 1
			SIF PENIS(PLAYER)
				EXP:V性交経験 += 1
		CASE 67
			EXP:V经验 += 1

			EXP:子宮経験 += 1
			SIF PENIS(PLAYER)
				EXP:V性交経験 += 1
		CASE 68

			EXP:子宮経験 += 2
			EXP:子宮奸経験 += 1
		ENDSELECT
		SIF TEQUIP:逆推 == 0
			EXP:PLAYER:腰技経験 += 1
	CASE 70 TO 79, 125 TO 129
		EXP:A经验 += 1
		EXP:PLAYER:肛虐経験 += 1
		IF PENIS(PLAYER)
			EXP:A性交経験 += 1
			EXP:PLAYER:A插入経験 += 1
			;大きいと経験BＯＮＵS
			IF SIZE("阴茎", PLAYER) >= 2
				EXP:A经验 += 1
				EXP:A性交経験 += 1
				EXP:PLAYER:肛虐経験 += 1
			ENDIF
		ENDIF
		IF SIZE("阴茎", PLAYER) >= MAX(ABL:A扩张 + 1, 2)
			EXP:A经验 += SIZE("阴茎", PLAYER) - ABL:A扩张
			CALL SETFLAG, "A扩张", SIZE("阴茎", PLAYER), HARD("阴茎", PLAYER)
		ENDIF

		CALL SETFLAG, "A插入阴茎", SIZE("阴茎", PLAYER)

		SELECTCASE ARG
		CASE 73
			SIF TALENT:恋慕 && CHECK_COM("接吻")
				EXP:接吻経験 += 1
		CASE 75, 127
			EXP:腰技経験 += 1
		ENDSELECT

		SIF TEQUIP:逆推 == 0
			EXP:PLAYER:腰技経験 += 1
	CASE 80
		EXP:腰技経験 += 1
		EXP:PLAYER:腰技経験 += 1
		IF CHECK_COM("双头按摩棒PLAY")
			EXP:V经验 += 1
			IF SIZE("按摩棒", TARGET) >= MAX(ABL:V扩张 - 1, 2)
				EXP:V经验 += SIZE("按摩棒", TARGET) - ABL:V扩张
				CALL SETFLAG, "V扩张", SIZE("按摩棒", TARGET), HARD("按摩棒", TARGET)
			ENDIF
			SWAP TARGET, PLAYER
			EXP:V经验 += 1
			IF SIZE("按摩棒", PLAYER) >= MAX(ABL:V扩张 - 1, 2)
				EXP:V经验 += SIZE("按摩棒", PLAYER) - ABL:V扩张
				CALL SETFLAG, "V扩张", SIZE("按摩棒", PLAYER), HARD("按摩棒", TARGET)
			ENDIF
			SWAP TARGET, PLAYER
		ENDIF
	CASE 81
		EXP:PLAYER:手交経験 += 1
		EXP:PLAYER:腰技経験 += 1
	CASE 82
		EXP:B性交経験 += 1
		EXP:PLAYER:B插入経験 += 1
		IF TEQUIP:逆推
			EXP:乳交経験 += 1
		ELSE
			EXP:PLAYER:腰技経験 += 1
		ENDIF
	CASE 83
		EXP:尿道経験 += 1
		EXP:尿道性交経験 += 1
		;动かしても大丈夫になるサイズまでは扩张
		IF SIZE("阴茎之直径", PLAYER) > MIN_STR(CONDS("尿道扩张度") )
			CALL SETFLAG, "尿道扩张", SIZE("阴茎之直径", PLAYER)
		ELSEIF SIZE("阴茎之直径", PLAYER)*4/3 > MIN_STR(CONDS("尿道扩张度") )
			CALL SETFLAG, "尿道扩张", MIN_STR(CONDS("尿道扩张度") ) + 1
		ENDIF
		EXP:PLAYER:尿道插入経験 += 1
		EXP:PLAYER:刺激尿道経験 += 1
	CASE 92, 104
		IF CHECK_COM("乳夹口交", ARG)
			EXP:口交経験 += 1
		ELSEIF CHECK_COM("两乳夹头手交", ARG)
			EXP:手交経験 += 1
		ENDIF
		EXP:乳交経験 += 1
	CASE 90, 97
		;特殊指令
		IF CHECK_COM("手搓口交", ARG)
			EXP:口交経験 += 1
		ELSEIF CHECK_COM("刺激肛门手交", ARG)
			EXP:肛虐経験 += 1
			EXP:舐A经验 += 1
			EXP:PLAYER:A经验 += 1
		ELSEIF CHECK_COM("两乳夹头手交", ARG)
			EXP:乳交経験 += 1
		ENDIF
		EXP:手交経験 += 1
	CASE 91, 208
		;特殊指令
		IF CHECK_COM("手搓口交", ARG)
			EXP:手交経験 += 1
		ELSEIF CHECK_COM("乳夹口交", ARG)
			EXP:乳交経験 += 1
		ENDIF
		EXP:口交経験 += 1
		SIF ARG == 208
			EXP:口交経験 += 1
		SIF TEQUIP:深喉口交
			EXP:喉性交経験 += 1
	CASE 93
		EXP:腰技経験 += 1
	CASE 95
		EXP:足交経験 += 1
	CASE 96
		SIF CHECK_COM("刺激肛门手交", ARG)
			EXP:手交経験 += 1
		EXP:肛虐経験 += 1
		EXP:舐A经验 += 1
		EXP:PLAYER:A经验 += 1
	CASE 98
		EXP:舐阴経験 += 1
	CASE 99
		;@GET_EXP_COMで特殊な处理をするため、ここには通常は来ない
		IF PENIS(PLAYER)
			EXP:口交経験 += 1
		ELSE
			EXP:舐阴経験 += 1
		ENDIF
		IF PENIS(TARGET)
			EXP:PLAYER:口交経験 += 1
		ELSE
			EXP:PLAYER:舐阴経験 += 1
		ENDIF
	CASE 133
		SIF TEQUIP:眼罩
			EXP:紧縛経験 += 2
	CASE 134
		SIF TEQUIP:拘束
			EXP:紧縛経験 += 5
	CASE 135
		SIF TEQUIP:口枷
			EXP:紧縛経験 += 2
	CASE 136
		EXP:PLAYER:肛虐経験 += 5
		IF TEQUIP:灌肠
			EXP:A经验 += 2
			EXP:灌肠経験 += 1
			EXP:紧縛経験 += 2
		ENDIF
	CASE 137
		IF TEQUIP:扩张扩张气囊 == 1
			EXP:V经验 += 2
			EXP:V扩张经验 += 2
			CALL SETFLAG, "V插入按摩棒", ABL:V扩张 + 1
		ELSEIF TEQUIP:扩张扩张气囊 == 2
			EXP:PLAYER:肛虐経験 += 2
			EXP:A经验 += 2
			EXP:A扩张经验 += 2
			CALL SETFLAG, "A插入按摩棒", ABL:A扩张 + 1
		ENDIF
	CASE 138
		EXP:PLAYER:放尿経験 += 1
		IF CHECK_COM("口内放尿", ARG)
			SIF PENIS(PLAYER) == 0
				EXP:PLAYER:圣水経験 += 1
			EXP:饮尿経験 += 1
		ELSEIF CHECK_COM("子宮内放尿", ARG)
			EXP:子宮浴尿経験 += 1
		ELSEIF CHECK_COM("膣内放尿", ARG)
			EXP:膣内浴尿経験 += 1
		ELSEIF CHECK_COM("温泉灌肠", ARG)
			EXP:灌肠経験 += 1
			EXP:温泉灌肠経験 += 1
			EXP:PLAYER:肛虐経験 += 2
		ELSEIF CHECK_COM("乳内放尿", ARG)
			EXP:乳内浴尿経験 += 1
		ELSEIF CHECK_COM("膀胱内放尿", ARG)
			EXP:输尿経験 += 1
		ENDIF
	CASE 140
		EXP:口交経験 += 1
		EXP:喉性交経験 += 1
		SIF TEQUIP:深喉口交
			EXP:喉性交経験 += 1
		EXP:PLAYER:腰技経験 += 1
	CASE 141 TO 143
		IF ARG == 141 || ARG == 143
			EXP:V经验 += 5
			IF SIZE("拳交", PLAYER) >= ABL:V扩张 - 1
				EXP:V经验 += SIZE("拳交", TARGET) - ABL:V扩张 + 1
				CALL SETFLAG, "V扩张", SIZE("拳交", PLAYER), HARD("拳交", PLAYER)
			ENDIF
			SIF SIZE("拳交", PLAYER) >= 3
				EXP:V拳交経験 += 1
			CALL SETFLAG, "V插入拳交", SIZE("拳交", PLAYER)
		ENDIF
		IF ARG == 142 || ARG == 143
			EXP:PLAYER:肛虐経験 += 5
			EXP:A经验 += 5
			IF SIZE("拳交", PLAYER) >= ABL:A扩张 - 1
				EXP:A经验 += SIZE("拳交", PLAYER) - ABL:A扩张 + 1
				CALL SETFLAG, "A扩张", SIZE("拳交", PLAYER), HARD("拳交", PLAYER)
			ENDIF
			SIF SIZE("拳交", PLAYER) >= 3
				EXP:A拳交経験 += 1
			CALL SETFLAG, "A插入拳交", SIZE("拳交", PLAYER)
		ENDIF
		EXP:PLAYER:手交経験 += 1
	CASE 144
		EXP:放尿経験 += 1
	CASE 145
		EXP:露出経験 += 2
		;录像摄影、馆内露出、野外PLAYは経験等にプラス
		SIF TEQUIP:摄像机
			EXP:露出経験 += 1
		SIF TEQUIP:屋内露出PLAY || TEQUIP:野外PLAY
			EXP:露出経験 += 1
		EXP:剃毛経験 += 1
	CASE 147
		EXP:精液経験 += TFLAG:16 * 3
		ITEM:精液储蓄量 -= TFLAG:16
		SIF FLAG:8
			PRINTFORML 魔法瓶中的精液残量：{ITEM:精液储蓄量}
	CASE 149
		IF TEQUIP:导尿管
			EXP:尿道経験 += 1
			EXP:PLAYER:刺激尿道経験 += 1
		ENDIF
	CASE 150
		IF TEQUIP:深喉口交
			EXP:口交経験 += 1
			EXP:喉性交経験 += 1
		ENDIF
	CASE 151
		IF TEQUIP:尿道ブジー
			;连续实行（尿道扩张之方は、飛んだ后で増加する处理アリ）
			IF PREVCOM == SELECTCOM
				EXP:尿道経験 += 1 + (ABL:PLAYER:S属性 + 1)/2
				EXP:露出経験 += 1 + (ABL:PLAYER:S属性 + 1)/2
				EXP:PLAYER:刺激尿道経験 += 1 + (ABL:PLAYER:S属性 + 1)/2
			ENDIF

			EXP:尿道経験 += 1
			CALL SETFLAG, "尿道扩张", TEQUIP:尿道ブジー
			EXP:PLAYER:刺激尿道経験 += 1

			EXP:露出経験 += 1
			SIF TEQUIP:摄像机
				EXP:露出経験 += 1
		ENDIF
	CASE 160
		EXP:PLAYER:腰技経験 += 1
		EXP:ASSI:V经验 += 1
		EXP:ASSI:腰技経験 += 1
		IF PENIS(PLAYER)
			EXP:PLAYER:V插入経験 += 1
			EXP:ASSI:V性交経験 += 1
		ENDIF
		CALL SETFLAG, "V插入阴茎", SIZE("阴茎", PLAYER), ASSI, PLAYER
	CASE 161
		EXP:腰技経験 += 1
		EXP:ASSI:V经验 += 1
		IF PENIS(TARGET)
			EXP:V插入経験 += 1
			EXP:ASSI:V性交経験 += 1
		ENDIF
		CALL SETFLAG, "V插入阴茎", SIZE("阴茎", TARGET), ASSI, TARGET
	CASE 162
		EXP:ASSI:口交経験 += 1
	CASE 210
		SIF TEQUIP:触手口辱
			EXP:口交経験 += 1
		IF TEQUIP:按摩棒
			EXP:V经验 += 1
			;大きいと経験BＯＮＵS
			SIF SIZE("按摩棒", TARGET) >= 3
				EXP:V经验 += 1
			IF SIZE("按摩棒", TARGET) >= MAX(ABL:V扩张 - 1, 2)
				EXP:V经验 += SIZE("按摩棒", TARGET) - ABL:V扩张 + 1
				CALL SETFLAG, "V扩张", SIZE("按摩棒", TARGET), HARD("按摩棒", TARGET)
			ENDIF
		ENDIF
		IF TEQUIP:肛用按摩棒
			EXP:A经验 += 1
			;大きいと経験BＯＮＵS
			SIF SIZE("肛用按摩棒", TARGET) >= 3
				EXP:A经验 += 1
			IF SIZE("肛用按摩棒", TARGET) >= MAX(ABL:A扩张 - 1, 2)
				EXP:A经验 += SIZE("肛用按摩棒", TARGET) - ABL:A扩张 + 1
				CALL SETFLAG, "A扩张", SIZE("肛用按摩棒", TARGET), HARD("按摩棒", TARGET)
			ENDIF
		ENDIF
	CASE 260
		IF CHECK_COM("润滑液按摩", ARG)
			EXP:PLAYER:手交経験 += 1
			EXP:A经验 += 1
			EXP:PLAYER:肛虐経験 += 1
		ENDIF
	CASE 261
		SIF TEQUIP:媚药
			EXP:媚药経験 += MAX(TEQUIP:媚药/100, 1)
		SIF TEQUIP:PLAYER:媚药
			EXP:PLAYER:媚药経験 += MAX(TEQUIP:PLAYER:媚药/100, 1)
	CASE 282
		EXP:PLAYER:自慰経験 += 1
	CASE 303
		EXP:紧縛経験 += 5
	CASE 304
		EXP:V经验 += 3
		EXP:V性交経験 += 3
		IF SIZE("龙") >= ABL:V扩张 - 1
			EXP:V经验 += SIZE("龙") - ABL:V扩张 + 1
			CALL SETFLAG, "V扩张", SIZE("龙"), HARD("龙")
		ENDIF
		EXP:V龙奸経験 += 1
		CALL SETFLAG, "V插入阴茎", SIZE("龙"), TARGET, -3
	CASE 305
		EXP:A经验 += 3
		EXP:A性交経験 += 3
		IF SIZE("龙") >= ABL:A扩张 - 1
			EXP:A经验 += SIZE("龙") - ABL:A扩张 + 1
			CALL SETFLAG, "A扩张", SIZE("龙"), HARD("龙")
		ENDIF
		EXP:A龙奸経験 += 1
		CALL SETFLAG, "A插入阴茎", SIZE("龙"), TARGET, -3
	CASE 307
		EXP:V经验 += 1
		EXP:史莱姆経験 += 1
	CASE 308
		EXP:PLAYER:肛虐経験 += 1
		EXP:A经验 += 1
		EXP:史莱姆経験 += 1
	ENDSELECT

	;逆调教指令
	SELECTCASE ARG
	CASE 106, 107, 120 TO 129
		SWAP TARGET, PLAYER
	CASE 304, 305
		;逆龙奸
		SIF TEQUIP:逆推 || TEQUIP:PLAYER:逆推
			SWAP TARGET, PLAYER
	ENDSELECT

	;百合、BL経験増加
	IF TALENT:性别 != 1 && TALENT:PLAYER:性别 != 0
		EXP:百合経験 += CALCF("百合・男同度", ARG)
		EXP:PLAYER:百合経験 += CALCF("百合・男同度", ARG)
	ELSEIF TALENT:性别 == 1 && TALENT:PLAYER:性别 == 1
		EXP:男同経験 += CALCF("百合・男同度", ARG)
		EXP:PLAYER:男同経験 += CALCF("百合・男同度", ARG)
	ENDIF

	;ARG:1に２以上之数字が代入されている场合には獲得経験をARG:1倍する
	SELECTCASE ARG:1
	CASE IS >= 2
		FOR LCOUNT, 0, 100
			SELECTCASE LCOUNT
			;増えない経験
			CASE 64, 65
			CASEELSE
				EXP:LCOUNT += (EXP:LCOUNT - LOCAL:LCOUNT) * (ARG:1 - 1)
				EXP:PLAYER:LCOUNT += (EXP:PLAYER:LCOUNT - LOCAL:(LCOUNT + 100) ) * (ARG:1 - 1)
			ENDSELECT
		NEXT
	ENDSELECT
CASE "体力气力减少"
	;苦痛からくる体力・气力之减少 ABL:M属性をみる
	LOCAL = MULTIPLY(CUP:苦痛, 100 - ABL:M属性*15)
	;抖M
	SIF TALENT:抖M
		TIMES LOCAL, 0.50

	LOCAL = GETPALAMLV(LOCAL, 9)
	LOCAL = LOCAL * LOCAL * 10

	DOWNBASE:体力 += LOCAL
	DOWNBASE:气力 += LOCAL

	;逆推
	IF TEQUIP:逆推
		TIMES DOWNBASE:体力, 0.80
		DOWNBASE:气力 /= 2
	ENDIF

	IF STATE("烂醉", TARGET)
		TIMES DOWNBASE:体力, 1.50
	ELSEIF STATE("醉酒", TARGET)
		TIMES DOWNBASE:体力, 1.20
	ENDIF

	;沐浴剂
	IF TEQUIP:MASTER:沐浴剂 == 2 || TEQUIP:MASTER:沐浴剂 == 3
		TIMES DOWNBASE:体力, 0.80
		TIMES DOWNBASE:气力, 0.80
	ENDIF
	IF COND("连续实行")
		TIMES DOWNBASE:体力, 1.50
		TIMES DOWNBASE:气力, 1.50
	ENDIF
	;倍化术
	IF STATE("倍化术", PLAYER) && TEQUIP:逆推 == 0
		SELECTCASE SELECTCOM
		CASE 3, 13, 90 TO 129, 139, IS >= 170
		CASEELSE
			TIMES DOWNBASE:体力, 2.00
			TIMES DOWNBASE:气力, 2.00
		ENDSELECT
	ENDIF

	;-------------------------------------------------
	;气力０、精力０による上下之处理
	;-------------------------------------------------
	SIF BASE:气力 <= 0 && DOWNBASE:体力 > 0
		DOWNBASE:体力 += 50
	SIF BASE:精力 <= 0 && DOWNBASE:气力 > 0
		DOWNBASE:气力 += 30

	;损伤している场合には１LVにつき50
	DOWNBASE:气力 += (TALENT:V损伤 + TALENT:A损伤)*50
	DOWNBASE:体力 += (TALENT:V损伤 + TALENT:A损伤)*50

	;BASEからDOWNBASEを引く
	BASE:体力 = MAX(BASE:体力 - DOWNBASE:体力, 0)
	;一方的にむさぼったあとなら
	SIF TEQUIP:逆推 && BASE:体力 <= 0
		BASE:体力 = 1
	BASE:气力 = MAX(BASE:气力 - DOWNBASE:气力, 0)
CASE "绝顶させた处理"
;---------------------------------------------------------
;调教におけるPLAYERが绝顶したとき之处理
;对象と调教者を两方指定できる之が望ましいが、PLAYERを对象にしている时固定とする
;ARG:1に日常中かどうかを入れる(调教中でないなら0以外を入力しておく）
;---------------------------------------------------------
	VARSET LOCAL
	;TARGETと助手だと异なる变数を一时的に交换
	IF ARG == ASSI && SELECTCOM:1 >= 0
		SWAP SELECTCOM, SELECTCOM:1
		SWAP PREVCOM, PREVCOM:1
		SWAP TCVAR:PLAYER:5, TCVAR:PLAYER:6
	ENDIF
	;SOURCE之补正
	CALL SETFLAG, "追加之SOURCE", ARG
	;助手指令
	IF ARG != ASSI && ASSI && SHOOT("助手")
		IF PENIS(PLAYER)
			IF SHOOT("助手之口")
				LOCAL:1 = 3
				CALL SETFLAG, "饮精経験", COND("绝顶", PLAYER), ASSI
			ELSEIF SHOOT("助手之膣内")
				LOCAL:1 = 2
				CALL SETFLAG, "膣内精液経験", COND("绝顶", PLAYER), ASSI
			ENDIF
			IF LOCAL:1
				EXP:ASSI:精液経験 += COND("绝顶", PLAYER) * LOCAL:1
				SIF FLAG:8
					PRINTFORML 精液経験+{COND("绝顶", PLAYER) * LOCAL:1}(助手
			ENDIF
			;PLAYER之阴茎に精液汚れを追加
			CALL ADD_STAIN, PLAYER, "P", "精液"
		ENDIF
		;MASTERを高潮回数＆経験值獲得
		SIF PLAYER == MASTER
			EXP:ASSI:高潮回数 += 1
		CALL GET_EXPERIENCE(COND("绝顶", PLAYER), ASSI)
		;最初之变数交换は发生していない之で中断ＯＫ
		RETURN 0
	;PLAYERが射精した
	ELSEIF PENIS(PLAYER)
		;饮む＞中出＞冲击之顺で精液経験が高い
		IF SHOOT("口")
			LOCAL = 3
			CALL SETFLAG, "饮精経験", COND("绝顶", PLAYER), ARG
		ELSEIF SHOOT("中出")
			LOCAL = 2
			IF SHOOT("膣内")
				CALL SETFLAG, "膣内精液経験", COND("绝顶", PLAYER), ARG
			ELSEIF SHOOT("A")
				CALL SETFLAG, "肛内精液経験", COND("绝顶", PLAYER), ARG
			ELSEIF SHOOT("尿道")
				CALL SETFLAG, "膀胱精液経験", COND("绝顶", PLAYER), ARG
			ENDIF
		ELSEIF SHOOT("乳头中出")
			LOCAL = 2
			CALL SETFLAG, "乳内精液経験", COND("绝顶", PLAYER), ARG
		ELSEIF SHOOT("除了自己以外中出")
			LOCAL = 0
		ELSEIF SHOOT("魔法瓶") == 0
			LOCAL = 1
			;液体追加之ソース
			IF SHOOT("观察")
				SOURCE:ARG:液体追加 += COND("绝顶", PLAYER) * LOCAL * 100
			ELSEIF SHOOT("A观察")
				SOURCE:ARG:A液体追加 += COND("绝顶", PLAYER) * LOCAL * 100
			ELSEIF SHOOT("股间")
				SOURCE:ARG:液体追加 += COND("绝顶", PLAYER) * LOCAL * 50
				SOURCE:ARG:A液体追加 += COND("绝顶", PLAYER) * LOCAL * 50
			ENDIF
		ENDIF
		IF LOCAL
			;饮精绝顶。何って之はないけどフレーバー的な
			IF SHOOT("口") && NOWEX:ARG:绝顶数合计 && ABL:ARG:精液成瘾
				SIF FLAG:8
					CALL PRINTL_COLORTEXT, "饮精绝顶（成瘾充足之ソースにBＯＮＵSが发生します）", DEF_COLOR("黄色")
				SOURCE:ARG:成瘾充足 += 3000
			ENDIF
			EXP:ARG:精液経験 += COND("绝顶", PLAYER) * LOCAL
			SIF FLAG:8
				PRINTFORML 精液経験+{COND("绝顶", PLAYER) * LOCAL}\@ ARG == ASSI ? (助手）# \@
			IF TALENT:ARG:梦魇 || TALENT:ARG:上位梦魇 || TALENT:ARG:吸精体質
				DOWNBASE:ARG:体力 -= COND("绝顶", PLAYER) * LOCAL * 5
				DOWNBASE:ARG:气力 -= COND("绝顶", PLAYER) * LOCAL * 5
				;精力も回復
				IF MAXBASE:ARG:精力 - BASE:ARG:精力 > 0
					LOCAL:1 = LIMIT(COND("绝顶", PLAYER) * LOCAL / 4, 1, MAXBASE:ARG:精力 - BASE:ARG:精力)
					BASE:ARG:精力 += LOCAL:1
					;TCVAR:ARG:109 -= LOCAL:1
					SIF FLAG:8
						PRINTFORML 精力+{LOCAL:1}\@ ARG == ASSI ? (助手）# \@
				ENDIF
			ENDIF
		ENDIF

		;成瘾充足之ソース
		IF ABL:ARG:精液成瘾
			SOURCE:ARG:成瘾充足 += COND("绝顶", PLAYER) * ABL:ARG:精液成瘾 * 100

			;被射敏感
			IF SHOOT("口")
				SIF COND("弱点：饮精", ARG)
					SOURCE:ARG:成瘾充足 += 3000
			ELSEIF SHOOT("V中出")
				SIF COND("弱点：腔内射精", ARG)
					SOURCE:ARG:成瘾充足 += 3000
			ELSEIF SHOOT("A中出")
				SIF COND("弱点：肛内射精", ARG)
					SOURCE:ARG:成瘾充足 += 3000
			ELSEIF SHOOT("乳头奸")
				SIF COND("弱点：乳内射精", ARG)
					SOURCE:ARG:成瘾充足 += 3000
			ELSEIF SHOOT("尿道中出")
				SIF COND("弱点：尿道射精", ARG)
					SOURCE:ARG:成瘾充足 += 3000
			ENDIF
		ENDIF

		;PLAYER之阴茎に精液汚れを追加
		CALL ADD_STAIN, PLAYER, "P", "精液"
		;精液を出したところに精液汚れを追加
		;口に汚れを追加
		IF SHOOT("口")
			;梦魇之口⇔调教者之P之汚れが移动
			CALL EQ_STAIN, ARG, PLAYER, 0, 2
			CALL ADD_STAIN, ARG, "M", "精液"
		;胸に汚れを追加
		ELSEIF SHOOT("胸")
			CALL ADD_STAIN, ARG, "B", "精液"
		;Vに汚れを追加
		ELSEIF SHOOT("V中出") || SHOOT("股间")
			;摩擦胖次
			IF SELECTCOM == 97
				STAIN:V = SET_STAIN(STAIN:V, "精液")
			ELSE
				CALL ADD_STAIN, ARG, "V", "精液"
			ENDIF
		;Aに汚れを追加
		ELSEIF SHOOT("A中出")
			CALL ADD_STAIN, ARG, "A", "精液"
		;手や胖次に汚れを追加
		ELSEIF SHOOT("手")
			;摩擦胖次
			IF SELECTCOM == 97
				CALL ADD_STAIN, ARG, "胖次", "精液"
			ELSE
				CALL ADD_STAIN, ARG, "手", "精液"
			ENDIF
		ENDIF
	ENDIF
	;MASTERを高潮回数＆経験值獲得
	SIF PLAYER == MASTER
		EXP:ARG:高潮回数 += 1
	CALL GET_EXPERIENCE(COND("绝顶", PLAYER), ARG)
	IF ARG == TARGET
		;好感觉补正
		TFLAG:30 += COND("绝顶", PLAYER) + ABL:ARG:精液成瘾
		SIF TALENT:ARG:恋慕 == 0 && MARK:ARG:反抗刻印
			TFLAG:30 -= TALENT:ARG:反抗心*2 + TALENT:ARG:看重贞操*2+ MARK:ARG:反抗刻印*MARK:ARG:反抗刻印
	ENDIF
	;变数を戻す
	IF ARG == ASSI && SELECTCOM:1 >= 0
		SWAP SELECTCOM, SELECTCOM:1
		SWAP PREVCOM, PREVCOM:1
		SWAP TCVAR:PLAYER:5, TCVAR:PLAYER:6
	ENDIF
CASE "绝顶した处理"
	SIF COND("绝顶", ARG) == 0
		RETURN 0
	;堪えた标志を初期化
	EX:ARG:忍住了没丢 = 0
	;精力を减少(对象が吸精体質だと必要量増)
	LOCAL = COND("绝顶", ARG)*2; + MAX(TALENT:吸精体質 - TALENT:ARG:吸精体質, 0)
	SIF SELECTCOM:1 >= 0
		LOCAL += MAX(TALENT:ASSI:吸精体質 - TALENT:ARG:吸精体質, 0)
	;性别女は优遇
	SIF TALENT:ARG:性别女 && LOCAL >= 6
		LOCAL = 6

	IF PENIS(ARG)
		NOWEX:ARG:射精 = COND("绝顶", ARG)
		EX:ARG:射精 += NOWEX:ARG:射精
		EXP:ARG:射精経験 += NOWEX:ARG:射精
		IF TALENT:ARG:早泄
			TIMES LOCAL, 0.75
		ELSEIF TALENT:ARG:晚泄
			TIMES LOCAL, 2.00
		ENDIF
	ELSE
		EXP:ARG:绝顶経験 += COND("绝顶", ARG)
	ENDIF

	LOCAL = MAX(LOCAL, 1)

	BASE:ARG:精力 = MAX(BASE:ARG:精力 - LOCAL, 0)
	TCVAR:ARG:109 += LOCAL
	IF FLAG:8
		PRINTFORM 精力-{LOCAL}
		IF ARG == MASTER
			PRINTFORM  (%CALLNAME:ARG%)
		ELSEIF ARG == ASSI
			PRINTFORM （助手）
		ENDIF
		PRINTL 
	ENDIF
	;射精槽之调整
	BASE:ARG:绝顶 = MAXBASE:ARG:绝顶  * (1 + MAX(BASE:ARG:精力, 1) )
	BASE:ARG:绝顶 /= 2 * (1 + MAXBASE:ARG:精力) * COND("绝顶", ARG)
	;快感许容值（阴茎）之调整
	BASE:ARG:快感许容值男 = BASE:ARG:绝顶
CASE "同时绝顶"
	VARSET LOCAL
	LOCAL = COND("绝顶", PLAYER)
	SIF SELECTCOM:2 >= 0
		LOCAL:1 = COND("绝顶", PLAYER:1)

	LOCAL:2 = MIN(NOWEX:绝顶数合计, 2)
	SIF SELECTCOM:1 >= 0 && ASSI
		LOCAL:3 = MIN(NOWEX:ASSI:绝顶数合计, 2)

	;PLAYER側とTARGET側が两方绝顶する事が条件
	SIF MAX(LOCAL, LOCAL:1) == 0 || MAX(LOCAL:2, LOCAL:3) == 0
		RETURN 0
	;既に同时绝顶となっている场合には繰り返さない
	SIF NOWEX:PLAYER:同时绝顶 || NOWEX:同时绝顶
		RETURN 0

	NOWEX:PLAYER:同时绝顶 = LOCAL
	EX:PLAYER:同时绝顶 += NOWEX:PLAYER:同时绝顶
	NOWEX:同时绝顶 = LOCAL:2
	EX:同时绝顶 += NOWEX:同时绝顶
	IF ASSI
		NOWEX:(PLAYER:1):同时绝顶 = LOCAL:1
		EX:(PLAYER:1):同时绝顶 += NOWEX:(PLAYER:1):同时绝顶
		NOWEX:ASSI:同时绝顶 = LOCAL:3
		EX:ASSI:同时绝顶 += NOWEX:ASSI:同时绝顶
	ENDIF

	SIF FLAG:8 == 0
		RETURN 0

	IF MAX(LOCAL, LOCAL:1) >= 2 && MAX(LOCAL:2, LOCAL:3) >= 2
		LOCALS = 同时强绝顶
	ELSE
		LOCALS = 同时绝顶
	ENDIF

	IF LOCAL && LOCAL:1 + LOCAL:3 && LOCAL:2
		LOCALS = %LOCALS%（みんな）
	ELSEIF LOCAL
		LOCALS = %LOCALS%（%CALLNAME:PLAYER%＆\@ LOCAL:2 ? %CALLNAME:TARGET% # %CALLNAME:ASSI% \@）
	ELSEIF LOCAL:1
		LOCALS = %LOCALS%（%CALLNAME:(PLAYER:1)%＆%CALLNAME:TARGET%）
	ENDIF

	CALL PRINTL_COLORTEXT, LOCALS, DEF_COLOR("粉红色心形")

	STR:9 = %LOCALS%

CASE "调教ソース之计算"
	TRYCALLFORM SOURCE_COM{SELECTCOM}, 100
	;-------------------------------------------------
	;SOURCE计算
	;-------------------------------------------------
	CALL CALC_SOURCE, "调教指令"

CASE "调教ソース之試算"
	TRYCALLFORM SOURCE_COM{SELECTCOM}, 100
	;-------------------------------------------------
	;SOURCE计算
	;-------------------------------------------------
	CALL CALC_SOURCE, "試算"

CASE "调教之标志取得"
	TRYCALLFORM FLAG_COM{SELECTCOM}, 100
;需要独立重构
CASE "调教补正值"
	SIF ARG == PLAYER
		RETURN 0

	VARSET TCVAR:ARG:0, 0, 600, 649

	;まずはSOURCE补正（下之ほうにCUP补正もあり）
	;CVABM之快感补正
	;～感觉LVや淫～系之素質によって对象之技巧やLVやＨ氛围で之マイナス修正が无视されます
	FOR LCOUNT, 0, 5
		VARSET LOCAL
		LOCAL += TALENT:ARG:接受快感*20 + STATE("魅强术", PLAYER)*10
		LOCAL -= TALENT:ARG:冷静*90 + TALENT:ARG:魔导人偶*50 + STATE("虚弱术", PLAYER)*50 + STATE("烂醉", PLAYER)*30

		SELECTCASE CONDS("机嫌", ARG)
		CASE "没干劲"
			LOCAL -= 100
		ENDSELECT

		SELECTCASE MARK:ARG:反抗刻印
		CASE 3
			LOCAL -= 50
		CASE 2
			LOCAL -= 20
		CASE 1
			LOCAL -= 10
		ENDSELECT

		;Ｈ氛围をみる（低いとき之修正は"快感上升之上限措置"で主に行う）
		SELECTCASE MOOD_HEART()
		CASE 0
			LOCAL:1 = -40
		CASE 1
			LOCAL:1 = -20
		CASE 2
			LOCAL:1 = -10
		CASEELSE
			LOCAL:1 = LIMIT(MOOD() - 70, 0, 100)
		ENDSELECT
		;淫乱な人はＨ氛围低くても関係なし
		SIF TALENT:ARG:淫乱 && LOCAL:1 < 0
			LOCAL:1 = 0
		;好色な人はＨ氛围が低くてもエンジンかかりやすい
		SIF TALENT:ARG:好色 && LOCAL:1 < 0
			LOCAL:1 /= 2
		;冷静持ちはＨ氛围に弱い
		SIF TALENT:ARG:冷静 && LOCAL:1 > 0
			LOCAL:1 += LIMIT(LOCAL:1, 20, 120)

		;调教者と之ABL:技巧差
		LOCAL:2 = MAX(-50, (ABL:PLAYER:技巧 - ABL:ARG:技巧)*25)

		;EASYモード
		IF CONFIG("EASY")
			SIF LOCAL < 0
				LOCAL /= 2
			LOCAL:1 = MAX(LOCAL:1, 0)
			LOCAL:2 = MAX(LOCAL:2, 0)
		ENDIF

		SELECTCASE LCOUNT
		CASE 0
			;阴茎はＨ氛围等にあまり左右されない
			SIF LOCAL:1 > 0 || (LOCAL:1 < 0 && PENIS(ARG) == 0 && TALENT:ARG:淫核 == 0)
				LOCAL += LOCAL:1
			SIF LOCAL:2 > 0 || (LOCAL:2 < 0 && PENIS(ARG) == 0 && ABL:ARG:C感觉 < 5 && TALENT:ARG:C敏感 == 0)
				LOCAL += LOCAL:2
			TCVAR:ARG:600 += LOCAL + TALENT:ARG:淫核*20
		CASE 1
			SIF LOCAL:1 > 0 || (LOCAL:1 < 0 && TALENT:ARG:淫穴 == 0)
				LOCAL += LOCAL:1
			SIF LOCAL:2 > 0 || (LOCAL:2 < 0 && ABL:ARG:V感觉 < 5 && TALENT:ARG:V敏感 == 0)
				LOCAL += LOCAL:2
			TCVAR:ARG:601 += LOCAL + TALENT:ARG:淫穴*20 + MARK:ARG:淫紋*10
		CASE 2
			SIF LOCAL:1 > 0 || (LOCAL:1 < 0 && TALENT:ARG:尻穴狂 == 0)
				LOCAL += LOCAL:1
			SIF LOCAL:2 > 0 || (LOCAL:2 < 0 && ABL:ARG:A感觉 < 5 && TALENT:ARG:A敏感 == 0)
				LOCAL += LOCAL:2
			TCVAR:ARG:602 += LOCAL + TALENT:ARG:尻穴狂*20
		CASE 3
			SIF LOCAL:1 > 0 || (LOCAL:1 < 0 && TALENT:ARG:淫乳 == 0)
				LOCAL += LOCAL:1
			SIF LOCAL:2 > 0 || (LOCAL:2 < 0 && ABL:ARG:B感觉 < 5 && TALENT:ARG:B敏感 == 0)
				LOCAL += LOCAL:2
			TCVAR:ARG:617 += LOCAL + TALENT:ARG:淫乳*20
		CASE 4
			SIF LOCAL:1 > 0 || (LOCAL:1 < 0 && TALENT:ARG:荡唇 == 0)
				LOCAL += LOCAL:1
			SIF LOCAL:2 > 0 || (LOCAL:2 < 0 && ABL:ARG:M感觉 < 5 && TALENT:ARG:M敏感 == 0)
				LOCAL += LOCAL:2
			TCVAR:ARG:618 += LOCAL + TALENT:ARG:荡唇*20
		ENDSELECT
	NEXT

	SIF TALENT:ARG:泌乳体質
		TCVAR:ARG:617 += 50
	SIF COND("发情期", ARG)
		TCVAR:ARG:601 += 100
	;VA损伤
	TCVAR:ARG:601 -= 200 * TALENT:ARG:V损伤
	TCVAR:ARG:602 -= 200 * TALENT:ARG:A损伤

	;快感以外
	IF TALENT:ARG:魔导人偶
		TCVAR:ARG:606 -= 50
		TCVAR:ARG:614 -= 50
		TCVAR:ARG:615 -= 90
	ENDIF
	IF TALENT:ARG:史莱姆
		TCVAR:ARG:606 -= 200
		TCVAR:ARG:610 += 50
	ENDIF

	SIF TALENT:ARG:怕羞
		TCVAR:ARG:612 += 100
	SIF TALENT:ARG:不知耻
		TCVAR:ARG:612 -= 50
	SIF TALENT:ARG:清纯
		TCVAR:ARG:612 += 50
	SIF TALENT:ARG:好奇心
		TCVAR:ARG:614 -= 30
	SIF TALENT:ARG:保守
		TCVAR:ARG:614 += 100
	SIF TALENT:ARG:好色
		TCVAR:ARG:614 -= 10
	SIF TALENT:ARG:淫乱
		TCVAR:ARG:614 -= 50

	;重复前提で计算
	IF TALENT:ARG:恋慕 && PLAYER == MASTER
		TCVAR:ARG:603 += 20
		IF GETBIT(NOWEX:ARG:处女丧失, 1)
			TCVAR:ARG:603 += 100
			TCVAR:ARG:606 -= 50
			TCVAR:ARG:614 -= 300
			TCVAR:ARG:615 -= 300
		ENDIF
	ENDIF

	;逆推BＯＮＵS
	IF TEQUIP:ARG:逆推
		TCVAR:ARG:606 -= 50
		TCVAR:ARG:609 -= 100
		TCVAR:ARG:613 -= 100
		TCVAR:ARG:614 -= 50
		TCVAR:ARG:615 -= 100
	ENDIF

	;放任生长
	IF COND("阴毛", ARG) == 7 && CHECK_CLO("V露出", ARG)
		TCVAR:ARG:612 += 30
	;光溜溜
	ELSEIF COND("阴毛", ARG) >= 2 && COND("阴毛", ARG) <= 3 && CHECK_CLO("V露出", ARG)
		TCVAR:ARG:612 += 20
	ENDIF

	;性器穿环
	SIF CHECK_CLO("V露出", ARG)
		TCVAR:ARG:612 += TALENT:ARG:阴蒂穿环*20 + TALENT:ARG:阴唇穿环*10
	SIF CHECK_CLO("乳首露出", ARG)
		TCVAR:ARG:612 += TALENT:ARG:乳头穿环*10

	;同时绝顶
	IF NOWEX:ARG:同时绝顶
		TCVAR:ARG:603 += NOWEX:ARG:同时绝顶 * 10
		TCVAR:ARG:605 += NOWEX:ARG:同时绝顶 * 10
		TCVAR:ARG:609 -= NOWEX:ARG:同时绝顶 * 20
		TCVAR:ARG:615 -= NOWEX:ARG:同时绝顶 * 20
	ENDIF

	;异常経験が多いほど、逃脱之SOURCE之増加が(异常経験*3%)少なくなり、屈从之SOURCEが(异常経験*3%)増加しやすくなる
	TCVAR:ARG:613 += EXP:ARG:异常経験*3
	TCVAR:ARG:614 -= EXP:ARG:异常経験*3

	;LOCAL之值で情爱,性行动,达成感,成瘾充足,液体追加,欲情追加,反感追加,恭顺追加に一斉补正
	VARSET LOCAL

	SELECTCASE CONDS("机嫌", ARG)
	CASE "观望"
		LOCAL += 100
		TCVAR:ARG:613 -= 100
	CASE "没干劲"
		LOCAL += 200
		TCVAR:ARG:613 -= 200
	ENDSELECT

	;VA损伤
	LOCAL:1 = EXP_GAIN(64, ARG) + EXP_GAIN(65, ARG) + TALENT:ARG:V损伤 + TALENT:ARG:A损伤
	IF LOCAL:1
		LOCAL += LOCAL:1 * 50
		TCVAR:ARG:609 += LOCAL:1 * 50
		TCVAR:ARG:614 += LOCAL:1 * 50
	ENDIF

	;精力０
	SIF BASE:ARG:精力 <= 0
		LOCAL += 50

	TCVAR:ARG:603 -= LOCAL
	TCVAR:ARG:604 -= LOCAL
	TCVAR:ARG:605 -= LOCAL
	TCVAR:ARG:607 -= LOCAL
	TCVAR:ARG:610 -= LOCAL
	TCVAR:ARG:611 -=  LOCAL
	TCVAR:ARG:615 +=  LOCAL
	TCVAR:ARG:616 -=  LOCAL

	SIF TALENT:PLAYER:解放
		TCVAR:ARG:603 += 20
	IF TALENT:PLAYER:魅惑
		TCVAR:ARG:614 -= 10
		TCVAR:ARG:615 -= 10
	ENDIF
	IF TALENT:PLAYER:迷之魅力
		TCVAR:ARG:603 += 10
		TCVAR:ARG:605 += 10
		TCVAR:ARG:614 -= 10
		TCVAR:ARG:615 -= 10
	ENDIF

	;装饰品效果（魅力）
	IF TALENT:PLAYER:魅力强化 > 0
		TCVAR:ARG:614 -= TALENT:PLAYER:魅力强化
		TCVAR:ARG:615 -= TALENT:PLAYER:魅力强化
	ENDIF

	SELECTCASE STATE("魅强术", PLAYER)
	CASE 3
		TCVAR:ARG:603 += 10
		TCVAR:ARG:605 += 10
		TCVAR:ARG:614 -= 10
		TCVAR:ARG:615 -= 10
	CASE 2
		TCVAR:ARG:614 -= 10
		TCVAR:ARG:615 -= 10
	ENDSELECT

	SIF TALENT:PLAYER:史莱姆
		TCVAR:ARG:610 += 30
	;虚弱术
	IF STATE("虚弱术", PLAYER)
		TCVAR:ARG:603 -= 30
		TCVAR:ARG:604 -= 30
		TCVAR:ARG:605 -= 30
		TCVAR:ARG:611 -= 30
		TCVAR:ARG:613 -= 30
		TCVAR:ARG:616 -= 30
	ENDIF

	;百合
	IF TALENT:ARG:性别 != 1 && TALENT:PLAYER:性别 != 0
		LOCAL = 0
		TCVAR:ARG:605 += LOCAL - 30
		TCVAR:ARG:614 += 20 - LOCAL
	ELSEIF TALENT:ARG:性别 == 1 && TALENT:PLAYER:性别 == 1
		LOCAL = 0
		TCVAR:ARG:605 += LOCAL - 30
		TCVAR:ARG:614 += 20 - LOCAL
	ENDIF

	TCVAR:ARG:614 -= TEQUIP:ARG:媚药/5
	;野外など
	SIF TEQUIP:ARG:屋内露出PLAY || TEQUIP:ARG:摄像机 || TEQUIP:ARG:野外PLAY || TEQUIP:ARG:羞恥PLAY
		TCVAR:ARG:612 += 20

	;反抗刻印が无いなら绝顶补正
	IF NOWEX:ARG:绝顶数合计 && MARK:ARG:反抗刻印 == 0
		TCVAR:ARG:612 -= 20
		TCVAR:ARG:614 -= 20
		TCVAR:ARG:615 -= 10
	ENDIF

	;SOURCE:快C(快C)～SOURCE:快M(快M)之补正值之最終結果
	FOR LCOUNT, 600, 620
		;マイナスPALAM之ソースは、最低值が-99。其他は、-80
		SELECTCASE LCOUNT
		CASE 608, 609, 614, 615
			TCVAR:ARG:LCOUNT = MAX(TCVAR:ARG:LCOUNT, -99)
		CASEELSE
			TCVAR:ARG:LCOUNT = MAX(TCVAR:ARG:LCOUNT, -80)
		ENDSELECT
	NEXT

	;以下はCUP补正
	;阴蒂や肛门之露出具合
	SELECTCASE COND("阴毛", ARG)
	CASE 1 TO 3
		TCVAR:ARG:620 += 10
	CASE IS >= 6
		TCVAR:ARG:622 -= 10
	ENDSELECT
	;阴蒂や乳首之露出、勃起具合。穿环优遇。
	TCVAR:ARG:620 += CALCF("C发情", ARG)*5
	TCVAR:ARG:634 += CALCF("B发情", ARG)*5
	IF STATE("小根术", PLAYER) && TALENT:ARG:妖精 == 0
		SIF INSERT("V插入")
			TCVAR:ARG:621 -= 30
		SIF INSERT("A插入")
			TCVAR:ARG:622 -= 30
		SIF INSERT("尿道插入")
			TCVAR:ARG:620 -= 30
	ENDIF
	SIF TALENT:ARG:性别 == 1
		TCVAR:ARG:622 += 20
	;装饰品效果（CVABM强化）
	SIF TALENT:PLAYER:C强化 > 0
		TCVAR:ARG:620 += TALENT:PLAYER:C强化
	SIF TALENT:PLAYER:V强化 > 0
		TCVAR:ARG:621 += TALENT:PLAYER:V强化
	SIF TALENT:PLAYER:A强化 > 0
		TCVAR:ARG:622 += TALENT:PLAYER:A强化
	SIF TALENT:PLAYER:B强化 > 0
		TCVAR:ARG:634 += TALENT:PLAYER:B强化
	SIF TALENT:PLAYER:M强化 > 0
		TCVAR:ARG:635 += TALENT:PLAYER:M强化
	;～感觉LVや淫～系之素質によって对象之技巧やLVなどで之マイナス修正が无视されます
	FOR LCOUNT, 0, 5
		VARSET LOCAL

		LOCAL -= TEQUIP:ARG:利尿剂*30
		LOCAL += TEQUIP:ARG:媚药

		SIF TEQUIP:ARG:触手
			LOCAL += 100
		SIF TALENT:PLAYER:解放
			LOCAL += 20
		SIF TEQUIP:ARG:爱抚
			LOCAL += 20
		SIF STATE("脱力", PLAYER)
			LOCAL -= 100

		;-------------------------------------------------
		;装饰品效果（技巧）による上下之处理
		;-------------------------------------------------
		SIF TALENT:PLAYER:技巧强化 > 0
			LOCAL += TALENT:PLAYER:技巧强化
		;-------------------------------------------------
		;相性による上下之处理（快乐系）
		;-------------------------------------------------
		SIF RELATION:ARG:(NO:PLAYER) > 0
			LOCAL += RELATION:ARG:(NO:PLAYER) - 100
		;-------------------------------------------------
		;LVで快感之补正をする（快乐系）
		;-------------------------------------------------
		LOCAL:1 = 0

		;补正がマイナス之时には技巧で緩和
		IF LOCAL:1 < 0
			SIF CONFIG("EASY")
				LOCAL:1 /= 2
			LOCAL:1 = MIN(LOCAL:1 + ABL:PLAYER:技巧*10, 0)
		ENDIF

		SELECTCASE LCOUNT
		CASE 0
			;阴茎はあまり左右されない
			SIF LOCAL:1 < 0 && (TALENT:ARG:C敏感 || PENIS(ARG) || ABL:ARG:C感觉 >= 5 || TALENT:ARG:淫核)
				LOCAL:1 = 0
			TCVAR:ARG:620 += LOCAL + LOCAL:1
		CASE 1
			SIF LOCAL:1 < 0 && (TALENT:ARG:V敏感 || ABL:ARG:V感觉 >= 5 || TALENT:ARG:淫穴)
				LOCAL:1 = 0
			TCVAR:ARG:621 += LOCAL + LOCAL:1
		CASE 2
			SIF LOCAL:1 < 0 && (TALENT:ARG:A敏感 || ABL:ARG:A感觉 >= 5 || TALENT:ARG:尻穴狂)
				LOCAL:1 = 0
			TCVAR:ARG:622 += LOCAL + LOCAL:1
		CASE 3
			SIF LOCAL:1 < 0 && (TALENT:ARG:B敏感 || ABL:ARG:B感觉 >= 5 || TALENT:ARG:淫乳)
				LOCAL:1 = 0
			TCVAR:ARG:634 += LOCAL + LOCAL:1
		CASE 4
			SIF LOCAL:1 < 0 && (TALENT:ARG:M敏感 || ABL:ARG:M感觉 >= 5 || TALENT:ARG:荡唇)
				LOCAL:1 = 0
			TCVAR:ARG:635 += LOCAL + LOCAL:1
		ENDSELECT
	NEXT

	;炭酸浴室
	IF TEQUIP:MASTER:沐浴剂 == 3
		TCVAR:ARG:620 += 10
		TCVAR:ARG:634 += 10
	ENDIF

	IF TALENT:ARG:胆怯
		TCVAR:ARG:630 += 100
		TCVAR:ARG:631 -= 30
		TCVAR:ARG:633 -= 75
	ENDIF
	IF TALENT:ARG:反抗心
		TCVAR:ARG:624 -= 75
		TCVAR:ARG:625 -= 50
		TCVAR:ARG:631 += 50
	ENDIF
	IF TALENT:ARG:刚强
		TCVAR:ARG:624 -= 20
		TCVAR:ARG:625 -= 25
		TCVAR:ARG:630 -= 50
		TCVAR:ARG:631 += 50
		TCVAR:ARG:633 += 50
	ENDIF
	IF TALENT:ARG:坦率
		TCVAR:ARG:624 += 100
		TCVAR:ARG:631 -= 20
	ENDIF
	SIF TALENT:ARG:内向
		TCVAR:ARG:631 -= 50
	IF TALENT:ARG:傲慢
		TCVAR:ARG:626 -= 50
		TCVAR:ARG:630 -= 40
		TCVAR:ARG:631 += 20
	ENDIF
	IF TALENT:ARG:低姿态
		TCVAR:ARG:626 += 100
		TCVAR:ARG:630 += 50
		TCVAR:ARG:631 -= 20
	ENDIF
	IF TALENT:ARG:强硬
		TCVAR:ARG:628 -= 20
		TCVAR:ARG:630 -= 50
	ENDIF
	SIF TALENT:ARG:坏心眼
		TCVAR:ARG:624 -= 20
	SIF TALENT:ARG:冷静
		TCVAR:ARG:630 -= 50
	IF TALENT:ARG:不关心
		TCVAR:ARG:624 -= 50
		TCVAR:ARG:625 -= 50
		TCVAR:ARG:626 -= 50
		TCVAR:ARG:630 -= 20
		TCVAR:ARG:631 -= 20
	ENDIF
	IF TALENT:ARG:缺乏情感
		TCVAR:ARG:624 -= 50
		TCVAR:ARG:625 -= 50
		TCVAR:ARG:626 -= 30
		TCVAR:ARG:628 -= 20
		TCVAR:ARG:630 -= 40
		TCVAR:ARG:631 -= 20
		TCVAR:ARG:633 -= 40
	ENDIF
	SIF TALENT:ARG:好奇心
		TCVAR:ARG:627 += 20
	SIF TALENT:ARG:保守
		TCVAR:ARG:627 -= 20
	SIF TALENT:ARG:开朗
		TCVAR:ARG:633 -= 70
	SIF TALENT:ARG:阴郁
		TCVAR:ARG:633 += 150
	IF TALENT:ARG:压抑
		TCVAR:ARG:625 -= 50
		TCVAR:ARG:631 += 50
		TCVAR:ARG:633 += 50
	ENDIF
	SIF TALENT:ARG:解放
		TCVAR:ARG:625 += 50
	IF TALENT:ARG:抵抗
		TCVAR:ARG:625 -= 50
		TCVAR:ARG:631 += 100
	ENDIF
	IF TALENT:ARG:纯情
		TCVAR:ARG:624 += 20
		TCVAR:ARG:626 += 10
	ENDIF
	IF TALENT:ARG:清纯
		TCVAR:ARG:624 += 20
		TCVAR:ARG:628 += 50
	ENDIF

	SIF TALENT:ARG:肠液分泌体質
		TCVAR:ARG:632 += 200
	SIF TALENT:ARG:发情体質
		TCVAR:ARG:625 += 50

	SIF TALENT:ARG:聪慧
		TCVAR:ARG:627 += 100
	IF TALENT:ARG:愚笨
		TCVAR:ARG:627 -= 30
		TCVAR:ARG:627 += TEQUIP:ARG:称賛 * 10
	ENDIF
	IF TALENT:ARG:献身
		TCVAR:ARG:626 += 50
		TCVAR:ARG:627 += 50
	ENDIF
	SIF TALENT:ARG:接受快感
		TCVAR:ARG:625 += 50
	SIF TALENT:ARG:否认快感
		TCVAR:ARG:625 -= 50
	IF TALENT:ARG:好色
		TCVAR:ARG:625 += 20
		TCVAR:ARG:628 -= 20
	ENDIF
	IF TALENT:ARG:接受快感 + TALENT:ARG:好色 && SEXUAL_PLAY()
		TCVAR:ARG:625 += 30
		TCVAR:ARG:630 -= 50
		TCVAR:ARG:631 -= 50
		TCVAR:ARG:633 -= 50
	ENDIF
	IF TALENT:ARG:性变态
		TCVAR:ARG:630 -= 50
		TCVAR:ARG:631 -= 20
		TCVAR:ARG:633 -= 20
		IF ABNORMAL_PLAY()
			TCVAR:ARG:625 += 50
			TCVAR:ARG:631 -= 30
			TCVAR:ARG:633 -= 30
		ENDIF
	ENDIF
	IF TALENT:ARG:恋慕 && PLAYER == MASTER
		TCVAR:ARG:631 -= 30
	ENDIF
	IF TALENT:ARG:妄信 && PLAYER == MASTER
		TCVAR:ARG:626 += 300
		TCVAR:ARG:631 -= 50
	ENDIF

	LOCAL = STATE("醉酒", ARG) + STATE("烂醉", ARG)
	;醉酒
	IF LOCAL
		TCVAR:ARG:624 -= LOCAL*20
		TCVAR:ARG:625 += LOCAL*30
		TCVAR:ARG:627 -= LOCAL*50
		TCVAR:ARG:628 -= LOCAL*30
		TCVAR:ARG:629 -= LOCAL*10
		TCVAR:ARG:630 -= LOCAL*10
		TCVAR:ARG:631 -= LOCAL*10
		TCVAR:ARG:633 -= LOCAL*30
	ENDIF
	IF TALENT:ARG:魔导人偶
		TCVAR:ARG:630 -= 80
		TCVAR:ARG:631 -= 80
		TCVAR:ARG:633 -= 80
	ENDIF
	IF TALENT:ARG:史莱姆
		TCVAR:ARG:629 -= 100
	ENDIF
	IF TALENT:ARG:上位梦魇
		TCVAR:ARG:624 -= 20
		TCVAR:ARG:625 -= 20
		TCVAR:ARG:626 -= 20
	ENDIF
	IF TCVAR:ARG:28
		;自分からやってる行动に对する不满が少ない人
		IF TALENT:ARG:开朗 || TALENT:ARG:聪慧
			TCVAR:ARG:630 -= 50 + TCVAR:ARG:28
			TCVAR:ARG:631 -= 50 + TCVAR:ARG:28
		ELSE
			TCVAR:ARG:630 -= 10 + TCVAR:ARG:28
			TCVAR:ARG:631 -= 10 + TCVAR:ARG:28
		ENDIF
	ENDIF
	IF COND("发情期", ARG)
		TCVAR:ARG:625 += 100
		TCVAR:ARG:626 -= 50
		TCVAR:ARG:627 -= 100
		TCVAR:ARG:628 -= 50
		TCVAR:ARG:630 -= 20
		TCVAR:ARG:631 -= 20
		TCVAR:ARG:633 -= 20
	ENDIF

	TCVAR:ARG:631 -= MARK:ARG:苦痛刻印*10

	SELECTCASE MARK:ARG:反抗刻印
	CASE 3
		TCVAR:ARG:624 -= 40
		TCVAR:ARG:625 -= 40
	CASE 2
		TCVAR:ARG:624 -= 20
		TCVAR:ARG:625 -= 20
	CASE 1
		TCVAR:ARG:624 -= 10
		TCVAR:ARG:625 -= 10
	ENDSELECT

	;开心（最低值自体が变わります！）
	SELECTCASE MARK:ARG:机嫌
	CASE 1
		SIF TCVAR:ARG:631 < 80
			TCVAR:ARG:631 = 80
		SIF TCVAR:ARG:633 < 80
			TCVAR:ARG:633 = 80
	CASE 2
		SIF TCVAR:ARG:631 < 200
			TCVAR:ARG:631 = 200
		SIF TCVAR:ARG:633 < 200
			TCVAR:ARG:633 = 200
	ENDSELECT

	;逆推or称賛BＯＮＵS
	IF TEQUIP:ARG:逆推 || TEQUIP:ARG:称賛指令から之派生标志
		TCVAR:ARG:630 -= 100
		TCVAR:ARG:631 -= 200
		TCVAR:ARG:633 -= 100
	ENDIF

	;六九式BＯＮＵS
	IF TEQUIP:ARG:六九式
		TCVAR:ARG:625 += 50
		TCVAR:ARG:628 += 50
	ENDIF

	;以下SM系。M属性と抖Mで色々緩和
	LOCAL = ABL:ARG:M属性*5 + TALENT:ARG:抖M*10
	;眼罩
	IF TEQUIP:ARG:眼罩
		TCVAR:ARG:624 += LOCAL - 10
		TCVAR:ARG:625 += ABL:ARG:M属性*5
		TCVAR:ARG:626 += 20
		TCVAR:ARG:627 -= 50
		TCVAR:ARG:630 += 20
	ENDIF
	;绳・拘束
	IF TEQUIP:ARG:拘束
		;苦痛を伴う
		SIF GETBIT(TEQUIP:ARG:拘束, 0)
			TCVAR:ARG:629 += 10
		SIF GETBIT(TEQUIP:ARG:拘束, 1)
			TCVAR:ARG:629 += 20
		SIF GETBIT(TEQUIP:ARG:拘束, 2)
			TCVAR:ARG:629 += 40
		;羞恥を伴う
		SIF GETBIT(TEQUIP:ARG:拘束, 3)
			TCVAR:ARG:628 += 10
		SIF GETBIT(TEQUIP:ARG:拘束, 4)
			TCVAR:ARG:628 += 20
		SIF GETBIT(TEQUIP:ARG:拘束, 5)
			TCVAR:ARG:628 += 40
		;恐怖を伴う
		SIF GETBIT(TEQUIP:ARG:拘束, 6)
			TCVAR:ARG:630 += 10
		SIF GETBIT(TEQUIP:ARG:拘束, 7)
			TCVAR:ARG:630 += 20
		SIF GETBIT(TEQUIP:ARG:拘束, 8)
			TCVAR:ARG:630 += 40

		;9bit 手が使えない 10bit 足が使えない 11bit 体勢が变えられない
		LOCAL:1 = 0
		SIF GETBIT(TEQUIP:ARG:拘束, 9)
			LOCAL:1 += 20
		SIF GETBIT(TEQUIP:ARG:拘束, 10)
			LOCAL:1 += 10
		SIF GETBIT(TEQUIP:ARG:拘束, 11)
			LOCAL:1 += 30

		TCVAR:ARG:624 -= 30 + LOCAL:1/2 - LOCAL
		TCVAR:ARG:625 += ABL:ARG:M属性*5
		TCVAR:ARG:626 += LOCAL:1
		TCVAR:ARG:627 -= LOCAL:1
		TCVAR:ARG:630 += LOCAL:1
		TCVAR:ARG:633 += LOCAL:1 - LOCAL*2
	ENDIF
	;口枷
	IF TEQUIP:ARG:口枷
		TCVAR:ARG:624 -= 20 - LOCAL
		TCVAR:ARG:625 += ABL:ARG:M属性*5
		TCVAR:ARG:626 += 20
		TCVAR:ARG:627 -= 10
		TCVAR:ARG:630 += 10
	ENDIF
	;灌肠
	IF TEQUIP:ARG:灌肠 || SELECTCOM == 136 || NOWEX:ARG:排泄
		LOCAL:1 = COND("液体灌肠", ARG)*10 + 10
		;排泄标志ＯＮ（こ之回合に漏らした）
		IF NOWEX:ARG:排泄
			LOCAL:1 = 200
		;栓无
		ELSEIF COND("肛塞", ARG) == 0
			LOCAL:1 += 30
		ENDIF
		TCVAR:ARG:624 -= 50 + LOCAL:1 - LOCAL*2
		TCVAR:ARG:626 += 50 + LOCAL:1
		TCVAR:ARG:627 -= 50 + LOCAL:1
		TCVAR:ARG:630 += 50 + LOCAL:1
	ENDIF

	LOCAL = 0
	;主人による调教経験
	IF MARK:ARG:屈服刻印 == 3
		LOCAL = 40
	ELSEIF CFLAG:ARG:2 >= 2000 || MARK:ARG:屈服刻印 == 2
		LOCAL = 30
	ELSEIF CFLAG:ARG:2 >= 700 || MARK:ARG:屈服刻印 == 1
		LOCAL = 20
	ELSEIF CFLAG:ARG:2 >= 100
		LOCAL = 10
	ENDIF
	TCVAR:ARG:631 -= LOCAL

	;调教者之ABL:技巧によって、教え方が上手くなる
	SELECTCASE ABL:PLAYER:技巧 - ABL:ARG:技巧
	CASE IS >= 3
		TCVAR:ARG:627 += 50
	CASE IS >= 2
		TCVAR:ARG:627 += 30
	CASE IS >= 1
		TCVAR:ARG:627 += 10
	ENDSELECT

	IF TEQUIP:ARG:媚药
		TCVAR:ARG:624 -= TEQUIP:ARG:媚药/2
		TCVAR:ARG:625 += TEQUIP:ARG:媚药/2
		TCVAR:ARG:627 -= TEQUIP:ARG:媚药/2
		TCVAR:ARG:629 -= TEQUIP:ARG:媚药/2
		TCVAR:ARG:630 -= TEQUIP:ARG:媚药
		TCVAR:ARG:631 -= TEQUIP:ARG:媚药/4
	ENDIF
	IF TEQUIP:ARG:利尿剂
		TCVAR:ARG:624 -= 20
		TCVAR:ARG:625 -= 20
		TCVAR:ARG:626 += 20
		TCVAR:ARG:631 += 50
		TCVAR:ARG:633 += 50
	ENDIF

	SELECTCASE TEQUIP:MASTER:沐浴剂
	;秘湯之沐浴剂
	CASE 2
		TCVAR:ARG:625 += 10
		TCVAR:ARG:628 -= 50
	;バラ之花びら
	CASE 4
		TCVAR:ARG:625 += 30
	;ひ之き
	CASE 6
		TCVAR:ARG:630 -= 20
		TCVAR:ARG:631 -= 20
		TCVAR:ARG:633 -= 20
	;ミステリアス
	CASE 7
		TCVAR:ARG:630 += 20
	ENDSELECT
	;野外PLAY…花園、森林、水边、海はBＯＮＵSあり
	IF COND("森林", ARG) || COND("花園", ARG) || COND("水边", ARG) || COND("海", ARG)
		SIF COND("森林", ARG)
			TCVAR:ARG:625 += 20
		SIF COND("花園", ARG)
			TCVAR:ARG:624 += 20
		SIF COND("水边", ARG)
			TCVAR:ARG:523 += 20
		TCVAR:ARG:631 -= 20
		TCVAR:ARG:633 -= 20
	ENDIF
	;爱抚
	IF TEQUIP:ARG:爱抚
		TCVAR:ARG:630 -= 10
		TCVAR:ARG:631 -= 10
		TCVAR:ARG:633 -= 10
	ENDIF
	;褒めて开心取り
	IF TEQUIP:ARG:称賛
		TCVAR:ARG:624 += 10
		TCVAR:ARG:630 -= 10
		TCVAR:ARG:631 -= 10
		TCVAR:ARG:633 -= 10
	ENDIF
	;甜言蜜语
	IF TEQUIP:ARG:甜言蜜语 >= 1 && TEQUIP:ARG:甜言蜜语 <= 8
		TCVAR:ARG:624 += 10
		TCVAR:ARG:625 += 10
		TCVAR:ARG:630 -= 10
		TCVAR:ARG:631 -= 10
		TCVAR:ARG:633 -= 10
	ENDIF
	;Ｈ氛围をみる
	LOCAL = MOOD() / 5
	TCVAR:ARG:624 += LOCAL
	TCVAR:ARG:625 += LOCAL
	TCVAR:ARG:630 -= LOCAL
	TCVAR:ARG:631 -= LOCAL
	TCVAR:ARG:633 -= LOCAL
	;反抗刻印が无いなら绝顶补正
	IF NOWEX:ARG:绝顶数合计 && MARK:ARG:反抗刻印 == 0
		TCVAR:ARG:630 -= 20
		TCVAR:ARG:631 -= 20
	ENDIF
	;奉仕指令之やる气补正
	IF TCVAR:ARG:28
		TCVAR:ARG:627 += TCVAR:ARG:28
		TCVAR:ARG:628 -= TCVAR:ARG:28
		TCVAR:ARG:630 -= TCVAR:ARG:28
		TCVAR:ARG:631 -= TCVAR:ARG:28
		TCVAR:ARG:633 -= TCVAR:ARG:28
	ENDIF

	LOCAL = -1
	;以下、ASSI,MASTER之调教していない側之素質
	IF ASSIPLAY
		LOCAL = RELATION:ARG:(NO:MASTER)
	ELSEIF ASSI
		LOCAL = RELATION:ARG:(NO:ASSI)
	ENDIF
	IF LOCAL > 100
		;あくまでSUBな之で、补正をかける
		LOCAL = (LOCAL - 100)/2
		TCVAR:ARG:631 -= LOCAL
		TCVAR:ARG:633 -= LOCAL
	ENDIF

	;-------------------------------------------------
	;同じ指令之连续实行による上下之处理
	;-------------------------------------------------
	;连续实行之例外
	LOCAL = 0
	SELECTCASE SELECTCOM
	CASE 31, 201
		SIF TEQUIP:ARG:按摩棒 == 0
			LOCAL = 1
	CASE 33, 202
		SIF TEQUIP:ARG:肛用按摩棒 == 0
			LOCAL = 1
	CASE 34, 203
		SIF TEQUIP:ARG:阴蒂夹 == 0
			LOCAL = 1
	CASE 35, 204
		SIF TEQUIP:ARG:乳头夹 == 0
			LOCAL = 1
	CASE 36, 205
		SIF TEQUIP:ARG:搾乳机 == 0
			LOCAL = 1
	CASE 38
		SIF TEQUIP:ARG:淋浴 == 0
			LOCAL = 1
	CASE 136, 207
		SIF TEQUIP:ARG:灌肠 == 0
			LOCAL = 1
	ENDSELECT

	;同じ指令之连续实行
	SIF SELECTCOM == PREVCOM && LOCAL == 0
		TCVAR:ARG:627 += 50
	;-------------------------------------------------
	;气力０による上下之处理
	;-------------------------------------------------
	IF BASE:ARG:气力 <= 0
		TCVAR:ARG:624 -= 90
		TCVAR:ARG:625 -= 90
		TCVAR:ARG:627 -= 90
		TCVAR:ARG:631 += 20
		TCVAR:ARG:633 += 20
	ENDIF

	;V润はある程度以上は増えずらい
	SELECTCASE PALAM:ARG:V润
	CASE IS <= 10000
	CASE IS <= 30000
		TCVAR:ARG:623 -= 100
	CASE IS <= 60000
		TCVAR:ARG:623 -= 200
	CASE IS <= 100000
		TCVAR:ARG:623 -= 400
	ENDSELECT
	;CUP:快C(快C) ～ CUP:快M(快M)之补正值之最終結果
	FOR LCOUNT, 620, 640
		SELECTCASE LCOUNT
		CASE 630, 631, 633
			TCVAR:ARG:LCOUNT = MAX(TCVAR:ARG:LCOUNT, -99)
		CASEELSE
			TCVAR:ARG:LCOUNT = MAX(TCVAR:ARG:LCOUNT, -80)
		ENDSELECT
	NEXT

CASE "被射敏感"
	;ここは最初に得る时限定之场所な之で、既に素質を持っているならば終了。精液成瘾も必須
	SIF TALENT:ARG:被射敏感 || ABL:ARG:精液成瘾 == 0
		RETURN 0
	;精液を受け止めた経験が必要
	SIF MAX(EXP:ARG:精液経験/10, EXP:ARG:膣内精液経験, EXP:ARG:肛内精液経験, EXP:ARG:饮精経験, EXP:ARG:乳内精液経験*10, EXP:ARG:膀胱精液経験*5) < 20
		RETURN 0
	SIF ABL:ARG:精液成瘾 + TALENT:ARG:性变态 + (EXP:ARG:膣内精液経験 + EXP:ARG:肛内精液経験 + EXP:ARG:饮精経験 + EXP:ARG:乳内精液経験*10 + EXP:ARG:膀胱精液経験*5)/100 < 3
		RETURN 0

	;以下でどこに射精される之に弱いかを決める
	LOCAL:0 = EXP:ARG:饮精経験 * (ABL:ARG:M感觉 + TALENT:ARG:M敏感 - TALENT:ARG:M钝感 + TALENT:ARG:荡唇*2)
	LOCAL:1 = EXP:ARG:膣内精液経験 * (ABL:ARG:V感觉 + TALENT:ARG:V敏感 - TALENT:ARG:V钝感 + TALENT:ARG:淫穴*2)
	LOCAL:2 = EXP:ARG:肛内精液経験 * (ABL:ARG:A感觉 + TALENT:ARG:A敏感 - TALENT:ARG:A钝感 + TALENT:ARG:尻穴狂*2)
	;以下は难易度高い之でサービスとして他よりもなりやすい
	LOCAL:3 = EXP:ARG:乳内精液経験 * 10 * (ABL:ARG:B感觉 + TALENT:ARG:B敏感 - TALENT:ARG:B钝感 + TALENT:ARG:淫乳*2)
	LOCAL:4 = EXP:ARG:膀胱精液経験 * 5 * TALENT:ARG:尿道性感
	;上之パ回合之うち最大之值をセット
	LOCAL:9 = MAX(LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4)
	;0bit弱点：饮精 1bit弱点：腔内射精 2bit弱点：肛内射精 3bit弱点：乳内射精 4bit弱点：尿道射精
	IF LOCAL:0 >= LOCAL:9
		TALENT:ARG:被射敏感 = 1p0
	ELSEIF LOCAL:1 >= LOCAL:9
		TALENT:ARG:被射敏感 = 1p1
	ELSEIF LOCAL:2 >= LOCAL:9
		TALENT:ARG:被射敏感 = 1p2
	ELSEIF LOCAL:3 >= LOCAL:9
		TALENT:ARG:被射敏感 = 1p3
	ELSEIF LOCAL:4 >= LOCAL:9
		TALENT:ARG:被射敏感 = 1p4
	ENDIF
CASE "被射敏感部位之变化"
	;素質を持って无いならば終了
	SIF TALENT:ARG:被射敏感 == 0
		RETURN 0

	;以下でどこに射精される之に弱いかを決める
	;VA以外之部位はサービスとして他よりもなりやすい
	LOCAL:0 = EXP:ARG:饮精経験 * 3 * (ABL:ARG:M感觉 + TALENT:ARG:M敏感 - TALENT:ARG:M钝感 + TALENT:ARG:荡唇*2)
	LOCAL:1 = EXP:ARG:膣内精液経験 * (ABL:ARG:V感觉 + TALENT:ARG:V敏感 - TALENT:ARG:V钝感 + TALENT:ARG:淫穴*2 + MARK:ARG:淫紋)
	LOCAL:2 = EXP:ARG:肛内精液経験 * (ABL:ARG:A感觉 + TALENT:ARG:A敏感 - TALENT:ARG:A钝感 + TALENT:ARG:尻穴狂*2)
	LOCAL:3 = EXP:ARG:乳内精液経験 * 10 * (ABL:ARG:B感觉 + TALENT:ARG:B敏感 - TALENT:ARG:B钝感 + TALENT:ARG:淫乳*2)
	LOCAL:4 = EXP:ARG:膀胱精液経験 * 5 * TALENT:ARG:尿道性感
	;上之パ回合之うち最大之值をセット
	LOCAL:9 = MAX(LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4)
	;ここで最大之值が50未满なら变化は起きない
	SIF LOCAL:9 < 50
		RETURN 0

	;0bit弱点：饮精 1bit弱点：腔内射精 2bit弱点：肛内射精 3bit弱点：乳内射精 4bit弱点：尿道射精
	;变更する際には１番大きい值が２番目之值よりも500多いか倍なければならないとする
	IF LOCAL:0 >= LOCAL:9
		SIF LOCAL:0 >= MIN(MAX(LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4)*2, MAX(LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4) + 500)
			TALENT:ARG:被射敏感 = 1p0
	ELSEIF LOCAL:1 >= LOCAL:9
		SIF LOCAL:1 >= MIN(MAX(LOCAL:0, LOCAL:2, LOCAL:3, LOCAL:4)*2, MAX(LOCAL:0, LOCAL:2, LOCAL:3, LOCAL:4) + 500)
			TALENT:ARG:被射敏感 = 1p1
	ELSEIF LOCAL:2 >= LOCAL:9
		SIF LOCAL:2 >= MIN(MAX(LOCAL:0, LOCAL:1, LOCAL:3, LOCAL:4)*2, MAX(LOCAL:0, LOCAL:1, LOCAL:3, LOCAL:4) + 500)
			TALENT:ARG:被射敏感 = 1p2
	ELSEIF LOCAL:3 >= LOCAL:9
		SIF LOCAL:3 >= MIN(MAX(LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:4)*2, MAX(LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:4) + 500)
			TALENT:ARG:被射敏感 = 1p3
	ELSEIF LOCAL:4 >= LOCAL:9
		SIF LOCAL:4 >= MIN(MAX(LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3)*2, MAX(LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3) + 500)
			TALENT:ARG:被射敏感 = 1p4
	ENDIF
CASE "中意的体位"
	IF TALENT:ARG:性别 == 1 || ARG == MASTER
		TALENT:ARG:中意的体位 = 0
		RETURN 0
	ENDIF
	;bitで管理する
	;0正常位, 1后背位, 2屈曲位, 3对面座位, 4背面座位, 5奉仕骑乘位, 6逆推骑乘位, 9特になし
	;10～16はA体位, 19黒桃A, 20～26は尿道体位

	VARSET LOCAL

	;初期設定
	IF TALENT:ARG:中意的体位 == 0 && MAX(EXP:ARG:V性交経験, EXP:ARG:A性交経験, EXP:ARG:腰技経験) >= 10
		LOCAL:0 = TALENT:ARG:清纯 + TALENT:ARG:坦率 + TALENT:ARG:优雅
		LOCAL:1 = TALENT:ARG:好色 + TALENT:ARG:粗野
		LOCAL:2 = TALENT:ARG:低姿态
		LOCAL:3 = TALENT:ARG:内向 + TALENT:ARG:胆怯
		LOCAL:4 = TALENT:ARG:开朗
		LOCAL:5 = TALENT:ARG:坏心眼 + TALENT:ARG:献身
		LOCAL:6 = TALENT:ARG:好色 + TALENT:ARG:强硬 + TALENT:ARG:抖S + TALENT:ARG:傲慢
		LOCAL:9 = TALENT:ARG:阴郁 + TALENT:ARG:冷静

		LOCAL:90 = MAX(LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5)

		IF LOCAL:9 >= MAX(LOCAL:90, LOCAL:6, LOCAL:19)
			SETBIT TALENT:ARG:中意的体位, 9
			RETURN 0
		ENDIF

		SIF ABL:ARG:A感觉 > MAX(ABL:ARG:V感觉, 2)
			LOCAL:10 = 10

		SELECTCASE LOCAL:90
		CASE IS < LOCAL:6
		CASE LOCAL:0
			SETBIT TALENT:ARG:中意的体位, 0 + LOCAL:10
		CASE LOCAL:1
			SETBIT TALENT:ARG:中意的体位, 1 + LOCAL:10
		CASE LOCAL:2
			SETBIT TALENT:ARG:中意的体位, 2 + LOCAL:10
		CASE LOCAL:3
			SETBIT TALENT:ARG:中意的体位, 3 + LOCAL:10
		CASE LOCAL:4
			SETBIT TALENT:ARG:中意的体位, 4 + LOCAL:10
		CASE LOCAL:5
			SETBIT TALENT:ARG:中意的体位, 5 + LOCAL:10
		ENDSELECT
		SIF LOCAL:6 >= LOCAL:90
			SETBIT TALENT:ARG:中意的体位, 6 + LOCAL:10
	ELSEIF TALENT:ARG:中意的体位 == 0
		SETBIT TALENT:ARG:中意的体位, 9
	ELSE
		LOCAL:0 = NUM_STR(CSTR:ARG:2, "正常位")
		LOCAL:1 = NUM_STR(CSTR:ARG:2, "后背位")
		LOCAL:2 = NUM_STR(CSTR:ARG:2, "屈曲位")
		LOCAL:3 = NUM_STR(CSTR:ARG:2, "对面座位")
		LOCAL:4 = NUM_STR(CSTR:ARG:2, "背面座位")
		LOCAL:5 = NUM_STR(CSTR:ARG:2, "骑乘位")
		LOCAL:6 = NUM_STR(CSTR:ARG:2, "逆推骑乘位")
		LOCAL:10 = NUM_STR(CSTR:ARG:2, "肛交正常位")
		LOCAL:11 = NUM_STR(CSTR:ARG:2, "肛交后背位")
		LOCAL:12 = NUM_STR(CSTR:ARG:2, "肛交屈曲位")
		LOCAL:13 = NUM_STR(CSTR:ARG:2, "肛交对面座位")
		LOCAL:14 = NUM_STR(CSTR:ARG:2, "肛交背面座位")
		LOCAL:15 = NUM_STR(CSTR:ARG:2, "肛交骑乘位")
		LOCAL:16 = NUM_STR(CSTR:ARG:2, "逆推肛交骑乘位")
		LOCAL:19 = NUM_STR(CSTR:ARG:2, "黒桃A")
		LOCAL:20 = NUM_STR(CSTR:ARG:2, "尿道插入")
		LOCAL:26 = NUM_STR(CSTR:ARG:2, "逆推尿道骑乘位")
		;今まで之中意的体位之ポイントは？
		FOR LCOUNT, 0, 30
			;PRINTFORML LOCAL:{LCOUNT} = {LOCAL:LCOUNT}
			SIF GETBIT(TALENT:ARG:中意的体位, LCOUNT) == 0
				CONTINUE
			LOCAL:30 = MAX(LOCAL:LCOUNT, LOCAL:30)
		NEXT

		;ポイント上位二つを選ぶ。LOCAL:40が１位、LOCAL:41が２位とする
		FOR LCOUNT, 0, 30
			;２位未满
			SIF LOCAL:LCOUNT < LOCAL:(LOCAL:41)
				CONTINUE
			SELECTCASE LOCAL:LCOUNT
			CASE IS >= LOCAL:(LOCAL:40)
				LOCAL:41 = LOCAL:40
				LOCAL:40 = LCOUNT
			CASE IS >= LOCAL:(LOCAL:41)
				LOCAL:41 = LCOUNT
			ENDSELECT
		NEXT

		;PRINTFORML LOCAL:30 = {LOCAL:30}, LOCAL:40 = {LOCAL:40}, LOCAL:41 = {LOCAL:41}, 
		;10以上、今まで之中意的体位之ポイント之最大值を上回っていると中意的体位が变化する
		IF LOCAL:(LOCAL:40) - 10 >= LOCAL:30
			;(正常位～奉仕骑乘位)、逆推骑乘位、(A正常位～A奉仕骑乘位)、逆推A骑乘位、磨镜 之中から上位二つを選ぶ
			TALENT:ARG:中意的体位 = 0
			SETBIT TALENT:ARG:中意的体位, LOCAL:40

			;基本は１つだけだが、１位と２位之差が２倍以内、かつ、２位之ポイントが10以上ならば２位も中意的体位となる
			SIF LOCAL:(LOCAL:41)*3 >= LOCAL:(LOCAL:40) && LOCAL:(LOCAL:41) >= 10
				SETBIT TALENT:ARG:中意的体位, LOCAL:41
		ENDIF
	ENDIF
CASE "每天之阴毛＆腋毛处理"
	SIF COND("阴毛", ARG) <= 1 || TALENT:ARG:魔导人偶
		RETURN 0

	;日数経過とともに阴毛が伸びます。若いとあんま生えない
	IF BASE:ARG:阴毛%100 < 90
		SELECTCASE TALENT:ARG:外表年龄
		CASE 0, 1
			BASE:ARG:阴毛 += 1
		CASE 2
			BASE:ARG:阴毛 += 2
		CASE IS >= 3
			BASE:ARG:阴毛 += 3
		ENDSELECT
	ENDIF
	LOCAL = BASE:ARG:阴毛%100

	SELECTCASE COND("阴毛", ARG)
	;脱毛状态→光溜溜
	CASE 2
		SIF LOCAL >= LIMIT(30 * TALENT:ARG:外表年龄, 30, 60)
			BASE:ARG:阴毛 = 300
	;光溜溜→刚刚长出（成熟は朝になると…）
	CASE 3
		SIF LOCAL >= 6 || TALENT:ARG:外表年龄 >= 3
			BASE:ARG:阴毛 = 400
	;刚刚长出→自然生长
	CASE 4
		SIF LOCAL >= 20 + RAND:20 && TALENT:ARG:外表年龄
			BASE:ARG:阴毛 = 600
	;お手入れ→自然生长
	CASE 5
		IF LOCAL >= 30 + RAND:20
			BASE:ARG:阴毛 = 600
			;小孩子なら、刚刚长出になる
			SIF TALENT:ARG:外表年龄 == 0
				BASE:ARG:阴毛 = 400
		ENDIF
	;大姐姐以上は自然生长→放任生长
	CASE 6
		SIF LOCAL >= 40 + RAND:40 && TALENT:ARG:外表年龄 >= 2
			BASE:ARG:阴毛 = 700
	ENDSELECT

	;梦魇によるお手入れ
	IF COND("外观：阴毛", ARG)
		SELECTCASE COND("外观：阴毛", ARG)
		CASE 3, 5
			SIF COND("外观：阴毛", ARG) <= COND("阴毛", ARG)
				BASE:ARG:阴毛 = COND("外观：阴毛", ARG)*100
		ENDSELECT
	;剃るなと指示されていない
	ELSE
		;抖Mッ气や露阴癖が高いと、たま～に光溜溜にします
		IF COND("阴毛", ARG) >= 4 && RAND:30 == 0 && MAX(ABL:ARG:露阴癖 - 1, ABL:ARG:M属性) >= 3
			BASE:ARG:阴毛 = 300
			;PRINTFORMW %CALLNAME:ARG%的阴毛被剃得光溜溜地……
		;食い込んでいる衣装之时には剃り整えます
		ELSEIF COND("阴毛", ARG) >= 6 && CHECK_CLO("紧勒", ARG)
			BASE:ARG:阴毛 = 500
		;放任生长状态は、ほぼ确实に避けます
		ELSEIF COND("阴毛", ARG) == 7 && RAND:100
			;怕羞+好色でない 梦魇は、剃ったと悟られる之が嫌な之で、自然生长になるよう頑張ります
			IF (TALENT:ARG:保守 + TALENT:ARG:怕羞 && TALENT:ARG:好色 == 0) || RAND:10
				BASE:ARG:阴毛 = 600
			ELSE
				BASE:ARG:阴毛 = 500
				;PRINTFORMW %CALLNAME:ARG%的阴毛被整齐地剃掉了……
			ENDIF
		;自然生长は、たま～に剃ります。好色だったり、夏が近いと、高确率で剃り整えます
		ELSEIF COND("阴毛", ARG) == 6 && (!RAND:500 || (FLAG:871 + 1 >= 20 + RAND:20 && FLAG:871 + 1 < 60 && !RAND:3) || (TALENT:ARG:好色 && COND("梦魇", ARG) && RAND:2) )
			BASE:ARG:阴毛 = 500
			;PRINTFORMW %CALLNAME:ARG%的阴毛被整齐地剃掉了……
		ENDIF
	ENDIF

	;次いで腋毛之处理
	SIF CONFIG("腋毛") == 0
		RETURN 0

	;日数経過とともに腋毛が伸びます。若いとあんま生えない
	SIF BASE:ARG:腋毛%100 < 90
		BASE:ARG:腋毛 += 1 + TALENT:ARG:外表年龄/2

	LOCAL = BASE:ARG:阴毛%100

	SELECTCASE COND("腋毛", ARG)
	;脱毛状态→光溜溜
	CASE 2
		SIF LOCAL >= LIMIT(30 * TALENT:ARG:外表年龄, 30, 60)
			BASE:ARG:腋毛 = 300
	;光溜溜→刚刚长出
	CASE 3
		SIF LOCAL >= 6
			BASE:ARG:腋毛 = 400
	;刚刚长出→放任生长
	CASE 4
		SIF LOCAL >= 20 + RAND:20 && TALENT:ARG:外表年龄
			BASE:ARG:腋毛 = 700
	ENDSELECT

	;梦魇によるお手入れ
	IF COND("外观：腋毛", ARG)
		SELECTCASE COND("外观：腋毛", ARG)
		CASE 3
			SIF COND("外观：腋毛", ARG) <= COND("腋毛", ARG)
				BASE:ARG:腋毛 = COND("外观：腋毛", ARG)*100
		ENDSELECT
	;剃るなと指示されていない
	ELSE
		;腋毛に関しては、皆かなりまめに剃ります…保守な人以外は！
		;好色or男孩子？なら超高确率で腋つるつる
		IF TALENT:ARG:好色 || COND("男孩子？", ARG)
			SIF COND("阴毛", ARG) >= 4 && RAND:100
				BASE:ARG:腋毛 = 300
		ELSEIF TALENT:ARG:保守 && RAND:100
		ELSEIF COND("阴毛", ARG) == 4
			SIF RAND:10 == 0 || CHECK_CLO("腋露出", ARG)
				BASE:ARG:腋毛 = 300
		;放任生长状态は、ほぼ确实に避けます
		ELSEIF COND("阴毛", ARG) > 4 && RAND:100
			BASE:ARG:腋毛 = 300
		ENDIF
	ENDIF
CASE "欲求不满解消"
	BASE:ARG:欲求不满 = LIMIT(BASE:ARG:欲求不满 - ARG:1, 0, 30)
CASE "消耗品之自动购入"
	SIF CONFIG("自动购入") == 0
		RETURN 0
	CALL SALEITEM_CHECK, "表示なし"
	VARSET LOCAL
	SIF GETBIT(FLAG:9, 0) && ITEMSALES:录像带 && ITEM:录像带 == 0
		LOCAL:33 = 1
	FOR LCOUNT, 30, 50
		IF LOCAL:LCOUNT
			IF MONEY >= ITEMPRICE:LCOUNT
				MONEY -= ITEMPRICE:LCOUNT
				ITEM:LCOUNT += 1
				PRINTFORML 买入了%ITEMNAME:LCOUNT%。
			ELSE
				PRINTFORML 没能买入%ITEMNAME:LCOUNT%。
			ENDIF
			LOCAL:100 = 1
		ENDIF
	NEXT
	SIF LOCAL:100
		WAIT
CASE "妖精化"
	BASE:ARG:身长 = 45 + RAND:11 + RAND:11 + RAND:11
	TALENT:ARG:高大 = 0
	TALENT:ARG:娇小 = 1
	TALENT:ARG:妖精 = 1
	CALL CALC, "BＷＨ计測", ARG
CASE "娇小化"
	LOCAL = 125 + RAND:10 + RAND:6 + RAND:6
	SIF TALENT:ARG:幼稚
		LOCAL -= RAND:11
	SELECTCASE BASE:ARG:身长
	CASE IS > NUM("娇小之上限")
		BASE:ARG:身长 = MIN(LOCAL, BASE:ARG:身长 - 10)
	CASEELSE
		BASE:ARG:身长 = LOCAL
	ENDSELECT
	TALENT:ARG:高大 = 0
	TALENT:ARG:娇小 = 1
	TALENT:ARG:妖精 = 0
	CALL CALC, "BＷＨ计測", ARG
CASE "普通体型化"
	LOCAL = 145 + RAND:11 + RAND:5 + RAND:5
	SELECTCASE BASE:ARG:身长
	CASE IS <= NUM("娇小之上限")
		BASE:ARG:身长 = MAX(LOCAL, BASE:ARG:身长 + 10)
	CASE IS > NUM("普通之上限")
		BASE:ARG:身长 = MIN(LOCAL, BASE:ARG:身长 - 10)
	CASEELSE
		BASE:ARG:身长 = LOCAL
	ENDSELECT
	TALENT:ARG:高大 = 0
	TALENT:ARG:娇小 = 0
	CALL CALC, "BＷＨ计測", ARG
CASE "高大化"
	BASE:ARG:身长 = MAX(164 + RAND:9 + RAND:9, BASE:ARG:身长 + 10)
	TALENT:ARG:高大 = 1
	TALENT:ARG:娇小 = 0
	CALL CALC, "BＷＨ计測", ARG
CASE "BＷＨ计測"
	VARSET LOCAL
	;ゴールデンカノン (胸部 = 身长*0.54, 腰部 = 身长*0.38, 臀部 = 身长*0.54)を参考
	;ちなみに、ゴールデンカノン比は当然同时”腰部は略细で臀部は大きめ”である
	;LOCALが腰部补正、LOCAL:1が臀部补正
	LOCAL = 380
	LOCAL:1 = 540
	;少女以下は体之ラインがそれほど際立たない
	IF TALENT:ARG:外表年龄 == 0 || TALENT:ARG:娇小
		LOCAL = 390
		LOCAL:1 = 510
	ELSE
		;潜在的な纤瘦or丰肥值之加味
		SELECTCASE BASE:ARG:种族
		;纤瘦
		CASE 5, 8, 9, 11, 12, 18, 20, 21, 24, 30, 39, 40, 41, 46, 52, 57
			LOCAL = 360
			LOCAL:1 = 520
		;丰肥
		CASE 10, 14, 26, 34, 35, 48
			LOCAL:1 = 550
		CASEELSE
			LOCAL = 385
			LOCAL:1 = 535
		ENDSELECT

		SELECTCASE TALENT:ARG:外表年龄
		;成熟
		CASE IS >= 3
			LOCAL += 20
			LOCAL:1 += 30
		;大姐姐
		CASE 2
			LOCAL += 10
			LOCAL:1 += 15
		ENDSELECT
		SIF COND("梦魇", ARG) == 0
			LOCAL += RAND:6
	ENDIF
	;腰技による腰之くびれ＆大きなお尻
	LOCAL -= MIN(RAND:(1 + TALENT:ARG:腰技*10), 30)
	LOCAL:1 += MIN(RAND:(1 + TALENT:ARG:腰技*5), 30)
	;力量は肌肉がつく之で腰が略细で尻がちょっとだけ大きい
	IF TALENT:ARG:力量
		LOCAL -= 20
		LOCAL:1 += 5
	ENDIF
	;随机变化
	LOCAL:3 = RAND:26
	LOCAL:4 = LIMIT(RAND:31, LOCAL:3 - 10, LOCAL:3 + 10)
	LOCAL = MULTIPLY(LOCAL, 90 + LOCAL:3)
	LOCAL:1 = MULTIPLY(LOCAL:1, 90 + LOCAL:4)

	;誤差は95～120%+α(理想值よりもちょっと粗大が平均)
	BASE:ARG:腰部 = LIMIT(BASE:ARG:身长*LOCAL/1000, BASE:ARG:身长*34/100, BASE:ARG:身长*(46 + TALENT:ARG:外表年龄)/100)
	BASE:ARG:臀部 = LIMIT(BASE:ARG:身长*LOCAL:1/1000, BASE:ARG:腰部*130/100, BASE:ARG:腰部*(170 + TALENT:ARG:外表年龄*10)/100)
	;魅力量之场合は、腰部＆臀部を理想值にする
	IF TALENT:ARG:魅力
		BASE:ARG:腰部 = LIMIT(BASE:ARG:腰部, BASE:ARG:身长*35/100, BASE:ARG:身长*39/100)
		BASE:ARG:臀部 = LIMIT(BASE:ARG:臀部, BASE:ARG:身长*52/100, BASE:ARG:身长*60/100)
	;大姐姐や成熟は大意了……したりする之も良いよね…
	ELSEIF TALENT:ARG:外表年龄 >= 2
		SELECTCASE RAND:20 + (TALENT:ARG:外表年龄 == 2) * RAND:10
		CASE 0
			BASE:ARG:腰部 += BASE:ARG:身长*5/100
			BASE:ARG:臀部 += BASE:ARG:身长*15/200
		CASE 1, 2
			BASE:ARG:腰部 += BASE:ARG:身长*4/100
			BASE:ARG:臀部 += BASE:ARG:身长*6/100
		CASE 3, 4
			BASE:ARG:腰部 += BASE:ARG:身长*3/100
			BASE:ARG:臀部 += BASE:ARG:身长*9/200
		CASE 5, 6
			BASE:ARG:腰部 += BASE:ARG:身长*2/100
			BASE:ARG:臀部 += BASE:ARG:身长*3/100
		ENDSELECT
	ENDIF
	;罩杯数に合わせて胸部变更
	IF TALENT:ARG:性别 == 1
		BASE:ARG:胸部 = CALCF("顶级胸部和低级之差", BASE:ARG:罩杯) + MAX(BASE:ARG:身长*470/1000, BASE:ARG:腰部)
	ELSE
		BASE:ARG:胸部 = CALCF("顶级胸部和低级之差", BASE:ARG:罩杯) + MAX(BASE:ARG:身长*432/1000, BASE:ARG:腰部)
	ENDIF
	;腰部と臀部に应じて小臀か巨臀か判定
	CALL SETFLAG, "臀部設定", ARG
CASE "男乳化"
	;AAA（5cm)未满
	BASE:ARG:胸部 = CALCF("胸部サイズ补正", 1 + RAND:4, ARG) + MAX(BASE:ARG:身长*470/1000, BASE:ARG:腰部)
	BASE:ARG:罩杯 = CALCF("胸部罩杯", CALCF("胸部罩杯计算值", ARG) )
	TALENT:ARG:贫乳 = 0
	TALENT:ARG:巨乳 = 0
CASE "贫乳化"
	;AAA（5cm）、AA（7.5cm)、A（10cm）、B（12.5cm）
	;AAA（5cm）以上、B（13cm）未满
	BASE:ARG:胸部 = CALCF("胸部サイズ补正", 5 + RAND:8, ARG) + MAX(BASE:ARG:身长*432/1000, BASE:ARG:腰部)
	BASE:ARG:罩杯 = CALCF("胸部罩杯", CALCF("胸部罩杯计算值", ARG) )
	TALENT:ARG:贫乳 = 1
	TALENT:ARG:巨乳 = 0
CASE "普乳化"
	;B（12.5cm）、C（15cm）、D（17.5cm）、E（20cm）
	;B（13cm）以上、E（20cm）未满
	BASE:ARG:胸部 = CALCF("胸部サイズ补正", 13 + RAND:7, ARG) + MAX(BASE:ARG:身长*432/1000, BASE:ARG:腰部)
	BASE:ARG:罩杯 = CALCF("胸部罩杯", CALCF("胸部罩杯计算值", ARG) )
	TALENT:ARG:贫乳 = 0
	TALENT:ARG:巨乳 = 0
CASE "巨乳化"
	;E（20cm）、F（22.5cm）、G（25cm）、H（27.5cm)、I（30cm)
	;E（20cm）以上、G～I（25～30cm）之どれか未满
	BASE:ARG:胸部 = CALCF("胸部サイズ补正", 20 + RAND:(CONFIG("巨乳补正") + 5), ARG) + MAX(BASE:ARG:身长*432/1000, BASE:ARG:腰部)
	BASE:ARG:罩杯 = CALCF("胸部罩杯", CALCF("胸部罩杯计算值", ARG) )
	TALENT:ARG:贫乳 = 0
	TALENT:ARG:巨乳 = 1
CASE "爆乳化"
	;G（25cm）、H（27.5cm)、I（30cm)、J（32.5cm)、K（35cm)、L（37.5cm)、M（40cm)、N（42.5cm)
	;ここ之み高大之场合にはCONFIG("巨乳补正")…值は0or2or5 が２回かかる事となるため振れ幅がとても大きい
	;条件			最小		最大
	;巨乳はＦまで		25…G(25cm)	32…I(30cm)、つまりJ(32.5cm)未满
	;巨乳はＧまで		27…G(25cm)	34…J(32.5cm)、つまりK(35cm)未满
	;巨乳はＨまで		30…I(30cm)	37…K(35cm)、つまりL(37.5cm)未满
	;巨乳はＧまで＆高大	27…G(25cm)	36…K(35cm)、つまりL(37.5cm)未满
	;巨乳はＨまで＆高大	30…I(30cm)	42…M(40cm)、つまりN(42.5cm)未满

	LOCAL = 25 + CONFIG("巨乳补正") + RAND:8
	SIF TALENT:ARG:高大
		LOCAL += RAND:(CONFIG("巨乳补正") + 1)
	LOCAL = MAX(LOCAL, CONFIG("巨乳补正") + 25)
	BASE:ARG:胸部 = CALCF("胸部サイズ补正", LOCAL, ARG) + MAX(BASE:ARG:身长*432/1000, BASE:ARG:腰部)
	BASE:ARG:罩杯 = CALCF("胸部罩杯", CALCF("胸部罩杯计算值", ARG) )
	TALENT:ARG:贫乳 = 0
	TALENT:ARG:巨乳 = 2
CASE "超乳化"
	;J（32.5cm)、K（35cm)、L（37.5cm)、M（40cm)、N（42.5cm)、O（45cm)…… 
	;J～L（30～35cm）以上
	LOCAL = 33 + CONFIG("巨乳补正") + RAND:7
	SIF TALENT:ARG:高大
		LOCAL += CONFIG("巨乳补正")
	LOCAL = MAX(LOCAL, CONFIG("巨乳补正") + 33)
	BASE:ARG:胸部 = CALCF("胸部サイズ补正", LOCAL, ARG)  + MAX(BASE:ARG:身长*432/1000, BASE:ARG:腰部)
	BASE:ARG:罩杯 = CALCF("胸部罩杯", CALCF("胸部罩杯计算值", ARG) )
	TALENT:ARG:贫乳 = 0
	TALENT:ARG:巨乳 = 3
CASE "胸部アップ"
	;性别男や将来性无い人はダメ
	SIF CONFIG("孕娠补丁：娘之胸之成长あり") == 0 || TALENT:性别 == 1 || TALENT:将来性 == 0
		RETURN 0

	VARSET LOCAL
	LOCAL:2 = CALCF("胸部罩杯", CALCF("胸部罩杯计算值", TARGET) )
	;今回得た快B之珠之量で成果が決まる
	LOCAL = GOTJUEL:快B * TALENT:将来性 / 100
	SELECTCASE LOCAL
	CASE 0
		RETURN 0
	CASE 1 TO 9
		SIF RAND:(LOCAL + 10) >= 10
			LOCAL:1 = 1 + RAND:2
	CASE 10 TO 99
		SIF RAND:(LOCAL + 100) >= 100
			LOCAL:1 = LIMIT(RAND:(1 + LOCAL/20), 1, 3) + RAND:2
	CASEELSE
		SIF RAND:2
			LOCAL:1 = LIMIT(RAND:10, 1, 5) + RAND:2
	ENDSELECT
	SIF TALENT:妖精
		LOCAL:1 /= 2

	BASE:胸部之成长 += LOCAL:1
	BASE:罩杯 = CALCF("胸部罩杯", CALCF("胸部罩杯计算值", TARGET) )

	;大きくなってきた？
	IF BASE:罩杯 > LOCAL:2
		PRINTFORML 哦呀……？
		PRINTFORML 是因为一直在揉%CALLNAME:TARGET%的胸吗？
		PRINTFORMW 好像稍微变大了一些呢……
		;将来性判定
		IF TALENT:将来性 && TALENT:巨乳
			SIF RAND:5 || TALENT:巨乳 >= 5
				TALENT:将来性 = 0
		ELSEIF TALENT:将来性 == 1 && TALENT:贫乳 == 0
			SIF RAND:3
				TALENT:将来性 = 0
		ENDIF
	ENDIF
ENDSELECT

;-------------------------------------------------
;色々と计算する関数(#FUNCTION)
;ARGは变更しない事。また、CALCF内之别之も之を参照することは禁止
;-------------------------------------------------
@CALCF(ARGS, ARG, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5)
#FUNCTION
#DIM LCOUNT
#DIM DYNAMIC MEMO, 10
#DIMS DYNAMIC MEMOS, 10

VARSET LOCAL
SELECTCASE ARGS
CASE "何番目？"
	SIF ARG == ARG:1
		RETURNF 1
	SIF ARG == ARG:2
		RETURNF 2
	SIF ARG == ARG:3
		RETURNF 3
	SIF ARG == ARG:4
		RETURNF 4
	SIF ARG == ARG:5
		RETURNF 5
	RETURNF 0
;ARGがARG:1に注がれた量を返す。指定无ならARG=TARGET、ARG:1=PLAYER
CASE "膣内精液経験", "肛内精液経験", "饮精経験", "乳内精液経験", "膀胱精液経験"
	SIF ARG < 0
		RETURNF 0
	SIF ARG == 0 && TARGET
		ARG = TARGET
	SIF ARG:1 == 0 && PLAYER
		ARG:1 = PLAYER
	VARSET LOCALS

	SELECTCASE ARGS
	CASE "膣内精液経験"
		SPLIT CSTR:ARG:11, "/", LOCALS
	CASE "肛内精液経験"
		SPLIT CSTR:ARG:12, "/", LOCALS
	CASE "饮精経験"
		SPLIT CSTR:ARG:13, "/", LOCALS
	CASE "乳内精液経験"
		SPLIT CSTR:ARG:14, "/", LOCALS
	CASE "膀胱精液経験"
		SPLIT CSTR:ARG:15, "/", LOCALS
	ENDSELECT

	FOR LCOUNT, 0, 30
		SIF LOCALS:LCOUNT == ""
			BREAK
		;对象を探す
		SIF MEMOS != SUBSTRING(LOCALS:LCOUNT, 0, STRLENS(MEMOS) )
			CONTINUE

		;对象：量 となっている之で"量"部分を抜き出して、数に戻す
		MEMOS:1 = %SUBSTRING(LOCALS:LCOUNT, STRLENS(MEMOS), -1)%

		LOCAL = TOINT(MEMOS:1)

		RETURNF LOCAL
	NEXT
	RETURNF 0

;これは痛が无い最大径
CASE "尿道径"
	SIF ARG < 0
		RETURNF 0
	SIF ARG == 0 && TARGET
		ARG = TARGET

	;CSTR:3orCSTR:4之值之平均が基本的な尿道之太さ
	IF PENIS(ARG)
		LOCAL = SUM_STR(CSTR:ARG:4)
	ELSE
		LOCAL = SUM_STR(CSTR:ARG:3)
	ENDIF

	;四捨五入する
	LOCAL = (LOCAL + 5)/10

	RETURNF LOCAL
CASE "尿道扩张之壁"
	SIF ARG < 0
		RETURNF 0
	SIF ARG == 0 && TARGET
		ARG = TARGET

	;だいたいP尿道は15mm、V尿道は30mmぐらいが壁
	IF PENIS(ARG)
		LOCAL = (2) * MIN_STR(CONDS("P尿道之初期扩张度", ARG) ) + SIZE("基本阴茎サイズ", ARG)*3
	ELSE
		LOCAL = (2) * MIN_STR(CONDS("V尿道之初期扩张度", ARG) ) + BASE:ARG:身长/30
	ENDIF
	RETURNF LOCAL
CASE "尿道扩张之限界"
	SIF ARG < 0
		RETURNF 0
	SIF ARG == 0 && TARGET
		ARG = TARGET

	;だいたいP尿道は25mm、V尿道は40mmぐらいが壁
	IF PENIS(ARG)
		LOCAL = (3) * MIN_STR(CONDS("P尿道之初期扩张度", ARG) ) + SIZE("基本阴茎サイズ", ARG)*6
	ELSE
		LOCAL = (3) * MIN_STR(CONDS("V尿道之初期扩张度", ARG) ) + BASE:ARG:身长/15
	ENDIF
	RETURNF LOCAL
CASE "阴茎观察"
	SIF ARG < 0 || ARG:1 < 0
		RETURNF 0
	SIF ARG:1 == 0 && TARGET
		ARG:1 = TARGET

	VARSET LOCALS
	;すでにARG之阴茎を观察済みかを调べる
	SPLIT CSTR:(ARG:1):1, "/", LOCALS

	FOR LCOUNT, 0, 100
		SIF LOCALS:LCOUNT == ""
			BREAK
	NEXT
	RETURNF 0
CASE "百合・男同度"
	SELECTCASE ARG
	CASE 0 TO 29
		RETURNF 3
	CASE 30 TO 59
		RETURNF 2
	CASE 60 TO 69
		IF TALENT:性别 != 1 && TALENT:PLAYER:性别 != 0
			RETURNF 7
		ELSE
			RETURNF 4
		ENDIF
	CASE 70 TO 79
		IF TALENT:性别 == 1 && TALENT:PLAYER:性别 == 1
			RETURNF 10
		ELSE
			RETURNF 4
		ENDIF
	CASE 80 TO 89
		RETURNF 8
	CASE 90 TO 119
		RETURNF 7
	CASE 120 TO 129
		RETURNF 10
	;拳交
	CASE 141 TO 143
		RETURNF 10
	;SM
	CASE 130 TO 149
		RETURNF 2
	CASE 161, 504
		RETURNF 7
	ENDSELECT
CASE "V扩张之上限"
	SIF ARG < 0
		RETURNF 0
	SIF ARG == 0 && TARGET
		ARG = TARGET

	;处女や男
	SIF TALENT:ARG:性别 == 1 || TALENT:ARG:处女
		RETURNF 0

	;V扩张经验、V破壊経験、娇小でない、生产经历などで判定。
	;V活用LV之方も同时に用いることが多い。

	LOCAL = 1 + MIN(GET_EXP_LV(EXP:ARG:V扩张经验), 6)
	;娇小なら-1
	SIF TALENT:ARG:娇小
		LOCAL -= 1
	SIF EXP:ARG:V扩张经验 >= 100
		LOCAL += 1
	SIF EXP:ARG:V扩张经验 >= 200 && TALENT:ARG:高大
		LOCAL += 1
	;好色
	;SIF TALENT:ARG:好色 && LOCAL < 2
	;	LOCAL += 1
	;V经验が多くて、V扩张LVが２未满なら+1
	SIF EXP:ARG:V经验 >= 100 && LOCAL < 2
		LOCAL += 1
	SIF EXP:ARG:V经验 >= 300 && LOCAL < 2
		LOCAL += 1
	SIF EXP:ARG:V经验 >= 50 && LOCAL <= 0
		LOCAL += 1

CASE "A扩张之上限"
	SIF ARG < 0
		RETURNF 0
	SIF ARG == 0 && TARGET
		ARG = TARGET

	LOCAL = LIMIT(GET_EXP_LV(EXP:ARG:A扩张经验) - 1, 0, 6)

	;A经验が多いと、扩张経験无でもA扩张LVが２まで獲得可能
	SIF EXP:ARG:A经验 >= 50
		LOCAL += 1
	SIF EXP:ARG:A经验 >= 150 || EXP:ARG:A扩张经验 >= 5
		LOCAL += 1
	SIF EXP:ARG:A扩张经验 >= 100
		LOCAL += 1
	SIF EXP:ARG:A扩张经验 >= 200 && TALENT:ARG:高大
		LOCAL += 1
	;好色
	;SIF TALENT:ARG:好色 && LOCAL < 2
	;	LOCAL += 1
	;SIF EXP:ARG:A损伤経験 - TALENT:ARG:A损伤 > 0
	;	LOCAL += 1 + MIN(2, (EXP:ARG:A损伤経験 - TALENT:ARG:A损伤)/3)
	;娇小なら扩张が停滞する
	IF TALENT:ARG:娇小
		SIF LOCAL >= 3
			LOCAL -= 1
	ENDIF
	SIF (TALENT:ARG:A敏感) && LOCAL < 1
		LOCAL += 1

CASE "尿道刺激"
	;C感觉で代用
	SELECTCASE ABL:C感觉
	CASE 0
		LOCAL = 50
	CASE 1 TO 2
		LOCAL = 100*ABL:C感觉
	CASE 3 TO 5
		LOCAL = 150*ABL:C感觉
	CASE 6 TO 8
		LOCAL = 200*ABL:C感觉
	CASE 9
		LOCAL = 2000
	CASEELSE
		LOCAL = 2500
	ENDSELECT
	SIF CONFIG("EASY")
		LOCAL += 200
	SIF TALENT:C敏感
		LOCAL += 200
	SIF TALENT:尿道性感 == 0
		LOCAL /= 10
	SIF TALENT:尿道狂
		LOCAL *= 2

	SELECTCASE ARG
	;道具之使用（基本值）
	CASE 0
	;舌や指などで刺激(緩やかな上升)
	CASE 1
		LOCAL = MULTIPLY(LOCAL, 30 + ABL:C感觉*10)
	;性爱や按摩棒など(急激な上升)
	CASE 2
		LOCAL = MULTIPLY(LOCAL, 20 + ABL:C感觉*20)
	;拳交、龙奸など上级者向け(極端な上升)
	CASE 3
		LOCAL = MULTIPLY(LOCAL, MAX(ABL:C感觉*30 - 100, (ABL:C感觉 + 1)*5) )
	ENDSELECT
	SIF ARG:1
		LOCAL = LOCAL * ARG:1 / 100
CASE "C刺激"
	SELECTCASE ABL:C感觉
	CASE 0
		LOCAL = 100
	CASE 1 TO 2
		LOCAL = 300*ABL:C感觉
	CASE 3 TO 5
		LOCAL = 500*ABL:C感觉
	CASE 6 TO 8
		LOCAL = 600*ABL:C感觉
	CASE 9
		LOCAL = 6000
	CASEELSE
		LOCAL = 8000
	ENDSELECT
	SIF CONFIG("EASY")
		LOCAL += 500
	SIF TALENT:C敏感
		LOCAL += 500
	SELECTCASE ARG
	;道具之使用（基本值）
	CASE 0
	;舌や指などで刺激(緩やかな上升)
	CASE 1
		LOCAL = MULTIPLY(LOCAL, 30 + ABL:C感觉*5)
	;素股系(刺激は副産物。弱め)
	CASE 2
		LOCAL = MULTIPLY(LOCAL, 30)
	;逆性爱系(强)
	CASE 3
		LOCAL = MULTIPLY(LOCAL, 100)
	;逆肛交系(强)
	CASE 4
		LOCAL = MULTIPLY(LOCAL, 100)
	ENDSELECT
	SIF ARG:1
		LOCAL = LOCAL * ARG:1 / 100
CASE "V刺激"
	SELECTCASE ABL:V感觉
	CASE 0
		LOCAL = 100
	CASE 1 TO 2
		LOCAL = 300*ABL:V感觉
	CASE 3 TO 5
		LOCAL = 500*ABL:V感觉
	CASE 6 TO 8
		LOCAL = 600*ABL:V感觉
	CASE 9
		LOCAL = 6000
	CASEELSE
		LOCAL = 8000
	ENDSELECT
	SIF CONFIG("EASY")
		LOCAL += 500
	SIF TALENT:V敏感
		LOCAL += 500
	SELECTCASE ARG
	;道具之使用（基本值）
	CASE 0
	;舌や指などで刺激(緩やかな上升)
	CASE 1
		LOCAL = MULTIPLY(LOCAL, 30 + ABL:V感觉*10)
	;性爱や按摩棒など(急激な上升)
	CASE 2
		LOCAL = MULTIPLY(LOCAL, 20 + ABL:V感觉*20)
	;拳交、龙奸など上级者向け(極端な上升)
	CASE 3
		LOCAL = MULTIPLY(LOCAL, MAX(ABL:V感觉*30 - 100, (ABL:V感觉 + 1)*5) )
	ENDSELECT
	SIF ARG:1
		LOCAL = LOCAL * ARG:1 / 100
CASE "刺激肛门"
	SELECTCASE ABL:A感觉
	CASE 0
		LOCAL = 100
	CASE 1 TO 2
		LOCAL = 300*ABL:A感觉
	CASE 3 TO 5
		LOCAL = 500*ABL:A感觉
	CASE 6 TO 8
		LOCAL = 600*ABL:A感觉
	CASE 9
		LOCAL = 6000
	CASEELSE
		LOCAL = 8000
	ENDSELECT
	SIF CONFIG("EASY")
		LOCAL += 500
	SIF TALENT:A敏感
		LOCAL += 500
	SELECTCASE ARG
	;道具之使用（基本值）
	CASE 0
	;舌や指などで刺激(緩やかな上升)
	CASE 1
		LOCAL = MULTIPLY(LOCAL, 30 + ABL:A感觉*10)
	;性爱や按摩棒など(急激な上升)
	CASE 2
		LOCAL = MULTIPLY(LOCAL, 20 + ABL:A感觉*20)
	;拳交、龙奸など上级者向け(極端な上升)
	CASE 3
		LOCAL = MULTIPLY(LOCAL, MAX(ABL:A感觉*30 - 100, (ABL:A感觉 + 1)*5) )
	ENDSELECT
	SIF ARG:1
		LOCAL = LOCAL * ARG:1 / 100
CASE "B刺激"
	SELECTCASE ABL:B感觉
	CASE 0
		LOCAL = 100
	CASE 1 TO 2
		LOCAL = 300*ABL:B感觉
	CASE 3 TO 5
		LOCAL = 500*ABL:B感觉
	CASE 6 TO 8
		LOCAL = 600*ABL:B感觉
	CASE 9
		LOCAL = 6000
	CASEELSE
		LOCAL = 8000
	ENDSELECT
	SIF CONFIG("EASY")
		LOCAL += 500
	SIF TALENT:B敏感
		LOCAL += 500
	SELECTCASE ARG
	;道具之使用（基本值）
	CASE 0
	;舌や指などで刺激(緩やかな上升)
	CASE 1
		LOCAL = MULTIPLY(LOCAL, 30 + ABL:B感觉*10)
	;乳交など(刺激は副産物。弱め)
	CASE 2
		LOCAL = MULTIPLY(LOCAL, 30)
	;性爱や按摩棒など(急激な上升)
	CASE 3
		LOCAL = MULTIPLY(LOCAL, 20 + ABL:B感觉*20)
	ENDSELECT
	SIF ARG:1
		LOCAL = LOCAL * ARG:1 / 100
CASE "M刺激"
	SELECTCASE ABL:M感觉
	CASE 0
		LOCAL = 100
	CASE 1 TO 2
		LOCAL = 300*ABL:M感觉
	CASE 3 TO 5
		LOCAL = 500*ABL:M感觉
	CASE 6 TO 8
		LOCAL = 600*ABL:M感觉
	CASE 9
		LOCAL = 6000
	CASEELSE
		LOCAL = 8000
	ENDSELECT
	SIF CONFIG("EASY")
		LOCAL += 500
	SIF TALENT:M敏感
		LOCAL += 500
	SELECTCASE ARG
	;道具之使用（基本值）
	CASE 0
	;接吻(緩やかな上升)
	CASE 1
		LOCAL = MULTIPLY(LOCAL, 30 + ABL:M感觉*10)
	;口交など(刺激は副産物。弱め)
	CASE 2
		LOCAL = MULTIPLY(LOCAL, 30)
	ENDSELECT
	SIF ARG:1
		LOCAL = LOCAL * ARG:1 / 100
CASE "快感上升之上限措置"
	;ARG=补正前之快感、ARG:1=对象部位之感觉,ARG:2=敏感-钝感
	SIF ARG <= 0
		RETURNF 0

	;欲情とＨ氛围が两方高くないと得られる快感之上限が下がる
	;欲情によるペナルティ
	SELECTCASE PALAM:欲情
	CASE IS < 100
		ARG:1 = 0
	CASE IS < 3000
		ARG:1 = MIN(1, ARG:1)
	CASE IS < 10000
		ARG:1 = MIN(2, ARG:1)
	CASE IS < 30000
		ARG:1 = MIN(3, ARG:1)
	ENDSELECT

	;敏感,钝感,特化素質补正
	ARG:1 = MAX(ARG:1 + ARG:2, 0)

	;媚药BＯＮＵS
	IF TEQUIP:媚药
		ARG:1 += MAX(TEQUIP:媚药/100, 1)
		ARG:2 += TEQUIP:媚药/300
	ENDIF

	;弱点突いている场合には绝顶补正BＯＮＵS
	IF ARG:1 <= 0 && ARG:2 > 0
		ARG:1 = 0
		SIF EX:绝顶数合计
			ARG:1 = 1
	ENDIF

	;Ｈ氛围による下方补正
	SELECTCASE MOOD_HEART()
	;ハート无
	CASE 0
		ARG:1 = MAX(ARG:1 - 3, 0)
	;ハート一个
	CASE 1
		ARG:1 = MAX(ARG:1 - 2, 0)
	;ハート２个
	CASE 2
		ARG:1 = MAX(ARG:1 - 1, 0)
	ENDSELECT

	;LOCAL:1は得られる快感に补正がかかり始める值（大体PALAM_LVに沿っている）
	;LOCAL:2が上限之ような役割となっている
	;PALAM_LV之值は、顺に1:100,2:500,3:3000,4:1万,5:3万,6:6万,7:10万,8:15万,9:25万
	SELECTCASE ARG:1
	CASE 0
		LOCAL:1 = 100
		LOCAL:2 = 500
	CASE 1
		LOCAL:1 = 500
		LOCAL:2 = 3000
	CASE 2
		LOCAL:1 = 3000
		LOCAL:2 = 6000
	CASE 3
		LOCAL:1 = 6000
		LOCAL:2 = 10000
	CASE 4
		LOCAL:1 = 10000
		LOCAL:2 = 20000
	CASEELSE
		LOCAL:1 = 20000*(ARG:1 - 4)
		LOCAL:2 = 20000*(ARG:1 - 3)
	ENDSELECT

	SELECTCASE ARG
	CASE IS <= LOCAL:1
		LOCAL = ARG
	CASEELSE
		;LOCAL:1を越えた分之快感については补正で减らされる
		LOCAL = LOCAL:1 + (ARG - LOCAL:1)/5
		SIF LOCAL > LOCAL:2
			LOCAL = LOCAL:2 + (LOCAL - LOCAL:2)/20
	ENDSELECT
	;PRINTFORML ARG = {ARG}, ARG:1 = {ARG:1}, LOCAL = {LOCAL}, LOCAL:1 = {LOCAL:1}, LOCAL:2 = {LOCAL:2}
	RETURNF LOCAL

CASE "快乐补正"
	;PALAM:欲情
	ARG = MULTIPLY(ARG, LIMIT(80 + GET_PALAMLV(PALAM:欲情)*10, 100, 150) )

	;ABL:侍奉精神とEXP:奉仕快乐経験
	LOCAL:1 = MIN(ABL:侍奉精神*10 + EXP:奉仕快乐経験/20 + TALENT:献身*30, 100)
	SELECTCASE SELECTCOM
	CASE 6, 140
		ARG = MULTIPLY(ARG, 100 + LOCAL:1)
	CASE 65, 75, 90 TO 94, 96 TO 119
		SIF TEQUIP:逆推 == 0
			ARG = MULTIPLY(ARG, 100 + LOCAL:1/2)
	ENDSELECT

	;ABL:S属性とEXP:嗜虐快乐経験をみる
	LOCAL:1 = MIN(ABL:S属性*10 + EXP:嗜虐快乐経験/20 + TALENT:抖S*30, 100)
	SELECTCASE SELECTCOM
	CASE 95, 105
		ARG = MULTIPLY(ARG, 100 + LOCAL:1)
	CASE 1, 9, 96, 120, 126, 304, 305
		SIF TEQUIP:逆推
			ARG = MULTIPLY(ARG, 100 + LOCAL:1/2)
	CASEELSE
		SIF TEQUIP:顔面骑乘
			ARG = MULTIPLY(ARG, 100 + LOCAL:1/2)
	ENDSELECT

	;ここまでで得た补正が２倍以上之场合には、２之n乗 < 补正倍率 < ２之(n+1)乗 →　(n+1)倍 < 补正倍率 < (n+2)倍　となるように。
	FOR LOCAL:1, 0, 10
		LOCAL:2 = 10 - LOCAL:1
		LOCAL:3 = POWER(2, LOCAL:2)
		IF ARG > LOCAL:3 * 1000
			ARG = (LOCAL:2 + 1)*1000 + (ARG - LOCAL:3*1000)/LOCAL:3
			BREAK
		ENDIF
	NEXT

	RETURNF ARG
CASE "基礎补正"
	LOCAL = 0
	;技巧之差などによる补正
	IF ABL:技巧 + (TEQUIP:逆推 > 0) > ABL:PLAYER:技巧
		LOCAL += (ABL:技巧 + (TEQUIP:逆推 > 0) - ABL:PLAYER:技巧)*50
		SIF ABL:技巧 > ABL:PLAYER:技巧
			LOCAL += ABL:技巧*10
	ELSE
		LOCAL += (ABL:技巧 + (TEQUIP:逆推 > 0) - ABL:PLAYER:技巧)*20
	ENDIF

	SIF LOCAL > 0 && CONFIG("EASY")
		LOCAL /= 3

	;媚药
	LOCAL += TEQUIP:PLAYER:媚药
	;最恶でも3割は与える
	LOCAL = MAX(LOCAL, -70)
	ARG = MULTIPLY(ARG, 100 + LOCAL)

	;偏爱や相思相爱、性癖など
	;魅力
	SIF TALENT:魅力
		TIMES ARG, 1.10
	;褒められてるとがんばる
	SIF TEQUIP:称賛
		TIMES ARG, 1.20
	;虚弱术
	SIF STATE("虚弱术", PLAYER)
		TIMES ARG, 1.50
	;前之回合に高潮管理されている（こ之回合で高潮管理解除之予定）
	SIF TEQUIP:PLAYER:高潮管理 == 1
		TIMES ARG, 1.50
	;溜まっている
	SIF BASE:PLAYER:精力 >= MAXBASE:PLAYER:精力 && PENIS(PLAYER)
		TIMES ARG, 2.00
	;-------------------------------------------------
	;ここから下はマイナス补正
	;-------------------------------------------------
	;目隐しされてると、技巧减少
	SIF TEQUIP:眼罩
		TIMES ARG, 0.80
	;利尿剂を使われてると、技巧减少
	SIF TEQUIP:利尿剂
		TIMES ARG, 0.80
	;拘束されてると、技巧减少
	SIF TEQUIP:拘束
		TIMES ARG, 0.80
	;精力０
	SIF BASE:精力 <= 0
		TIMES ARG, 0.50

	SELECTCASE STATE("防强术", PLAYER)
	CASE 1
		TIMES ARG, 0.80
	CASE 2
		TIMES ARG, 0.65
	CASE IS >= 3
		TIMES ARG, 0.50
	ENDSELECT

	SELECTCASE CONDS("机嫌")
	CASE "观望"
		TIMES ARG, 0.50
	CASE "没干劲"
		TIMES ARG, 0.25
	ENDSELECT

	;装饰品效果（精神力）
	ARG = DIVIDE(ARG, 100 + TALENT:PLAYER:精神力强化*2)
	;装饰品效果（得到的快感UP）
	ARG = MULTIPLY(ARG, 100 + TALENT:PLAYER:得到的快感)
	RETURNF ARG
CASE "实行值による欲情追加"
	;TCVAR:29之值によって奉仕or嗜虐快乐が決められている
	;0bit弱奉仕, 1bit奉仕, 2bit弱嗜虐, 3bit嗜虐

	;奉仕中心
	IF GETBIT(TCVAR:29, 1)
		LOCALS = 侍奉精神
	;嗜虐中心
	ELSEIF GETBIT(TCVAR:29, 3)
		LOCALS = S属性
	ELSEIF GETBIT(TCVAR:29, 0) && GETBIT(TCVAR:29, 2)
		LOCALS = 侍奉精神
		SIF ABL:S属性 > ABL:侍奉精神
			LOCALS = S属性
	ELSEIF GETBIT(TCVAR:29, 0)
		LOCALS = 侍奉精神
	ELSEIF GETBIT(TCVAR:29, 2)
		LOCALS = S属性
	ELSE
		RETURNF 0
	ENDIF
	SELECTCASE ABL:LOCALS
	CASE 0
		LOCAL = 50
	CASE 1
		LOCAL = 150
	CASE 2
		LOCAL = 250
	CASE 3
		LOCAL = 450
	CASE 4
		LOCAL = 600
	CASEELSE
		LOCAL = 750
	ENDSELECT
	;弱奉仕or弱嗜虐なら25％
	SIF GETBIT(TCVAR:29, 1) == 0 && GETBIT(TCVAR:29, 3) == 0
		TIMES LOCAL, 0.25

	;TARGETとPLAYER之性别により参照するABLが变わる。PLAYERに阴茎ありなら精液成瘾。
	IF PENIS(PLAYER)
		LOCALS:1 = 精液成瘾
	;女性に男が奉仕なら抖Mッ气
	ELSEIF TALENT:性别 == 1
		LOCALS:1 = M属性
	;女同士なら百合ッ气
	ELSE
	ENDIF
	SELECTCASE ABL:(LOCALS:1)
	CASE 0
		TIMES LOCAL, 0.20
	CASE 1
	CASE 2
		TIMES LOCAL, 1.50
	CASE 3
		TIMES LOCAL, 2.00
	CASE 4
		TIMES LOCAL, 3.00
	CASEELSE
		TIMES LOCAL, 4.00
	ENDSELECT
	;实行值之量で效果増
	LOCAL = MULTIPLY(LOCAL, 100 + TCVAR:28)
CASE "阴茎喜欢度";剔除成性交喜好度
	;恋慕
	SIF PLAYER == MASTER && TALENT:ARG:恋慕
		LOCAL += 1
	;接受快感or好色
	SIF TALENT:ARG:接受快感 || TALENT:ARG:好色
		LOCAL += 1
	SIF TALENT:ARG:淫穴 || TALENT:ARG:尻穴狂
		LOCAL += 1
CASE "C发情"
	SIF ARG == 0 && TARGET
		ARG = TARGET
	;0通常 1皮之下で勃起 2顔を出す 3ギンギン
	SIF TALENT:ARG:淫核
		LOCAL += 1
	;欲情(媚药)
	SIF PALAM:ARG:欲情 >= 10000 || TEQUIP:ARG:媚药
		LOCAL += 1
	SIF (ABL:ARG:C感觉 >= 3 && PALAM:ARG:欲情 >= 3000) || ABL:ARG:C感觉 >= 5
		LOCAL += 1
CASE "V发情"
	SIF ARG == 0 && TARGET
		ARG = TARGET
	;0通常 1发情 2ビショビショ 3爱液駄々漏れ
	SIF TALENT:ARG:淫穴
		LOCAL += 1
	;欲情(媚药)
	SIF PALAM:ARG:欲情 >= 10000 || TEQUIP:ARG:媚药
		LOCAL += 1
	SIF ABL:ARG:V感觉 >= 3 && PALAM:ARG:欲情 >= 3000
		LOCAL += 1
	;绝顶
	SIF EX:ARG:V绝顶 || EX:ARG:绝顶数合计 >= 3
		LOCAL += 1
	SIF EX:ARG:V绝顶 >= 3 || EX:ARG:绝顶数合计 >= 10
		LOCAL += 1
CASE "B发情"
	SIF ARG == 0 && TARGET
		ARG = TARGET
	;0通常 1しこりはじめ 2勃起 3ギンギン
	SIF TALENT:ARG:淫乳
		LOCAL += 1
	;欲情(媚药)
	SIF PALAM:ARG:欲情 >= 10000 || TEQUIP:ARG:媚药
		LOCAL += 1
	SIF ABL:ARG:B感觉 >= 3 && PALAM:ARG:欲情 >= 3000
		LOCAL += 1
CASE "绝顶"
	;射精条件を满たしているか？
	SELECTCASE BASE:ARG:绝顶
	CASE IS < MAXBASE:ARG:绝顶
		RETURNF 0
	CASE IS < MAXBASE:ARG:绝顶*2
		SIF COND("绝顶许可", PLAYER) == 0 && TEQUIP:ARG:逆调教 == 0
			RETURNF 0
	ENDSELECT

	;念之ため
	SIF EJAC:ARG <= 0
		EJAC:ARG = 10000


		SELECTCASE BASE:ARG:绝顶
		CASE IS >= EJAC:ARG*2
			LOCAL = MIN(2 + (STATE("精强术", ARG) + 1)/2, 5)
		CASE IS >= EJAC:ARG
			LOCAL = 1
		CASEELSE
			RETURNF 0
		ENDSELECT

	;现状はMAX5(ためしに限定解除)
	LOCAL = MIN(LOCAL, BASE:ARG:精力/2 + 1)
CASE "绝顶之原因"
	;ARGは角色、ARG:1はSELECTCOM
	SIF ARG < 0
		RETURNF 0
	;PLAYERに阴茎がない时之绝顶标志（暫定）
	IF PENIS(PLAYER) == 0
		LOCAL = 30

		;调教者V插入
		IF INSERT("V插入", PLAYER, ARG)
			LOCAL = 32
		;调教者A插入
		ELSEIF INSERT("A插入", PLAYER, ARG)
			LOCAL = 35
		;自慰
		ELSEIF TEQUIP:PLAYER:自慰
			LOCAL = 39
		ELSE
			SELECTCASE ARG:1
			;接吻、舐阴、舐肛など、口へ之刺激
			CASE 1, 4, 6, 9, 104
				LOCAL = 31
			;舐阴强制や逆强暴など、Vへ之刺激
			CASE 80, 98, 120 TO 124
				LOCAL = 32
			;足交系
			CASE 95
				LOCAL = 33
			;洗浴服务や身体系
			CASE 94
				LOCAL = 34
			;逆肛交
			CASE 96, 125 TO 129
				LOCAL = 35
			;自慰
			CASE 282
				LOCAL = 39
			ENDSELECT
		ENDIF
	ELSEIF PENIS(PLAYER)
		;不慮之射精之场合は素股(身体)で射精となるようにする
		LOCAL = 7

		;V插入
		IF INSERT("V插入", ARG, PLAYER)
			LOCAL = 3
		;A插入
		ELSEIF INSERT("A插入", ARG, PLAYER)
			LOCAL = 4
		;尿道插入
		ELSEIF INSERT("尿道插入", ARG, PLAYER)
			LOCAL = 19
		;调教者V插入
		ELSEIF INSERT("V插入", PLAYER, ARG)
			LOCAL = 10
		;调教者A插入
		ELSEIF INSERT("A插入", PLAYER, ARG)
			LOCAL = 11
		;自慰
		ELSEIF TEQUIP:PLAYER:自慰
			LOCAL = 20
		;六九式
		ELSEIF TEQUIP:ARG:六九式
			SELECTCASE TEQUIP:ARG:六九式
			CASE 90, 97, 105
				LOCAL = 2
			CASE 91, 150
				LOCAL = 1
			CASE 92
				LOCAL = 5
			CASE 98
				LOCAL = 9
			CASEELSE
				LOCAL = 8
			ENDSELECT
		ELSE
			SELECTCASE ARG:1
			CASE 3
				SIF CHECK_COM("口交自慰")
					LOCAL = 1
			;口交系
			CASE 91, 99, 140, 150
				LOCAL = 1
			;手交系
			CASE 90, 97, 105
				LOCAL = 2
				SIF CHECK_COM("刺激肛门手交", ARG:1)
					LOCAL = 15
			;性爱
			CASE 60 TO 69, 177
				LOCAL = 3
			;A性爱
			CASE 70 TO 79
				LOCAL = 4
			;胸系
			CASE 81, 92
				LOCAL = 5
				SIF CHECK_COM("乳夹口交", ARG:1)
					LOCAL = 1
			;乳头奸
			CASE 82
				LOCAL = 18
			;尿道插入
			CASE 83
				LOCAL = 19
			;足交系
			CASE 95
				LOCAL = 6
			;素股や身体系
			CASE 16, 93, 94, 100
				LOCAL = 7
			;接吻、舐阴、舐肛など、口へ之刺激
			CASE 1, 9
				LOCAL = 16
				SIF TEQUIP:ARG:逆推
					LOCAL = 14
			CASE 4, 6
				LOCAL = 8
			;逆舐阴
			CASE 98
				LOCAL = 9
			;调教者V插入
			CASE 120 TO 124
				LOCAL = 10
			;调教者A插入
			CASE 125 TO 129
				LOCAL = 11
			;逆龙奸
			CASE 304
				SIF TEQUIP:ARG:逆推
					LOCAL = 12
			;逆肛龙奸
			CASE 305
				SIF TEQUIP:ARG:逆推
					LOCAL = 13
			;舐肛奉侍
			CASE 96
				LOCAL = 15
			;两乳夹头
			CASE 104
				LOCAL = 17
				SIF CHECK_COM("两乳夹头手交", ARG:1)
					LOCAL = 2
			;自慰
			CASE 282
				LOCAL = 20
			;助手性交
			CASE 160
				LOCAL = 50
			;助手口交
			CASE 162
				LOCAL = 52
			ENDSELECT
		ENDIF
	ENDIF

CASE "自由な手"
	;手を拘束
	SIF GETBIT(TEQUIP:拘束, 20)
		RETURNF 0
	;自慰に使える手之数。多手なら、そ之分追加。
	LOCAL = 2 + TALENT:多手
	;ARGでSELECTCOMを管理。ARGに入力が无いなら、SELECTCOMが代入される。
	SIF ARG == 0
		ARG = SELECTCOM
	SELECTCASE ARG
	CASE 3
		RETURNF LOCAL
	CASE 13, 14, 90, 92 TO 94, 97 TO 99, 104 TO 107
		LOCAL -= 2
	CASE 91, 96
		IF CHECK_COM("乳夹口交") || CHECK_COM("刺激肛门手交")
			LOCAL -= 2
		ELSE
			LOCAL -= 1
		ENDIF
	ENDSELECT
	;插入中
	IF INSERT("插入")
		IF INSERT("背面座位")
		ELSEIF INSERT("后背位")
			LOCAL -= 2
		ELSEIF INSERT("对面座位") || INSERT("狂野摇摆")
			LOCAL = 0
		ELSE
			LOCAL -= 1
		ENDIF
	ENDIF
CASE "精液好き度"
	LOCAL = (TCVAR:28 + 30) / 20
CASE "漏尿"
	;だいたい、值が5以上だと漏尿してしまう
	;射精したor绝顶して无いor尿意が无いor性别男orV扩张扩张气囊
	SIF NOWEX:ARG:射精 || NOWEX:ARG:绝顶数合计 == 0 || BASE:ARG:尿意 < 1000 || TALENT:ARG:性别 == 1 || TEQUIP:ARG:扩张扩张气囊 == 1 || COND("尿道使用中", ARG)
		RETURNF 0
	; SIF GETBIT(TEQUIP:ARG:肌肉松弛剂, 1) == 0 && ABL:ARG:尿道扩张 < 0 && TALENT:ARG:漏尿癖 == 0 && BASE:ARG:尿意 < 10000
	; 	RETURNF 0
	LOCAL = BASE:ARG:尿意 / 5000

	;Gスポ刺激
	SIF CHECK_COM("进攻Ｇ点") || SELECTCOM == 66
		LOCAL += 1
	SIF BASE:ARG:气力 <= 0
		LOCAL += 1
	SIF MAX(EX:ARG:C绝顶, EX:ARG:V绝顶, EX:ARG:A绝顶, EX:ARG:尿道绝顶) >= 20
		LOCAL += 1
	IF NOWEX:ARG:绝顶数合计 >= 5
		LOCAL += 2
	ELSEIF NOWEX:ARG:绝顶数合计 >= 3
		LOCAL += 1
	ENDIF
	SIF GETBIT(TEQUIP:ARG:肌肉松弛剂, 1)
		LOCAL += 2
	;年龄層补正
	; SIF TALENT:ARG:外表年龄 >= 2 && LOCAL >= 1
	; 	LOCAL -= 1
	SIF TALENT:ARG:漏尿癖
		LOCAL += 3

CASE "随机射精"
	IF RAND:2 == 0
		LOCAL = 0
	ELSEIF RAND:3
		;口→顔
		IF SHOOT("口")
			LOCAL = 2
		;手か胸→口
		ELSEIF SHOOT("手") || SHOOT("胸")
			LOCAL = 3
		;插入系→お腹かお尻
		ELSEIF SHOOT("膣内") || SHOOT("A")
			IF INSERT("后背位") || INSERT("背面座位")
				LOCAL = 7
			ELSE
				LOCAL = 6
			ENDIF
		;→股间
		ELSE
			LOCAL = 5
		ENDIF
	ELSE
		;口→发顔胸股间
		IF SHOOT("口")
			LOCAL = 1 + RAND:2 + 3*RAND:2
		;手or插入系→发顔口胸股间
		ELSEIF SHOOT("手") || SHOOT("膣内") || SHOOT("A")
			LOCAL = 1 + RAND:5
		;胸→发顔口股间
		ELSEIF SHOOT("胸")
			LOCAL = LIMIT(1 + RAND:3 + 5*(RAND:4 == 0), 1, 5)
		;其他
		ELSE
			LOCAL = LIMIT(1 + RAND:2 + 5*(RAND:3 == 0), 1, 5)
		ENDIF
	ENDIF
CASE "称賛"
	;基本は好感觉+前回绝顶と绝顶回数合计
	LOCAL = MIN(CFLAG:2/50, 50)
	;前回绝顶した
	SIF PREVEX("绝顶数合计")
		LOCAL += 20
	;好色
	LOCAL += 30 * TALENT:好色
	;怕羞
	LOCAL += 5 * TALENT:怕羞
	;胆怯
	LOCAL += 5 * TALENT:胆怯
	;献身的
	LOCAL += 15 * TALENT:献身
	;幼稚
	LOCAL += 20 * TALENT:幼稚
	;坦率
	LOCAL += 5 * TALENT:坦率
	;好奇心
	LOCAL += 5 * TALENT:好奇心
	;乐观的
	LOCAL += 30 * TALENT:开朗
	;喜欢受人注目
	LOCAL += 10 * TALENT:喜欢受人注目
	;解放
	LOCAL += 5 * TALENT:解放
	;愚笨
	LOCAL += 20 * TALENT:愚笨
	;淫乱
	LOCAL += 5 * TALENT:淫乱
	;恋慕
	LOCAL += 10 * TALENT:恋慕
	;妄信
	LOCAL += 10 * TALENT:妄信
	;誓約
	; SIF TALENT:誓約
	; 	LOCAL += 2
	;相思相爱
	; SIF TALENT:相思相爱
	; 	LOCAL += 10
	;ABL顺从
	LOCAL += ABL:顺从 * 8
	SELECTCASE TFLAG:16
	CASE 1
		LOCAL += (ABL:侍奉精神 + ABL:精液成瘾) * 5
	CASE 2
		LOCAL += ABL:V感觉 * 5
		;处女丧失时に屈服刻印Lv1未满
		SIF PREVEX("处女丧失") && MARK:屈服刻印 <= 1
			LOCAL -= 25
	CASE 3
		LOCAL += ABL:A感觉 * 5
	CASE 4
		LOCAL += ABL:M属性 * 10
		;SM系实行时で、M属性3未满である
		SIF ABL:M属性 < 3
			LOCAL -= 45
	CASE 5, 6
		LOCAL += ABL:露阴癖 * 10
		;怕羞+露阴癖5未满＆羞恥系实行
		; SIF TALENT:怕羞 && ABL:露阴癖 < 5
		; 	LOCAL -= 35
		;羞恥系实行时で、露阴癖5未满である
		; SIF TFLAG:16 == 5 && ABL:露阴癖 < 5
		; 	LOCAL -= 15
		; SIF TFLAG:16 == 6 && ABL:露阴癖 < 5
		; 	LOCAL -= 50
	ENDSELECT
	;屈服刻印
	LOCAL += MARK:屈服刻印 * 10
	;ABL技巧
	LOCAL += ABL:PLAYER:技巧 * 5
	;なぜか詩集之持ってる数も影響
	LOCAL += ITEM:罗兹尼尔詩集

	;成功率Down补正
	;反抗刻印
	LOCAL -= MARK:反抗刻印 * 10
	;按摩棒插入中
	SIF TEQUIP:按摩棒
		LOCAL -= 2
	;肛用按摩棒插入中
	SIF TEQUIP:肛用按摩棒
		LOCAL -= 3
	;阴蒂夹、乳头夹、搾乳机、飞机杯之いずれか使用中
	SIF TEQUIP:阴蒂夹 || TEQUIP:乳头夹 || TEQUIP:搾乳机 || TEQUIP:飞机杯
		LOCAL -= 2
	;利尿剂使用中
	SIF TEQUIP:利尿剂
		LOCAL -= 5
	;绳使用中
	SIF TEQUIP:拘束
		LOCAL -= 5
	;球形口枷使用中
	SIF TEQUIP:口枷
		LOCAL -= 3
	;灌肠、空气灌肠、扩张扩张气囊之いずれか使用中
	SIF TEQUIP:灌肠 || TEQUIP:扩张扩张气囊
		LOCAL -= 10
CASE "甜言蜜语"
	LOCAL = MAX(CFLAG:269*20 + 500, 1800)
	;胆怯
	SIF TALENT:胆怯
		TIMES LOCAL, 1.25
	;反抗心
	; SIF TALENT:反抗心
	; 	TIMES LOCAL, 1.50
	;刚强
	SIF TALENT:刚强
		TIMES LOCAL, 2.00
	;自大
	; SIF TALENT:自大
	; 	TIMES LOCAL, 1.50
	;自制心
	SIF TALENT:冷静
		TIMES LOCAL, 1.25
	;不关心
	; SIF TALENT:不关心
	; 	TIMES LOCAL, 1.50
	;缺乏情感
	; SIF TALENT:缺乏情感
	; 	TIMES LOCAL, 1.50
	;保守
	SIF TALENT:保守
		TIMES LOCAL, 1.25
	;悲观的
	; SIF TALENT:阴郁
	; 	TIMES LOCAL, 1.25
	;难以逾越的界线
	; SIF TALENT:难以逾越的界线
	; 	TIMES LOCAL, 1.50
	;抵抗
	SIF TALENT:抵抗
		TIMES LOCAL, 2.00
	;怕羞
	SIF TALENT:怕羞
		TIMES LOCAL, 1.25
	;狂气
	SIF TALENT:狂气
		TIMES LOCAL, 2.00
; CASE "弱点突かれ"
; 	SELECTCASE ARG
; 	CASE 90, 97
; 		SIF GETBIT(TALENT:PLAYER:弱点, 0)
; 			LOCAL += 5
; 	CASE 91, 99, 140, 150
; 		SIF GETBIT(TALENT:PLAYER:弱点, 1)
; 			LOCAL += 5
; 	CASE 5, 81, 92, 94, 104
; 		SIF GETBIT(TALENT:PLAYER:弱点, 2)
; 			LOCAL += 5
; 	CASE 95
; 		SIF GETBIT(TALENT:PLAYER:弱点, 3)
; 			LOCAL += 5
; 	CASE 1, 60 TO 69
; 		SIF GETBIT(TALENT:PLAYER:弱点, 4)
; 			LOCAL += 5
; 		SIF GETBIT(TALENT:PLAYER:弱点, 4) && TEQUIP:逆推 && SELECTCOM == 1
; 			LOCAL += 5
; 		SIF GETBIT(TALENT:PLAYER:弱点, 5) && ARG == 63
; 			LOCAL += 5
; 	CASE 6
; 		SIF GETBIT(TALENT:PLAYER:弱点, 5)
; 			LOCAL += 5
; 	CASE 2, 9, 70 TO 79
; 		SIF GETBIT(TALENT:PLAYER:弱点, 6)
; 			LOCAL += 5
; 		SIF GETBIT(TALENT:PLAYER:弱点, 5) && ARG == 73
; 			LOCAL += 5
; 	ENDSELECT
; 	FOR LCOUNT, 0, NUM("属性")
; 		SIF GETBIT(BASE:属性, LCOUNT) && GETBIT(TALENT:MASTER:性癖, LCOUNT)
; 			LOCAL += 3
; 	NEXT
CASE "苦痛刻印獲得"
	SIF TEQUIP:ARG:逆推 || SELECTCOM == 280
		RETURNF 0
	SELECTCASE CUP:ARG:苦痛
	CASE IS >= 30000
		SIF MARK:ARG:苦痛刻印 < 3
			RETURNF 3
	CASE IS >= 10000
		SIF MARK:ARG:苦痛刻印 < 2
			RETURNF 2
	CASE IS >= 3000
		SIF MARK:ARG:苦痛刻印 < 1
			RETURNF 1
	ENDSELECT
	RETURNF 0
CASE "反抗刻印獲得"
	SIF TEQUIP:ARG:逆推 || TCVAR:ARG:28 > 10 || SELECTCOM == 280
		RETURNF 0
	LOCAL = CUP:ARG:反感
	SELECTCASE MARK:ARG:苦痛刻印
	CASE 1
		LOCAL -= 500
	CASE 2
		LOCAL -= 1500
	CASE 3
		LOCAL -= 4000
	ENDSELECT
	SELECTCASE MARK:ARG:快乐刻印
	CASE 1
		LOCAL -= 100
	CASE 2
		LOCAL -= 500
	CASE 3
		LOCAL -= 1000
	ENDSELECT
	SELECTCASE MARK:ARG:屈服刻印
	CASE 1
		LOCAL -= 500
	CASE 2
		LOCAL -= 1000
	CASE 3
		LOCAL -= 2000
	ENDSELECT
	SELECTCASE LOCAL
	CASE IS >= 10000
		SIF MARK:ARG:反抗刻印 < 3
			RETURNF 3
	CASE IS >= 3000
		SIF MARK:ARG:反抗刻印 < 2
			RETURNF 2
	CASE IS >= 500
		SIF MARK:ARG:反抗刻印 < 1
			RETURNF 1
	ENDSELECT
	RETURNF 0
CASE "経験值獲得"
	;今回高潮＆射精させた
	IF ARG == TARGET
		;执行了什么
		SIF PREVCOM >= 0
			LOCAL += 1
		SIF EXP_GAIN(2, ARG) + EXP_GAIN(3, ARG)
			LOCAL += (EXP_GAIN(2, ARG) + EXP_GAIN(3, ARG) ) / 5 + 1
		;性欲大满足（探索で之経験BＯＮＵSもこれ）
		IF BASE:性欲 >= MAXBASE:性欲*2
			LOCAL += MAX(MAXBASE:性欲, 10)
		;性欲满足
		ELSEIF BASE:性欲 >= MAXBASE:性欲
			LOCAL += MAX(MAXBASE:性欲/2, 5)
		ENDIF
	ENDIF
	;屈服刻印
	IF MARK:ARG:屈服刻印 && GETBIT(CFLAG:ARG:33, 0) == 0
		LOCAL += 30
		SETBIT CFLAG:ARG:33, 0
	ENDIF
	IF MARK:ARG:屈服刻印 >= 2 && GETBIT(CFLAG:ARG:33, 1) == 0
		LOCAL += 30
		SETBIT CFLAG:ARG:33, 1
	ENDIF
	IF MARK:ARG:屈服刻印 == 3 && GETBIT(CFLAG:ARG:33, 2) == 0
		LOCAL += 50
		SETBIT CFLAG:ARG:33, 2
	ENDIF
	;恋慕淫乱
	IF TALENT:ARG:淫乱 && GETBIT(CFLAG:ARG:33, 3) == 0
		LOCAL += 60
		SETBIT CFLAG:ARG:33, 3
	ENDIF
	IF TALENT:ARG:恋慕 && GETBIT(CFLAG:ARG:33, 4) == 0
		LOCAL += 60
		SETBIT CFLAG:ARG:33, 4
	ENDIF

	;给予的绝顶経験or射精経験中更大的值
	LOCAL:1 = EXP:ARG:绝顶経験 - CSVEXP(NO:ARG, 2, 0)
	LOCAL:2 = MAX(EXP:ARG:射精経験 - CSVEXP(NO:ARG, 3, 0), LOCAL:1)

	;绝顶or射精経験
	IF LOCAL:2 > 0 && GETBIT(CFLAG:ARG:33, 5) == 0
		LOCAL += 10
		SETBIT CFLAG:ARG:33, 5
	ENDIF
	IF LOCAL:2 >= 50 && GETBIT(CFLAG:ARG:33, 6) == 0
		LOCAL += 20
		SETBIT CFLAG:ARG:33, 6
	ENDIF
	IF LOCAL:2 >= 200 && GETBIT(CFLAG:ARG:33, 7) == 0
		LOCAL += 40
		SETBIT CFLAG:ARG:33, 7
	ENDIF

	;淫穴など
	IF TALENT:ARG:淫穴+TALENT:ARG:尻穴狂+TALENT:ARG:淫乳+TALENT:ARG:淫核 && GETBIT(CFLAG:ARG:33, 8) == 0
		LOCAL += 30
		SETBIT CFLAG:ARG:33, 8
	ENDIF

	;装饰品效果（取得経験值）
	; SIF TALENT:MASTER:取得経験值率 > 0
	; 	LOCAL = MULTIPLY(LOCAL, 100 + TALENT:MASTER:取得経験值率)

	;EASYモード
	SIF CONFIG("EASY")
		TIMES LOCAL, 2.00
CASE "性别男特有之素質"
	SELECTCASE ARG
	CASE 58, 65, 98, 133, 134
		RETURNF 1
	CASEELSE
		RETURNF 0
	ENDSELECT
CASE "性别女特有之素質"
	SELECTCASE ARG
	CASE 0, 44, 74, 103, 104, 109, 110, 130, 135, 137, 197, 221, 225
		RETURNF 1
	CASEELSE
		RETURNF 0
	ENDSELECT
CASE "逆之素質"
;互为逆反的素质
	SELECTCASE ARG
	CASE 10
		RETURNF 12
	CASE 12
		RETURNF 10
	CASE 11
		RETURNF 63
	CASE 63
		RETURNF 11
	CASE 13
		RETURNF 16
	CASE 16
		RETURNF 13
	CASE 14
		RETURNF 18
	CASE 18
		RETURNF 14
	CASE 15
		RETURNF 17
	CASE 17
		RETURNF 15
	CASE 20
		RETURNF 28
	CASE 28
		RETURNF 20
	CASE 23
		RETURNF 24
	CASE 24
		RETURNF 23
	CASE 25
		RETURNF 26
	CASE 26
		RETURNF 25
	CASE 27
		RETURNF 38
	CASE 38
		RETURNF 27
	CASE 30
		RETURNF 31
	CASE 31
		RETURNF 30
	CASE 32
		RETURNF 33
	CASE 33
		RETURNF 32
	CASE 35
		RETURNF 36
	CASE 36
		RETURNF 35
	CASE 40
		RETURNF 41
	CASE 41
		RETURNF 40
	CASE 42
		RETURNF 43
	CASE 43
		RETURNF 42
	CASE 46
		RETURNF 47
	CASE 47
		RETURNF 46
	CASE 50
		RETURNF 51
	CASE 51
		RETURNF 50
	CASE 58
		RETURNF 133
	CASE 133
		RETURNF 58
	CASE 61
		RETURNF 62
	CASE 62
		RETURNF 61
	CASE 70
		RETURNF 71
	CASE 71
		RETURNF 70
	CASE 81
		RETURNF 82
	CASE 82
		RETURNF 81
	CASE 96
		RETURNF 97
	CASE 97
		RETURNF 96
	CASE 99
		RETURNF 100
	CASE 100
		RETURNF 99
	CASE 101
		RETURNF 102
	CASE 102
		RETURNF 101
	CASE 103
		RETURNF 104
	CASE 104
		RETURNF 103
	CASE 105
		RETURNF 106
	CASE 106
		RETURNF 105
	CASE 107
		RETURNF 108
	CASE 108
		RETURNF 107
	CASE 109
		RETURNF 110
	CASE 110
		RETURNF 109
	CASE 111
		RETURNF 112
	CASE 112
		RETURNF 111
	CASE 115
		RETURNF 116
	CASE 116
		RETURNF 115
	ENDSELECT
	RETURNF 0
CASE "性格素質"
	;性格相关返回1，非相关返回0
	;胆怯,反抗心,刚强,坦率,内向,傲慢,自大,低姿态,强硬,坏心眼,冷静,不关心,缺乏情感
	;好奇心,保守,开朗,阴郁,喜欢受人注目,怕羞,不知耻,纯情,清纯,献身,好色,小恶魔
	SELECTCASE ARG
		CASE 10 TO 26, 28, 35, 36, 38, 39, 63, 75, 87
			RETURNF 1
	ENDSELECT
	RETURNF 0
ENDSELECT
RETURNF LOCAL

