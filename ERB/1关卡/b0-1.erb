@B01
#DIM CONST HEIGHT=2
#DIM CONST WEIGHT=7
#DIM DYNAMIC HEALTH=30
#DIM COUNT2
#DIM COUNT3
#DIM STACK
#DIM POINTER
#DIM BLOCK
#DIM ENEMYLEFT
;=================
;initialize
;=================
ALIGNMENT CENTER
PRINTW 关卡0-1
PRINTL 教学关卡
ALIGNMENT LEFT
PRINTL 敌人每回合将会从左往右移动一个，部署干员阻挡他们
PRINTW 请将夜刀部署至10号位
FOR COUNT, 0, CHARANUM
    BASE:COUNT:部署位置=-1;
    BASE:COUNT:剩余生命值=BASE:COUNT:生命值
    BASE:COUNT:剩余部署时间=0
    BASE:COUNT:技能剩余时间=0
    BASE:COUNT:技力=0
NEXT
FOR COUNT2,0,20
    FOR STACK,0,20
        FOR COUNT,0,20
            ENEMY:COUNT2:COUNT:STACK=0
            ENEMYH:COUNT2:COUNT:STACK=0            
        NEXT
    NEXT
NEXT
ENEMYLEFT=3
ROUND=0
COST=7
CALL SELECTSQUAD
$ROUNDSTART
ROUND++
COST+=2
;================
;spawn enemy
;================
COUNT=0
SELECTCASE ROUND
    CASE 2
        WHILE ENEMY:1:6:COUNT!=0
            COUNT++
        WEND
        ENEMY:1:6:COUNT=1
        ENEMYH:1:6:COUNT=ENEMY_HP:1
    CASE 3
        WHILE ENEMY:1:6:COUNT!=0
            COUNT++
        WEND
        ENEMY:1:6:COUNT=1
        ENEMYH:1:6:COUNT=ENEMY_HP:1
    CASE 4
        WHILE ENEMY:1:6:COUNT!=0
            COUNT++
        WEND
        ENEMY:1:6:COUNT=1
        ENEMYH:1:6:COUNT=ENEMY_HP:1
ENDSELECT
$INPUT_LOOP
;================
;drawmap
;================
CLEARLINE 100
PRINTFORML 剩余生命值{HEALTH}剩余敌人数{ENEMYLEFT}部署费用{COST}
DRAWLINE 
FOR COUNT2,0,HEIGHT
    ;render operator
    FOR COUNT,0,WEIGHT
        CALL GETOPERATOR_B01(COUNT,COUNT2)
        IF RESULT==-1
            PRINTFORMLC [{COUNT+COUNT2*WEIGHT}]可部署           
        ELSE
            PRINTFORMLC [{COUNT+COUNT2*WEIGHT}]%CALLNAME:RESULT%%"("%{BASE:RESULT:剩余生命值}%")"%
        ENDIF
    NEXT
    PRINTL
    ;render enemies
    FOR STACK,0,20
        COUNT3=0
        REPEAT WEIGHT
        SIF ENEMY:COUNT2:COUNT:STACK
            COUNT3++
        REND
        IF COUNT3>0||STACK==0
            FOR COUNT,0,WEIGHT
                IF ENEMY:COUNT2:COUNT:STACK==0
                    PRINTLC    
                ELSE
                    PRINTFORMLC %CSTRNAME:(ENEMY:COUNT2:COUNT:STACK)%{ENEMYH:COUNT2:COUNT:STACK}HP
                ENDIF
            NEXT 
            PRINTL
        ENDIF
    NEXT
    DRAWLINE 
NEXT
PRINTL
;================
;drawmenu
;================
PRINT [900]下一步

INPUT
POINTER=RESULT
IF RESULT<HEIGHT*WEIGHT
    CALL GETOPERATOR_B01(POINTER%WEIGHT,POINTER/WEIGHT)
    IF RESULT==-1;
        CALL SELECTOP(1)
        IF RESULT!=999
        IF COST>=BASE:RESULT:部署费用
            PRINTFORML %CALLNAME:RESULT%部署至{POINTER}
            PRINTW
            BASE:RESULT:部署位置=POINTER
            BASE:RESULT:技能剩余时间=0
            COST-=BASE:RESULT:部署费用
        ELSE
            PRINTW 费用不足
        ENDIF
        ENDIF
    ELSE
        CALL CONFOP(POINTER%WEIGHT,POINTER/WEIGHT,RESULT)
    ENDIF
ELSE
    SIF RESULT==900
        GOTO ROUNDCAL
ENDIF
GOTO INPUT_LOOP

$ROUNDCAL
;=================
;operator action
;=================
FOR COUNT2,0,HEIGHT
    FOR COUNT,0,WEIGHT
        CALL GETOPERATOR_B01(COUNT,COUNT2)
        LOCAL=RESULT
        IF LOCAL!=-1
            BASE:LOCAL:技力++
            SIF BASE:LOCAL:技能剩余时间>0
            BASE:LOCAL:技能剩余时间--            
            LOCAL=NO:LOCAL
            CALLFORM ATTACK_{LOCAL}(COUNT2,COUNT,"B01")   
            CALLFORM SKILL_{LOCAL}(COUNT2,COUNT,0)     
        ENDIF
    NEXT
NEXT

;=================
;enemy action
;=================
FOR COUNT2,0,HEIGHT
    FOR STACK,0,20
        FOR COUNT,0,WEIGHT
            IF ENEMY:COUNT2:COUNT:STACK!=0
                ;health
                IF ENEMYH:COUNT2:COUNT:STACK<=0
                    PRINTFORML %CSTRNAME:(ENEMY:COUNT2:COUNT:STACK)%倒下了
                    PRINTW
                    ENEMYLEFT--            
                    ENEMY:COUNT2:COUNT:STACK=0
                    ENEMYH:COUNT2:COUNT:STACK=0
                    CONTINUE
                ENDIF 
                ;attack
                CALL ENEMY_ATTACK("B01",COUNT,COUNT2,ENEMY:COUNT2:COUNT:STACK)
                FOR  COUNT3,1,CHARANUM
                    SIF BASE:COUNT3:剩余部署时间>0
                        BASE:COUNT3:剩余部署时间--                ;剩余部署时间减少
                    IF BASE:COUNT3:剩余生命值<=0
                        PRINTFORMW %CALLNAME:COUNT3%倒下了
                        PRINTW
                        BASE:COUNT3:剩余部署时间=BASE:COUNT3:再部署时间
                        BASE:COUNT3:部署位置=-1
                        BASE:COUNT3:剩余生命值=BASE:COUNT3:生命值
                        CALLFORM SKILL_{NO:COUNT3}(COUNT2,COUNT,2) ;DEACTIVE SKILL
                    ENDIF
                NEXT
                ;cal block number
                CALL GETOPERATOR_B01(COUNT,COUNT2)
                BLOCK=0
                FOR COUNT3,0,20
                    SIF ENEMY:COUNT2:COUNT:COUNT3!=0
                    BLOCK++
                NEXT
                IF RESULT==-1||BASE:RESULT:阻挡数<BLOCK ;FORWARD
                    IF COUNT==0 ;LOSE ONE
                        PRINTFORML 一个%CSTRNAME:(ENEMY:COUNT2:COUNT:STACK)%进入了蓝门
                        HEALTH-=1
                        ENEMYLEFT--
                        IF HEALTH>0
                            PRINTFORML 你手撕了%CSTRNAME:(ENEMY:COUNT2:COUNT:STACK)%，你还能手撕{HEALTH}次
                            ENEMY:COUNT2:COUNT:STACK=0
                            ENEMYH:COUNT2:COUNT:STACK=0
                            IF ENEMY:COUNT2:COUNT:(STACK+1)!=0
                                ENEMY:COUNT2:COUNT:STACK=ENEMY:COUNT2:COUNT:(STACK+1)
                                ENEMYH:COUNT2:COUNT:STACK=ENEMYH:COUNT2:COUNT:(STACK+1)
                                ENEMY:COUNT2:COUNT:(STACK+1)=0
                                ENEMYH:COUNT2:COUNT:(STACK+1)=0
                            ENDIF                            
                            PRINTW
                        ELSE
                            GOTO LOOSE   
                        ENDIF 
                    ELSE
                        COUNT3=0
                        WHILE ENEMY:COUNT2:(COUNT-1):COUNT3!=0
                            COUNT3++
                        WEND
                        ENEMY:COUNT2:(COUNT-1):COUNT3=ENEMY:COUNT2:COUNT:STACK
                        ENEMYH:COUNT2:(COUNT-1):COUNT3=ENEMYH:COUNT2:COUNT:STACK
                        ENEMY:COUNT2:COUNT:STACK=0
                        ENEMYH:COUNT2:COUNT:STACK=0
                    ENDIF
                ENDIF
                IF STACK>0&&ENEMY:COUNT2:COUNT:(STACK-1)==0
                    ENEMY:COUNT2:COUNT:(STACK-1)=ENEMY:COUNT2:COUNT:STACK
                    ENEMYH:COUNT2:COUNT:(STACK-1)=ENEMYH:COUNT2:COUNT:STACK                       
                    ENEMY:COUNT2:COUNT:STACK=0
                    ENEMYH:COUNT2:COUNT:STACK=0
                ENDIF
            ENDIF
        NEXT
    NEXT
NEXT
SIF ENEMYLEFT==0
        GOTO  VICTORY  
GOTO ROUNDSTART

$LOOSE
CALL DEACTIVE_SKILL
SETCOLOR 0xED1C24
ALIGNMENT CENTER
PRINTW 作战失败
RESETCOLOR
ALIGNMENT LEFT
RETURN

$VICTORY
CALL DEACTIVE_SKILL
SETCOLOR 0x62ED47
ALIGNMENT CENTER
PRINTW 作战成功
MONEY+=750
PRINTFORMW 获得龙门币750
IF LEVEL_A==0
    MONEY:1+=1
    PRINTW 获得源石1
ENDIF
ALIGNMENT LEFT
SIF LEVEL_A<1
LEVEL_A=1
RESETCOLOR
RETURN

@GETOPERATOR_B01(ARG:0,ARG:1)
#DIM COUNT3
#DIM POS 
POS=ARG:0+ARG:1*7
SIF POS<0
    RETURN -1
FOR COUNT3, 0, CHARANUM
    SIF BASE:COUNT3:部署位置==POS
        RETURN COUNT3
NEXT
RETURN -1